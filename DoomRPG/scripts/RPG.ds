#include "RPG.dh"

#include "Arena.dh"
#include "Augs.dh"
#include "GUI.dh"
#include "HealthBars.dh"
#include "HUD.dh"
#include "ItemData.dh"
#include "Map.dh"
#include "Menu.dh"
#include "Mission.dh"
#include "Monsters.dh"
#include "Outpost.dh"
#include "Popoffs.dh"
#include "Shield.dh"
#include "Shop.dh"
#include "Skills.dh"
#include "Stats.dh"
#include "Stims.dh"
#include "Turret.dh"
#include "Utils.dh"

// Version
const str Version = "v0.9.14 Beta - Pew Pew Turrets";

// Flags
static bool Transported;
static bool GlobalsInitialized;
static int CompatMode;
    
// Arrays
static str[MAX_PLAYERS] PlayerWeapon;

// Structs
static PlayerData[MAX_PLAYERS] _PlayerData;

// Items
bool ItemsInitialized;
int[MAX_ITEMS] ItemTIDs;

// --------------------------------------------------
// Multiplayer HUD
// 

// HUD Item Counts
GlobalArray(18) int[MAX_PLAYERS] Credits @ 0;

// Energy Points (EP)
GlobalArray(2)  int[MAX_PLAYERS] EP @ 0;

// Shield
GlobalArray(50) int[MAX_PLAYERS] Shield @ 0;
GlobalArray(51) int[MAX_PLAYERS] ShieldCapacity @ 0;
GlobalArray(52) int[MAX_PLAYERS] ShieldHealth @ 0;

script void GlobalInit() open
{
    if (!GlobalsInitialized)
    {
        // Version Info
        Log("\cnDoom RPG %s (Compiled on %s at %s) loaded!\n", Version, __DATE__, __TIME__);
        
        // Compatibility checking
        CheckCompatibility();
        
        // Get the XP Curve from the CVAR
        XPCurve = GetCVar("drpg_xp_curve");
        
        // Initialize XP, Stat and Rank Tables
        InitXPTable();
        Delay(1);
        InitStatXPTable();
        Delay(1);
        InitNegativeStatXPTable();
        Delay(1);
        InitRankTable();
        
        // Arena Wave
        ArenaMaxWave = 1;
        
        GlobalsInitialized = true;
        
        Delay(1); // Allow CVARs to update
    };
    
    // Create Translations
    CreateTranslations();
    
    // Initial build of ItemData
    BuildItemData();
        
    // Initial build of SkillData
    BuildSkillData();
    
    // Initial build of TurretUpgradeData
    BuildTurretData();
    
    // Initialize the Item handling script
    ItemHandler();
    
    // Initialize the Dynamic Loot Generator
    if (GetCVar("drpg_loot_system") && !CurrentLevel->UACBase)
        DynamicLootGenerator("DRPGGenericDropper");
};

// Init Script
script void Init() enter
{
    // Wait until globals are initialized
    while (!GlobalsInitialized) Delay(1);
    
    // [KS] This needs to be done on its' own, so death exits don't rob you of your TID and break EVERYTHING EVER FFS
    if (ActivatorTID() == 0)
    {
        AssignTIDs();
        if (!InTitle) // Don't give equipment on the title screen
            DefaultLoadout();
    };
    
    if (!Player.FirstRun)
    {
        // Funds
        GiveInventory("DRPGCredits", GetActivatorCVar("drpg_start_credits"));
        Player.PrevCredits = CheckInventory("DRPGCredits");
        GiveInventory("DRPGModule", GetActivatorCVar("drpg_start_modules"));
        GiveInventory("DRPGTurretPart", GetActivatorCVar("drpg_start_turretparts"));
        
        // Level/Rank
        if (GetCVar("drpg_start_level") > 0)
        {
            Player.Level = GetActivatorCVar("drpg_start_level");
            Player.XP = XPTable[Player.Level - 1];
        };
        if (GetCVar("drpg_start_rank") > 0)
        {
            Player.RankLevel = GetActivatorCVar("drpg_start_rank");
            Player.Rank = RankTable[Player.RankLevel - 1];
        };
        
        // Stats
        Player.Strength = GetActivatorCVar("drpg_start_strength");
        Player.Defense = GetActivatorCVar("drpg_start_defense");
        Player.Vitality = GetActivatorCVar("drpg_start_vitality");
        Player.PrevVitality = Player.Vitality;
        Player.Energy = GetActivatorCVar("drpg_start_energy");
        Player.PrevEnergy = Player.Energy;
        Player.Regeneration = GetActivatorCVar("drpg_start_regeneration");
        Player.Agility = GetActivatorCVar("drpg_start_agility");
        Player.Capacity = GetActivatorCVar("drpg_start_capacity");
        Player.Luck = GetActivatorCVar("drpg_start_luck");
        
        // Stat XP
        Player.StrengthXP = StatTable[Player.Strength - 1];
        Player.DefenseXP = StatTable[Player.Defense - 1];
        Player.VitalityXP = StatTable[Player.Vitality - 1];
        Player.EnergyXP = StatTable[Player.Energy - 1];
        Player.RegenerationXP = StatTable[Player.Regeneration - 1];
        Player.AgilityXP = StatTable[Player.Agility - 1];
        Player.CapacityXP = StatTable[Player.Capacity - 1];
        Player.LuckXP = StatTable[Player.Luck - 1];
        
        // Default Health/EP
        Player.EP = Player.Energy * 10;
        Player.ActualHealth = Player.Vitality * 10;
        Player.PrevHealth = Player.ActualHealth;
        SetActorProperty(0, APROP_Health, Player.ActualHealth);
        
        // Setup HUD stuff
        Player.StatusTypeHUD = SE_NONE;

        for (int i = 0; i < MAPEVENT_MAX; i++)
            Player.SeenEventTip[i] = false;
        
        // Setup the Skill Wheel from the persistence CVARs
        for (int i = 0; i < MAX_SKILLKEYS; i++)
        {
            Player.SkillCategory[i] = GetActivatorCVar(StrParam("drpg_skillwheel_category_%d\n", i + 1));
            Player.SkillIndex[i] = GetActivatorCVar(StrParam("drpg_skillwheel_index_%d\n", i + 1));
        };
        
        // Transport should always be known
        Player.SkillLevel[MAX_CATEGORIES - 1][SkillCategoryMax[MAX_CATEGORIES - 1] - 1].Level = 1;
        Player.SkillLevel[MAX_CATEGORIES - 1][SkillCategoryMax[MAX_CATEGORIES - 1] - 1].CurrentLevel = 1;
        
        // Set default selected skill to nothing
        Player.SkillSelected = -1;
        
        // Fill Augmentation Battery
        Player.Augs.Battery = Player.Capacity * 10;
        
        // Setup the New! shield parts arrays
        for (int i = 0; i < SHIELDPAGE_MAX; i++)
            for (int j = 0; j < MAX_PARTS; j++)
                Player.NewShieldParts[i][j] = true;
        for (int i = 0; i < MAX_ACCESSORIES; i++)
            Player.NewShieldAccessoryParts[i] = true;
        
        // Done first run
        Player.FirstRun = true;
    };
    
    // Death exit handling
    if (Player.ActualHealth <= 0)
        Player.ActualHealth = Player.HealthMax;
    
    // Apply camera textures and vars
    SetCameraToTexture(Player.TID, StrParam("P%iVIEW\n", PlayerNumber() + 1), 110);
    SetCameraToTexture(Player.TID, StrParam("P%iSVIEW\n", PlayerNumber() + 1), 90);
    Player.PlayerView = PlayerNumber();
    
    // Remove Aura if the keep Aura CVAR is off
    if (!GetCVar("drpg_skill_keepauras"))
        RemoveAura();
    
    // Transport FX
    if (Transported)
    {
        for (int i = 0; i < MAX_PLAYERS; i++)
        {
            // Player is not in-game
            if (!PlayerInGame(i)) continue;
            
            TransportInFX(Players(i).TID);
        };
        
        SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN); // In case we were in the menu
        Transported = false;
    };
    
    // Clear the Player's summons
    for (int i = 0; i < MAX_SUMMONS; i++)
        Player.SummonTID[i] = 0;
    Player.Summons = 0;
    
    // Initialize DropTID Array
    ArrayCreate(&Player.DropTID, "DRPS\x00", 64, sizeof(int));
    
    // Mission Initialization
    InitMission();
    
    // Small delay before initializing the scripts for mod compatibility
    Delay(4);
    
    // Reassignment is necessary for some mods
    if (CompatMode == COMPAT_DRLA)
        AssignTIDs();
    
    // Execute Game Loops
    Loop();
    PlayerDamage();
    ShieldTimer();
    WeaponSpeed();
    StatRandomizer();
    DamageNumbers();
    InfoPopoffs();
    HealthBars();
    AutosaveHandler();
    ShopItemAutoHandler();
    
    // GUI
	CheckGUI();
	CheckCursor();
    // UpdateMenu();
};

// Loop Script
script void Loop()
{
    Start:
    
    // If we're on the title map, terminate
    if (InTitle)
    {
        SetActorProperty(0, APROP_Invulnerable, true);
        TeleportOther(Player.TID, 1003, false);
        return;
    };
    
    // If the player's dead, terminate
    if (GetActorProperty(0, APROP_Health) <= 0) return;
    
    // Update Functions
    CheckCombo();
    CheckStats();
    CheckStatBonus();
    CheckHardStatCaps();
    CheckRegen();
    CheckLuck();
    CheckHealth();
    CheckAugSlots();
    CheckAugs();
    CheckLevel();
    CheckRank();
    CheckSkills();
    CheckAuras();
    CheckBurnout();
    CheckStim();
    CheckPerks();
    CheckShields();
    CheckShieldAccessory();
    CheckStatBounds();
    CheckToxicity();
    CheckStatusEffects();
    CheckCapacity();
    CheckShopCard();
    CheckMission();
    CheckHUD();
    CheckKeys();
    
    // Handle the menu cursor color
    MenuCursorColor = CursorColors[(Timer() / 3) % 6];
    
    // Calculate Shop Discount
    Player.ShopDiscount = (int)((Player.RankLevel * 2.1) + (CurrentLevel->UACBase ? (Player.ShopCard * 5) : 0));
    
    // Main Menu
    if (Player.InMenu)
        MenuLoop();
    
    // Shop Menu
    if (Player.InShop)
        ShopLoop();
    
    // Menu freezing/dimming
    if (Player.InMenu || Player.InShop || Player.OutpostMenu > 0)
    {
        if (GetCVar("drpg_menufreeze") && !InMultiplayer)
            GiveInventory("DRPGMenuFreezer", 1);
        if (GetCVar("drpg_menudim") && !Player.StatusType[SE_BLIND])
            FadeRange(0, 0, 0, 0.5, 0, 0, 0, 0.0, 0.25);
    };
    
    // Menu-specific Help
    if (Player.InMenu || Player.InShop || Player.OutpostMenu > 0 || Player.CrateOpen)
        MenuHelp();
    
    // Menu icon in multiplayer
    if (InMultiplayer && (Player.InMenu || Player.InShop || Player.OutpostMenu > 0))
    {
        fixed X = GetActorX(Player.TID);
        fixed Y = GetActorY(Player.TID);
        fixed Z = GetActorZ(Player.TID);
        fixed Height = GetActorPropertyFixed(Player.TID, APROP_Height);
        
        SpawnForced("DRPGMenuIcon", X, Y, Z + Height + 8.0, 0, 0);
    };
    
    // Regeneration
    DoRegen();
    
    // Set mass stupid high when Invulnerable or transporting to prevent knockback
    if (CheckInventory("PowerInvulnerable") || Transported)
        Player.Mass *= 128;
    
    // Apply Stats
    SetActorPropertyFixed(Player.TID, APROP_DamageMultiplier, (1.0 + ((fixed)Player.TotalDamage / 100.0)) * (Player.DamageMult > 1 ? Player.DamageMult : 1));
    SetActorPropertyFixed(Player.TID, APROP_DamageFactor, Player.DamageFactor);
    SetActorProperty(Player.TID, APROP_Mass, Player.Mass);
    SetActorProperty(Player.TID, APROP_SpawnHealth, Player.HealthMax);
    SetActorPropertyFixed(Player.TID, APROP_Speed, Player.Speed);
    SetActorPropertyFixed(Player.TID, APROP_JumpZ, Player.JumpHeight);
    
    // Make you invincible while in the Menus and if menu freezing is enabled
    if ((Player.InMenu || Player.InShop) && GetCVar("drpg_menufreeze"))
        SetActorProperty(Player.TID, APROP_DamageFactor, 0);
    
    // Store your current weapon in the Player Weapon array
    PlayerWeapon[PlayerNumber()] = GetWeapon();
    
    // Loop
    Delay(1);
    
    goto Start;
};

// Damage Handler Entry Point
script void PlayerDamage()
{
    bool CanSurvive;
    bool Critical;
    int BeforeDamage;
    int DamageTaken;
    int ShieldDamageAmount;
    int MonsterID;
    fixed LuckChance;
    fixed EnergyLevel;
    
    Start:
    
    // If the player's dead, terminate
    if (GetActorProperty(0, APROP_Health) <= 0) return;
    
    CanSurvive = Player.SurvivalBonus > 0 && RandomFixed(0.0, 100.0) <= Player.SurvivalBonus && !Player.LastLegs;
    
    if (Player.ActualHealth <= 0) // [KS] This is to make sure the player actually dies, even if they're shielded.
    {
        SetPlayerProperty(0, 0, PROP_BUDDHA);
        SetActorProperty(0, APROP_Health, 0);
        goto Start;
    };
    
    if (!Player.Shield.Active || !Player.Shield.Charge)
    {
        SetActorProperty(0, APROP_Health, Player.ActualHealth);
        
        if (Player.ActualHealth > 2)
            Player.LastLegs = false;
        
        if (CanSurvive || CheckInventory("DRPGContinue"))
            SetPlayerProperty(0, 1, PROP_BUDDHA)
        else
            SetPlayerProperty(0, 0, PROP_BUDDHA);
    }
    else
    {
        SetPlayerProperty(0, 0, PROP_BUDDHA);
        
        SetActorProperty(0, APROP_Health, SHIELD_HEALTH);
        
        // Hilarious hack to allow healing detection
        SetActorProperty(0, APROP_SpawnHealth, Player.ActualHealth < Player.HealthMax ? SHIELD_HEALTH * 2 : SHIELD_HEALTH);
    };
    
    Player.DamageType = DT_NONE;
    BeforeDamage = GetActorProperty(Player.TID, APROP_Health);
    
    Delay(1);
    
    // Get attacker's ID
    Player.DamageTID = WhoShotMe();
    
    DamageTaken = BeforeDamage - GetActorProperty(Player.TID, APROP_Health);
    ShieldDamageAmount = 0;
    
    if (DamageTaken > 0)
    {
        Player.AutosaveTimerReset = true;
        
        // Find Monster ID
        MonsterID = FindMonster(Player.DamageTID);
        
        // Calculate monster crit/status chance
        LuckChance = (fixed)Monsters[MonsterID].Luck / 20.0;
        EnergyLevel = (fixed)Monsters[MonsterID].Energy / 12.5;
        
        // Calculate a critical hit
        if (Player.DamageTID > 0 && MonsterID > 0)
            if (RandomFixed(0.0, 100.0) <= EnergyLevel)
            {
                DamageTaken *= 2;
                Critical = true;
            };
        
        AugDamage(DamageTaken);
        ToxicityDamage();
        if (MonsterID > 0)
            StatusDamage(DamageTaken, LuckChance, Critical)
        else
            StatusDamage(DamageTaken, RandomFixed(0.0, 100.0), Critical);
        ResetRegen();
        DamageHUD(DamageTaken, Critical);
    };
    
    // Reset attacker's ID and critical damage state
    Player.DamageTID = -1;
    Critical = false;
    
    if (DamageTaken > 0 && Player.Shield.Active)
        ShieldTimerReset();
    
    if (DamageTaken > 0 && Player.Shield.Active && Player.Shield.Charge > 0)
    {
        ShieldDamageAmount = DamageTaken; // For callback
        if (ShieldDamageAmount > Player.Shield.Charge)
            ShieldDamageAmount = Player.Shield.Charge;
        
        Player.Shield.Charge -= DamageTaken;
        Player.Shield.Full = false;
        
        ShieldDamage(ShieldDamageAmount);
        
        if (Player.Shield.Charge <= 0)
        {
            if (Player.Shield.Charge < 0)
            {
                DamageTaken = -Player.Shield.Charge;
                Player.ActualHealth -= DamageTaken;
                Player.Shield.Charge = 0;
            }
            else
                DamageTaken = 0;
            
            ShieldBroken();
        }
        else
            DamageTaken = 0;
    };
    
    if (DamageTaken > 0)
    {
        Player.ActualHealth -= DamageTaken;
        
        if (Player.ActualHealth <= 1) // Near-Death stuff
        {
            // Continue check
            if (CheckInventory("DRPGContinue"))
            {
                Player.ActualHealth = Player.HealthMax;
                ActivatorSound("health/resurrect", 127);
                TakeInventory("DRPGContinue", 1);
                
                SetHudSize(320, 200, false);
                PrintSpriteFade("CONTA0", 0, 172.0, 160.0, 0.5, 1.5);
                SetFont("BIGFONT");
                HudMessage("Used a Continue!\n", HUDMSG_FADEOUT, 0, CR_GOLD, 160.0, 140.0, 0.5, 1.5);
            };
            
            // Survival Bonus
            if (CanSurvive && Player.ActualHealth <= 1)
            {
                Player.ActualHealth = 2;
                Player.LastLegs = true;
                
                if (Player.Shield.Accessory && Player.Shield.Accessory->PassiveEffect == SHIELD_PASS_SURVIVECHARGE)
                    Player.Shield.Charge = Player.Shield.Capacity;
                
                ActivatorSound("health/survive", 127);
                SetHudSize(320, 200, false);
                SetFont("BIGFONT");
                HudMessage("Agility Save!\n", HUDMSG_FADEOUT, 0, CR_ORANGE, 160.0, 140.0, 0.5, 0.5);
                PrintSpriteFade("AGISAVE", 0, 160.0, 140.0, 0.5, 0.5);
            };
        };
        
        // Always Quick Heal if CVAR is set
        if (GetActivatorCVar("drpg_auto_heal"))
            QuickHeal(false);
    }
    else if (DamageTaken < 0)
    {
        DamageTaken = -DamageTaken;
        
        // Negative "damage" == We got health from an outside source (eg. RLA assembly unit)
        // We'll just need to assume it's a regular capped heal if we got it this way.
        // Anything that needs otherwise should be redefined anyway.
        
        if (Player.ActualHealth + DamageTaken > Player.HealthMax)
            DamageTaken = Player.HealthMax - Player.ActualHealth;
        
        Player.ActualHealth += DamageTaken;
    };
    
    goto Start;
};

acscript int AddHealth(int HealthPercent, int MaxPercent)
{
    int RealMax = Player.HealthMax * MaxPercent / 100;
    int HealthAmount = Player.HealthMax * HealthPercent / 100;
    
    if (Player.ActualHealth >= RealMax)
        return 0;
    
    if (Player.ActualHealth + HealthAmount > RealMax)
        HealthAmount = RealMax - Player.ActualHealth;
    
    // Add Vitality XP for using healing items
    if (GetCVar("drpg_levelup_natural") && !Player.Stim.Active)
    {
        fixed Scale = GetCVarFixed("drpg_vitality_scalexp");
        if (GetCVar("drpg_allow_spec"))
        {
            if (GetActivatorCVar("drpg_character_spec") == 3)
                Scale *= 2;
        };
        
        int Factor = CalcPercent(HealthAmount, Player.HealthMax);
        Player.VitalityXP += (int)(Factor * Scale * 10);
    };
    
    Player.ActualHealth += HealthAmount;
    return 1;
};

acscript int AddHealthDirect(int HealthAmount, int MaxPercent)
{
    int RealMax = Player.HealthMax * MaxPercent / 100;
    
    if (Player.ActualHealth >= RealMax)
        return 0;
    
    if (Player.ActualHealth + HealthAmount > RealMax)
        HealthAmount = RealMax - Player.ActualHealth;
    
    // Add Vitality XP for using healing items
    if (GetCVar("drpg_levelup_natural") && !Player.Stim.Active)
    {
        fixed Scale = GetCVarFixed("drpg_vitality_scalexp");
        if (GetCVar("drpg_allow_spec"))
        {
            if (GetActivatorCVar("drpg_character_spec") == 3)
                Scale *= 2;
        };
        
        int Factor = CalcPercent(HealthAmount, Player.HealthMax);
        Player.VitalityXP += (int)(Factor * Scale * 10);
    };
    
    Player.ActualHealth += HealthAmount;
    return 1;
};

acscript void GiveTip()
{
    if ((CurrentLevel->Event == MAPEVENT_SKILL_HELL && GameSkill() != 5) || (CurrentLevel->Event == MAPEVENT_SKILL_ARMAGEDDON && GameSkill() != 6))
        return;
    
    struct TipInfo
    {
        str Title;
        str Tip;
    };
    
    static TipInfo[MAX_TIPS] Tips =
    {
        // Level/Rank
        { "\cjLevel";                           "Your level determines your basic strength and progress through the game. Your level is increased by killing monsters, completing missions and achieving combos."; };
        { "\ckRank";                            "Your rank determines your standing within the UAC. Rank will determine how much money you are paid by the UAC as well as the discount you will receive when purchasing items from the shop."; };
        { "\ctCombo";                           "The combo system allows you to kill enemies in rapid succession in order to gain bonus XP and Rank. When an enemy is killed, you will gain +1 to your combo counter and your combo timer will reset. When your timer reaches its cooldown point, indicated by the split between the red and green in the bar, it will add the indicated XP and Rank to your totals and calculate a bonus (the green number). If you keep incrementing your combo during the cooldown period, you can keep stacking XP and Rank into the bonus. When the combo timer ends, your combo will be completely reset and your bonus will be added to your totals."; };
        { "\ckPay Bonus";                       "Your pay bonus is shown to the right of your payout information. A higher pay bonus will yield better payouts. Your pay bonus can be increased by completing missions and achieving 100% level completion."; };
        
        // Currencies
        { "\cfCredits";                         "Credits are the universal currency used by the UAC and are used for purchasing goods in the shop."; };
        { "\cdModules";                         "Modules are used to upgrade your stats and skills. They are typically given by level ups, missions and completing level events as well as occasionally being dropped by monsters and found in the world."; };
        { "\ccTurret Parts";                    "Turret parts are used to build and upgrade your turret. They can be found by disassembling broken-down turrets you find in the world."; };
        
        // Stats
        { "\cgStrength";                        "Your Strength stat determines the extra damage you deal with all types of attacks."; };
        { "\cdDefense";                         "Your Defense stat determines your resistance to all forms of damage."; };
        { "\caVitality";                        "Your Vitality stat determines your max health and health regeneration rate."; };
        { "\cnEnergy";                          "Your Energy stat determines your max EP and EP regeneration rate."; };
        { "\ctRegeneration";                    "Your Regeneration stat determines your regeneration rate and the time that Regeneration Spheres last."; };
        { "\ciAgility";                         "Your Agility stat governs your movement, jumping and fire speeds as well as your survival bonus."; };
        { "\chCapacity";                        "Your Capacity stat determines your ammo carrying capacity, how many compounds of each type you can carry for Stims as well as how many inventory items you can hold"; };
        { "\cfLuck";                            "Your Luck stat determines which items can drop as well as the chance which they will drop. Luck also affects several other hidden factors as well."; };
        
        // Stats Extended
        { "\ciSurvival Bonus";                  "Survival Bonus is a special attribute which works as a last stand mechanic. When you take a hit that would kill you and your survival bonus is triggered, you will not be killed by the attack."; };
        { "\cdPerks";                           "Perks are special enhancements that you receive when you upgrade a stat to 100 points or more."; };
        { "\cgStat Cap";                        "You can only increase a stat past 100 up to the maximum of your current level + 100."; };
        
        // Skills
        { "\cnEP";                              "Energy Points are needed in order to use skills. They can also be used to charge your Shield and to deposit and withdraw items from your personal locker while not in the Outpost."; };
        { "\cnOverdrive";                       StrParam("You can Overdrive a skill by holding the \cd%K\c- button. Doing this will always use the skill, regardless of it's EP cost. However, this will bring your current EP into the negatives. When negative, you will suffer stat penalties and not be able to use any skills until your EP is restored past 0.\n", "+speed"); };
        { "\cnAuras";                           "Auras are special passive abilities you can activate to give you temporary boosts to your stats. Auras will also affect teammates within it's radius, determined by your Energy stat"; };
        
        // Augmentations
        { "\ckAugmentations";                   "Augmentations give you permanent bonuses to your stats while they are active."; };
        { "\ckAugmentation Battery";            "Augmentations can only work while your augmentation battery has a charge. Your battery can be recharged by using augmentation batteries from your inventory or by receiving certain types of damage. Battery drain is determined by how many augmentations you have active and how high their levels are. The more augmentation active and the higher their levels, the quicker the battery will drain."; };
        { "\ckAugmentation Slots";              "You can only activate as many augmentations as you have available slots for. To increase the number of slots you have, you must find an augmentation slot upgrade. Augmentation slot upgrades can be bought from the shop, found as rare drops from enemies or found in the world."; };
        
        // Shield
        { "\cvShields";                         "Shields will protect you from taking direct damage by absorbing the damage into their own charge pool. When a shield takes damage, it's charge is directly affected and will be reduced by the damage's amount. When a shield's charge reaches 0, you will begin taking normal damage again."; };
        { "\cvShield Components";               "Shields are built from 4 basic components which you can collect and customize during the game. The body is named after the manufacturer and is generally responsible for modifying the capacity of the Shield, but also evenly modifies the other stats as well. The battery is generally responsible for modifying the charge rate of the shield. The capacitor is generally responsible for modifying the charge rate and delay rate of the shield. Accessories give the shield unique effects and abilities which can be triggered in various ways."; };
        { "\cvShield Stats";                    "Each shield has 4 stats which govern it's behavior and ability. Capacity determines the total amount of charge the shield can hold. Charge Rate determines the amount the charge pool will regenerate when a charge cycle is completed. Delay Rate determines the wait time that occurs when a charge cycle is interrupted before charging will resume again. Charge interval determines the length between charge cycles."; };
        { "\cvShield Charge Cycle";             "A charging cycle occurs based on the Charge Interval (default of 1 second) and increases the Shield's charge pool, determined by the Charge Rate. When the shield is struck, the charging cycle is interrupted and a waiting period, determined by the Delay Rate, must complete before charging cycles will resume again."; };
        
        // Stims
        { "\ccStims";                           "Stims are portable injected temporary stat increases. There are three forms of Stim vials that can be encountered: Stat vials, Booster vials and Powerup vials."; };
        { "\ccStim Injectors";                  "Injectors are what are used to actually store the compounds for usage. Injectors can either be bought in the shop or found in the world. There are 4 different injector sizes, which hold varying amounts of compounds."; };
        { "\ccStim Vials";                      "Vials are the items you pickup which contain the different kinds of compounds. Vials can be bought in the shop or found by killing enemies which have auras or elsewhere in the world."; };
        { "\ccStim Stat Vials";                 "Stat vials increase their various corresponding stats when used."; };
        { "\ccStim Booster Vials";              "Booster vials are special vials which have unique stat-related effects. Purifier vials increase the duration that stat vials will last. Potency vials increase all stats at once."; };
        { "\ccStim Powerup Vials";              "Powerup vials give various special powers for a limited duration. Unlike the Stat stims, they are unaffected by Purifier vials, and their time is affected solely by the amount of vials put into the Stim."; };
        { "\cdToxicity";                        "Toxicity is a measure of the radiation and toxins in your body. Toxicity is increased by taking radiation or toxin related damage, such as standing on damage floors. The higher your toxicity levels get, the more penalties you will suffer. If your toxicity levels reach 100%, you will die. Toxicity will naturally dissipate over time, the speed of which is determined by your Regeneration stat."; };
        
        // Turrets
        { "\cdTurrets";                         "Turrets are portable sentry drones which can be built and equipped with multiple offensive and defensive upgrades and abilities to aid the user. Turrets must initially be built from turret parts, at which point the turret may be used and upgraded further as more parts become available."; };
        { "\cdDestroyed Turrets";               "In the world, you will find destroyed turrets. These turrets can be scavenged for spare turret parts. Some turrets will be in better condition than others, allowing for more parts to be scavenged."; };
        { "\cdTurret Maintenance";              StrParam("Your turret will need routine maintenance performed on it in order to keep it in working condition. There are three components to maintenance: charging, repairing and refitting. Charging will charge the turret's internal battery, allowing it to stay active on the field. Repairing will patch up the turret, restoring it's health. Refitting occurs when upgrades are performed on the turret, and must be allowed to finish before the turret can be used again. In order for maintenance to begin and to continue, a steady supply of Credits will be deducted from your account. To send the turret in for maintenance, in either the turret menu or with the turret command wheel open, press \cd%K\c-.", "+speed"); };
        { "\cdTurret Deployment";               StrParam("To quickly deploy or deactivate your turret, use \cd%K\c- + \cd%K\c-.", "+speed", "+user2"); };
        
        // Monsters
        { "\cgMonster Stats";                   "Like you, monsters also have a level and stats which will affect their abilities in various ways."; };
        { "\cgMonster Auras";                   "Like you, monsters can also have one or multiple auras which will give them special abilities. Each aura a monster has will also double its respective stat."; };
        { "\cgMonster Shadow Auras";            "Monsters which have all auras active at once will become a shadow enemy with extremely high stats and abilities for their level. These enemies will receive names and should be priority targets to take down, as their combined abilities can quickly overwhelm a player if they are not prepared."; };
        { "\cgMonster Regeneration Cycle";      "Like you, monsters will regenerate a portion of their health every so often. The amount of health they will regenerate is determined by their regeneration stat."; };
        { "\cgMonster Threat Level";            "Each monster has a threat level. Their threat level is determined by their level, stats and active auras and ranges from 0 to 10. Threat level can be used as a general way to gauge a monster's abilities and which ones should be prioritized when attacking. The higher a monster's threat level, the higher XP and Rank you will receive for defeating them. Threat level is represented on a monster's health bar by the number of emblems present."; };
        { "\cgMegabosses";                      "Under certain circumstances, you will encounter monsters known as megabosses. These bosses are extremely difficult, having special and unique behaviors which can quickly and effectively destroy you if not prepared. When killed, megabosses will provide a tremendous amount of loot as well as XP and Rank."; };
        
        // Status Effects
        { "\caStatus Effects";                  "Status effects are debuffs which will affect you in different ways. You can get them from taking different types of damage or by exposing yourself to specific types of hazards. When inflicted with a status effect, it will last for a certain amount of time. Each status effect has 5 levels of intensity which are increasingly more detrimental to you. If you are hit with a status effect you already have, it will add to your current timer and potentially increase it's intensity."; };
        { "\ccBlindness";                       "Blindness is a status effect which will darken your view and make it harder to see."; };
        { "\cfConfusion";                       "Confusion is a status effect which will make your vision blurry and make it harder for you to aim and move."; };
        { "\cqPoison";                          "Poison is a status effect which will deal direct health damage to you every second. It will not actually kill you if your health reaches a critical level."; };
        { "\cdCorrosion";                       "Corrosion is a status effect which will deal damage to your armor every second."; };
        { "\ciFatigue";                         "Fatigue is a status effect which will decrease your movement speed and jumping height."; };
        { "\ctVirus";                           "Virus is a status effect which will stop you from regenerating."; };
        { "\cnSilence";                         "Silence is a status effect which prevents you from using skills."; };
        { "\cgCurse";                           "Curse is a status effect which increases the amount of damage you take."; };
        { "\cvEMP";                             "EMP is a status effect which will completely disable your shields and augmentations and not allow you to re-enable them. It will also drain your augmentation battery."; };
        { "\cdRadiation";                       "Radiation is a status effect which slowly increases your toxicity levels. It will not increase your toxicity past 85%."; };
        
        // Outpost/Arena
        { "\cqOutpost";                         "The UAC Outpost is the main hub of operations where you can perform various activities, access various facilities and stock up between levels. You can transport to the Outpost at any time using the Transport skill."; };
        { "\cgArena";                           "In the Arena, you can fight waves of enemies for XP, Rank and loot."; };
        { "\cdLevel Transporter";               "In the Outpost, you can use the level transporter to view stats on all the levels you have visited and to transport back to them at will."; };
        { "\cdSkill Computer";                  "In the Outpost, you can use the skill computer to change the current skill level without the need to restart the game."; };
        { "\cdMinigames";                       "In the Outpost, there are minigames which you can play in order to win items using your gold and platinum chips."; };
        { "\cgPractice Target";                 "In the Outpost, you can shoot the target in order to gauge the amount of damage your weapons are doing."; };
        
        // Missions
        { "\cdMissions";                        "You can accept different missions in the Outpost in order to gain XP, Rank, Credits, Modules and items. There are several different types of missions at varying difficulty levels you can undertake. The available missions will refresh each time you visit the Outpost."; };
        { "\cdCollection Missions";             "You must collect a specific number of a specified item to complete the mission. These items are found scattered around the world."; };
        { "\caKill Missions";                   "You must kill a given amount of enemies of the same type to complete the mission."; };
        { "\caKill Auras Missions";             "You must Kill a given amount of enemies which have at least one aura to complete the mission."; };
        { "\caKill Reinforcements Missions";    "While in the field, monsters will spawn in around you. You must kill a given amount of them to complete the mission."; };
        { "\cgAssassination Missions";          "You must eliminate the specified enemy, which will be located somewhere in the level and will always have a shadow aura to complete the mission."; };
        { "\ckFind Secrets Missions";           "You must find the given amount of unique secrets to complete the mission."; };
        { "\cnFind Items Missions";             "You must find the given amount of items to complete the mission."; };
        { "\ctAchieve Combo Missions";          "You must get your combo to the given amount to complete the mission."; };
        
        // Shop/Locker
        { "\cfShop";                            "The shop is where you can go to purchase and sell the various equipment and items you can find during the game. The shop also contains a locker system, where you may withdraw and deposit items for later usage."; };
        { "\chLocker";                          StrParam("You can access the locker system via the shop and deposit/withdraw your items there. Withdrawing and depositing items has a 1%% cost to your EP per use outside the Outpost. You can switch between the shop and locker using the \cd%K\c- key.\n", "+jump"); };
        { "\cfShop Cards";                      "While playing, you may find UAC cards which provide you with a discount as well as other benefits. Finding a new card will replace your old card with the upgraded version. Discount benefits from a card will only apply when shopping at the Outpost."; };
        
        // Level Events
        { "\cdLevel Events";                    "When entering a level, sometimes unique level-wide events will happen. Each event has it's own unique characteristics and conditions for ending the event."; };
        { "\cdRolling Level Events";            "Level events will not only occur when entering a level, but previous levels which you have visited will also sometimes contain events and require revisiting in order to participate in the event."; };
    };
    
    static TipInfo[MAPEVENT_MAX] EventTips =
    {
        { ""; ""; }; // Never used
        
        { "\cgMegaboss Event";                  "All of the hostiles have vacated the area, and a very powerful creature is lurking about. Return to the Outpost if needed, and make sure you have the power and munitions to take down the death machine."; };
        { "\cqEnvironmental Hazard Event";      "The area's covered in deadly radiation, but there is also a Radiation Neutralizer to fix the problem. Gather fuel and refill the Neutralizer whenever it runs low, until the radiation is brought down to safe levels."; };
        { "\ciThermonuclear Bomb Event";        "Find as many keys as you can to disable the countdown. Worse comes to worst, you can hack through the key slots by using the bomb without keys, but it's going to take time to do so for each key missing, as indicated by the HUD. And make sure the bomb doesn't get shot!"; };
        { "\cuLow Power Event";                 "The UAC had a major battle in this area, and the discards have been left behind when the area was damaged. Pluck some extra items out of the wreckage before they come back to clean up. You can even get some extra credits and modules if you restore the backup power before you leave."; };
        { "\cdAll Auras Event";                 "All of the monsters have rather deadly magic auras. Understand their weaknesses, and attempt to stay away from some of them, as they are dangerous."; };
        { "\cnOne-Monster Event";               "There is one species of demons currently occupying the area. Clearing the area of the hostiles will net you a bonus."; };
        { "\cgHell Unleashed Event";            "Open the box, and partake in the rare contents within, but be warned: You will anger the demonic army, and they will pursue you endlessly until you are killed or leave the area. The longer you stay, the more likely they are to drop items, but the higher level they will become."; };
        { "\cdHarmonized Destruction Event";    "All of the enemies in the area have one aura type. Get accustomed to the aura's strength and weakness and use it to your advantage. Don't get caught off-guard, as some monsters can and will have other auras as well."; };
        { "\cdCracks in the Veil Event";        "The area is spatially unstable. Avoid stepping into the portals, as they will teleport you to other points in the area at random. Enemies will be teleported in as well."; };
        
        { "\cg...";                             "\caC          O        N     S         U       M       E                    \cgY       O          U"; };
        { "\cmWhispers of Darkness";            "\cg\"He who fights with monsters should look to it that he himself does not become a monster. \caAnd when you gaze long into an abyss, \cuthe abyss also gazes into you.\""; };
        
        
        { "\cgR\ciA\ckI\cdN\chB\ctO\caW\cjS";   "SHE BROKE THE REALITY GENERATOR AGAIN! WE'RE DOOMED!"; };
        
        { "\caHell Event";                      "The difficulty level has increased to \caHell\c- for this area."; };
        { "\cmArmageddon Event";                "The difficulty level has increased to \cmArmageddon\c- for this area."; };
    };
    
    int TipID = Random(0, MAX_TIPS - 1);
    fixed HoldTime = StrLen(Tips[TipID].Tip) * 0.1;
    
    str TipTitle = Tips[TipID].Title;
    str TipText = Tips[TipID].Tip;
    
    if (CurrentLevel->Event != MAPEVENT_NONE && !Player.SeenEventTip[CurrentLevel->Event])
    {
        TipTitle = EventTips[CurrentLevel->Event].Title;
        TipText = EventTips[CurrentLevel->Event].Tip;
        HoldTime = StrLen(Tips[TipID].Tip) * 0.1;
        
        Player.SeenEventTip[CurrentLevel->Event] = true;
    };
    
    SetHudSize(0, 0, false);
    // Shove it in the console as one line
    HudMessage("%s\n\n\c-%s\n", TipTitle, TipText, HUDMSG_PLAIN | HUDMSG_LOG, 9998, CR_WHITE, 0, 0, 1);
    HudMessage("\n", HUDMSG_PLAIN, 9998, CR_WHITE, 0, 0, 1);
    SetFont("BIGFONT");
    HudMessage("%s\n", TipTitle, HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.7, HoldTime, 3.0);
    SetFont("SMALLFONT");
    HudMessage("%s\n", TipText, HUDMSG_FADEOUT, 0, CR_WHITE, 1.5, -0.725, HoldTime, 3.0);
};

// Handles Weapon Firing Speed
script void WeaponSpeed()
{
    Start:
    
    int Time;
    
    if (Player.Agility <= 100)
        Time = Abs(Round(Player.WeaponSpeed * 0.35 - 35.0))
    else
        Time = 0;
    
    if (GetActivatorCVar("drpg_stat_weaponspeed") || Player.Stim.PowerupTimer[STIM_RAGE] > 0)
        SetInventory("DRPGSpeed", 1);
    
    Delay((Player.Stim.PowerupTimer[STIM_RAGE] > 0 ? 1 : Time + 1));
    goto Start;
};

// Stat Randomizer Script
script void StatRandomizer()
{
    Start:
    
    if (GetActivatorCVar("drpg_auto_spend") && !Player.Stim.Active)
    {
        while (CheckInventory("DRPGModule") > 0 && !StatsCapped())
        {
            // Select Preferred Stat
            if (Random(1, 2) == 1 && GetActivatorCVar("drpg_auto_spend_pref") >= 0)
                IncreaseStat(GetActivatorCVar("drpg_auto_spend_pref"))
            else // Select Random Stat
                IncreaseStat(Random(0, STAT_MAX - 1));
            
            Delay(1);
        };
    };
    
    Delay(1);
    goto Start;
};

// Handles autosaving
script void AutosaveHandler()
{
    // Terminate if the autosave CVar is disabled
    if (GetCVar("drpg_autosave") == 0 || (PlayerCount() > 1 && !Arbitrator)) return;
    
    int SaveTimer = GetCVar("drpg_autosave") * (35 * 60);
    bool Safe;
    int DamageTimer;
    
    while (true)
    {
        if (Player.AutosaveTimerReset)
            DamageTimer = 0
        else
            ++DamageTimer;
        Player.AutosaveTimerReset = false;
        
        if ((Player.InMenu || Player.InShop || Player.OutpostMenu > 0) && !GetCVar("drpg_menufreeze"))
            DamageTimer = ASAVE_SAFETIME;
        
        if (!SaveTimer)
        {
            Safe = true;
            
            if (DamageTimer < ASAVE_SAFETIME ||
                GetActorProperty(Player.TID, APROP_Health) <= GetActorProperty(Player.TID, APROP_SpawnHealth) / 10)
                Safe = false;
            
            if (Safe)
            {
                Autosave();
                SaveTimer = GetCVar("drpg_autosave") * (35 * 60);
            }
            else
                SaveTimer = ASAVE_RETRYTIME;
        }
        else
            --SaveTimer;
        
        Delay(1);
    };
};

script void ShopSpecialHandler() enter
{
    Start:
    
    // Reset the item
    if (ShopSpecialTimer <= 0)
    {
        bool ValidItem = false;
        int Tries = 0;
        int MinValue = 0;
        int MaxValue = 0;
        int Category = 0;
        int Index = 0;
        
        // Calculate min and max based on settings
        switch (GetCVar("drpg_shopspecial_type"))
        {
        case SHOPSPECIAL_MINMAX:
            MinValue = GetCVar("drpg_shopspecial_min");
            MaxValue = GetCVar("drpg_shopspecial_max");
            break;
        case SHOPSPECIAL_LEVEL:
            MinValue = AveragePlayerLevel() * MAX_LEVEL;
            MaxValue = AveragePlayerLevel() * (100 * MAX_LEVEL);
            break;
        case SHOPSPECIAL_RANK:
            MinValue = AveragePlayerRank() * (25 * MAX_RANK);
            MaxValue = AveragePlayerRank() * (2500 * MAX_RANK);
            break;
        case SHOPSPECIAL_CREDITS:
            MinValue = AveragePlayerCredits() / 10;
            MaxValue = AveragePlayerCredits() * 10;
            break;
        case SHOPSPECIAL_LUCK:
            MinValue = AveragePlayerLuck() * 500;
            MaxValue = AveragePlayerLuck() * 5000;
            break;
        };
        if (GetCVar("drpg_debug"))
            Log("\cdDEBUG: Shop Special \c-Min/Max Calculated: \ca%d \c-/ \cd%d\n", MinValue, MaxValue);
        
        // Blank out the item until a new one is found
        ShopSpecialItem = GetBlankItem();
        
        while (!ValidItem)
        {
            ValidItem = true;
            
            Delay(1);
            
            // Pick an item
            Category = (GetCVar("drpg_shopspecial_category") >= 0 ? GetCVar("drpg_shopspecial_category") : Random(0, ItemCategories - 1));
            Index = Random(0, ItemMax[Category] - 1);
            
            // We're not having any luck meeting the criteria, or the min/max value are impossible values to meet (0 / 0)
            // Break and give up on generating a shop special this time
            if (Tries > 100 || (MinValue == 0 && MaxValue == 0))
            {
                ValidItem = false;
                break;
            };
            
            // Retry if you land on an item with no cost
            if (ItemData[Category][Index].Price == 0)
            {
                ValidItem = false;
                Tries++;
                continue;
            };
            
            // Skip Loot category entirely
            if (Category == 7)
            {
                ValidItem = false;
                Tries++;
                continue;
            };
            
            // Make sure the item is within the valid price range
            if (ItemData[Category][Index].Price < MinValue || ItemData[Category][Index].Price > MaxValue)
            {
                ValidItem = false;
                Tries++;
                continue;
            };
            
            // Item is valid, continue
            if (ValidItem)
                break;
        };
        
        if (ValidItem)
        {
            // Assign the item to the Shop Special item
            ShopSpecialItem = &ItemData[Category][Index];
            
            // Spawn the item if you're in the Outpost
            SpawnShopSpecialItem();
        };
        
        // Reset the timer and bought status
        ShopSpecialTimer = 35 * 60 * GetCVar("drpg_shopspecial_time");
        ShopSpecialBought = false;
        
        if (GetCVar("drpg_debug"))
        {
            if (ShopSpecialItem == GetBlankItem())
                Log("\cdDEBUG: Shop Special expired! \caNo new item generated\n")
            else
                Log("\cdDEBUG: Shop Special expired! Now \cj%s\n", ShopSpecialItem->Name);
        };
    };
    
    // Remove the item if it was bought
    if (CurrentLevel->UACBase && ShopSpecialBought)
        Thing_Remove(ShopSpecialTID + 1);
    
    // Decrease the timer
    ShopSpecialTimer--;
    
    Delay(1);
    goto Start;
};

// Handles the Items dynamic array and associated behavior
script void ItemHandler()
{
    // Create the Items Array
    for (int i = 0; i < MAX_ITEMS; i++)
    {
        ItemTIDs[i] = -1;
        
        if ((i % 15000) == 0)
            Delay(1);
    };
    
    // Items array initialization complete
    ItemsInitialized = true;
    
    while (ItemsInitialized && !CurrentLevel->UACBase)
    {
        for (int i = 0; i < MAX_ITEMS; i++)
        {
            // We've hit the end of the array
            if (ItemTIDs[i] == -1) break;
            
            // Invalid item, continue
            if (ItemTIDs[i] == 0) continue;
            
            // This item was picked up or otherwise removed
            if (ClassifyActor(ItemTIDs[i]) == ACTOR_NONE || ClassifyActor(ItemTIDs[i]) == ACTOR_WORLD) continue;
            
            // Iterate players and check distances
            bool NoClip = false;
            for (int j = 0; j < MAX_PLAYERS; j++)
            {
                // Skip this player if they aren't in the game
                if (!PlayerInGame(j)) continue;
                
                // Skip this player if they aren't Magnetic
                if (Players(j).Stim.PowerupTimer[STIM_MAGNETIC] <= 0) continue;
                
                // Skip this player if they are dead
                if (GetActorProperty(Players(j).TID, APROP_Health) <= 0) continue;
                
                fixed Dist = Distance(ItemTIDs[i], Players(j).TID);
                fixed Height = GetActorPropertyFixed(Players(j).TID, APROP_Height);
                fixed Divisor = (Dist - 16.0) + Dist;
                fixed Amount;
                fixed X;
                fixed Y;
                fixed Z;
                
                // Calculate the amount based on close versus far distance
                if (Dist < 16.0)
                    Amount = 1.0
                else
                    Amount = 16.0 / Divisor;
                
                // Calculate the lerped positions
                X = Lerp(GetActorX(ItemTIDs[i]), GetActorX(Players(j).TID), Amount);
                Y = Lerp(GetActorY(ItemTIDs[i]), GetActorY(Players(j).TID), Amount);
                Z = Lerp(GetActorZ(ItemTIDs[i]), GetActorZ(Players(j).TID) + 32.0, Amount);
                
                SetActorPosition(ItemTIDs[i], X, Y, Z, false);
                NoClip = true;
            };
            
            // Enable/Disable clipping on this item
            if (NoClip)
                SetActorInventory(ItemTIDs[i], "DRPGItemNoClip", 1)
            else
                SetActorInventory(ItemTIDs[i], "DRPGItemNoClipOff", 1);
        };
        
        Delay(4);
    };
};

// Initializes an item and adds it to the Items array
acscript void ItemInit()
{
    // Delay while the Items array is being initialized
    while (!ItemsInitialized) Delay(1);
    
    // [KS] Some items cease existing once they're added to inventory
    // If that's the case, return early so we don't add empty item info
    if (ClassifyActor(0) == ACTOR_WORLD)
        return;
    
    // Add to Items array
    for (int i = 0; i < MAX_ITEMS; i++)
        if (ItemTIDs[i] == -1)
        {
            //if (GetCVar("drpg_debug"))
            //    Log("\cdDEBUG: \c-Item \cd%s\c- added (Index \cd%d\c-)\n", GetActorClass(0), i);
            
            // Doesn't have a TID, so assign it one
            if (ActivatorTID() == 0)
            {
                int TID = UniqueTID();
                ItemTIDs[i] = TID;
                Thing_ChangeTID(0, TID);
            }
            else
                ItemTIDs[i] = ActivatorTID();
            
            break;
        };
};

acscript void ItemDump()
{
    Log("\cd  ===== ITEM ARRAY DATA =====\n");
    
    for (int i = 0; i < MAX_ITEMS; i++)
    {
        if (ItemTIDs[i] == -1) break;
        Log("\cd%d\c-: \cd%s\n", i, GetActorClass(ItemTIDs[i]));
    };
    
    Log("\cd  ===== ITEM ARRAY DATA END =====\n");
};

// Dynamic Loot Generation System
script void DynamicLootGenerator(str Actor, int MaxItems)
{
    fixed LowerX = GetActorX(0);
    fixed UpperX = GetActorX(0);
    fixed LowerY = GetActorY(0);
    fixed UpperY = GetActorY(0);
    int LevelNum = CurrentLevel->LevelNum;
    int Items = 0;
    int Iterations = 0;
    int NumItems = 0;
    
    // Initial delay to make sure all items have been added/initialized in the level
    while (!ItemsInitialized) Delay(1);
    Delay(4);
    
    // Determine the max amount of items to create if it's not specifically specified
    if (MaxItems == 0)
    {
        MaxItems = Random(LevelNum / GameSkill(), LevelNum) + ((AveragePlayerLuck() + AveragePlayerLevel()) / (GameSkill() * 2));
        MaxItems *= GetCVarFixed("drpg_lootgen_factor");
    };
    
    if (MaxItems == 0)
    {
        if (GetCVar("drpg_debug"))
            Log("\cdDebug: \c-Skipped item generation.\n");
        return;
    }
    else if (MaxItems < 0)
    {
        // [KS] Negative luck? Na na nana na! *raspberry*
        if (CurrentLevel->Event == MAPEVENT_MEGABOSS)
            return;
        
        MaxItems = -MaxItems;
        if (CurrentLevel != NULL && CurrentLevel->Event == MAPEVENT_ONEMONSTER)
            Actor = GetMissionMonsterActor(CurrentLevel->SelectedMonster->Actor)
        else
            Actor = "DRPGGenericMonsterDropper";
    };
    
    // Find the furthest item from the player to determine some approximate boundaries to work with
    for (int i = 0; ItemTIDs[i] != -1; i++)
    {
        if (ClassifyActor(ItemTIDs[i]) == ACTOR_NONE) continue;
        
        NumItems = i + 1;
        
        fixed ItemX = GetActorX(ItemTIDs[i]);
        fixed ItemY = GetActorY(ItemTIDs[i]);
        
        if (LowerX > ItemX) LowerX = ItemX;
        if (UpperX < ItemX) UpperX = ItemX;
        if (LowerY > ItemY) LowerY = ItemY;
        if (UpperY < ItemY) UpperY = ItemY;
    };
    
    if (NumItems < 1) // [KS] If we can't find any possible positions to spawn anything, fuck it.
        return;
    
    while (Items < MaxItems)
    {
        int TID = UniqueTID();
        fixed X = RandomFixed(LowerX, UpperX);
        fixed Y = RandomFixed(LowerY, UpperY);
        fixed Z = 0;
        int A = 32 * Random(0, 7);
        
        // Try to keep Z near the floor
        SpawnForced("MapSpot", X, Y, 0, TID, 0);
        Z = GetActorFloorZ(TID);
        Thing_Remove(TID);
        
        bool Spawned = Spawn("MapSpotGravity", X, Y, Z, TID, A);
        if (Spawned)
        {
            bool Remove = DynamicLootGeneratorCheckRemoval(TID, Z);
            
            Thing_Remove(TID);
            if (!Remove)
            {
                if (Spawn(Actor, X, Y, Z, TID, A))
                    Items++;
            };
        };
        
        if (GetCVar("drpg_debug"))
            HudMessage("\cfGenerating Loot\n\cd%d \cj/ \cd%d\n\n\cdActor: \c-%s\n\cdIteration: %d\n\ciBoundaries: %k-%k, %k-%k\n\nX: %k\nY: %k\nZ: %k\n", Items, MaxItems, Actor, Iterations, LowerX, UpperX, LowerY, UpperY, X, Y, Z, HUDMSG_FADEOUT, MAKE_ID('L', 'O', 'O', 'T'), CR_WHITE, 1.5, 0.8, 1.5, 0.5);
        
        Iterations++;
        
        if (Iterations == 8000) // Trick to restart ourselves if we ran out of iterations
        {
            if (GetCVar("drpg_debug"))
                Log("\cdDebug: \c-Dynamic Loot Generation created \cd%d\c- items of type \cd%s\c- and is restarting to place more\n", Items, Actor);
            DynamicLootGenerator(Actor, MaxItems - Items);
            return;
        };
    };
    
    if (GetCVar("drpg_debug"))
        Log("\cdDebug: \c-Dynamic Loot Generation created \cd%d\c- items of type \cd%s\n", Items, Actor);
};

script bool DynamicLootGeneratorCheckRemoval(int TID, fixed Z)
{
    bool Remove = true;
    
    for (int i = 0; ItemTIDs[i] != -1; i++)
    {
        // If this spot is blank, skip it
        if (ClassifyActor(ItemTIDs[i]) == ACTOR_WORLD) continue;
        
        // Randomly continue for variance
        if (Random(1, 4) == 1) continue;
        
        bool CanSee = CheckSight(ItemTIDs[i], TID, 0);
        
        // [KS] Don't spawn stuff on special-effects flats like skyfloor or blackness, or at extreme height differences, because more often than not those are used as void space filler or instakill floors.
        // Examples: Epic2 MAP14, SF2012 MAP02, some CC4 maps.
        bool BadFloor = (CheckActorFloorTexture(TID, "F_SKY1") || CheckActorFloorTexture(TID, "F_SKY2") || CheckActorFloorTexture(TID, "BLACK") || CheckActorFloorTexture(TID, "FBLACK") || CheckActorFloorTexture(TID, "ALLBLAKF"));
        
        if (CanSee && !BadFloor && Abs(Z - GetActorZ(ItemTIDs[i])) <= 128)
        {
            Remove = false;
            break;
        };
    };
    
    return Remove;
};

// Quick Heal
acscript void QuickHeal(int Quick) net
{
    // If the player's dead, terminate
    if (GetActorProperty(0, APROP_Health) <= 0) return;
    
    int Health = Player.ActualHealth;
    int Percent = GetActivatorCVar("drpg_auto_heal_percent");
    
    if ((Health < Player.HealthMax / Percent) || Quick)
    {
        if (GetActivatorCVar("drpg_auto_heal_order") == 1) // Smallest to Largest
        {
                 if (CheckInventory("DRPGStimPack"))            UseInventory("DRPGStimPack")
            else if (CheckInventory("DRPGMedikit"))             UseInventory("DRPGMedikit")
            else if (CheckInventory("DRPGLargeMedikit"))        UseInventory("DRPGLargeMedikit")
            else if (CheckInventory("DRPGXLMedikit"))           UseInventory("DRPGXLMedikit")
            else if (CheckInventory("DRPGMedPack"))             UseInventory("DRPGMedPack")
            else if (CheckInventory("DRPGSurgeryKit"))          UseInventory("DRPGSurgeryKit");
        }
        else if (GetActivatorCVar("drpg_auto_heal_order") == 2) // Largest to Smallest
        {
                 if (CheckInventory("DRPGSurgeryKit"))          UseInventory("DRPGSurgeryKit")
            else if (CheckInventory("DRPGMedPack"))             UseInventory("DRPGMedPack")
            else if (CheckInventory("DRPGXLMedikit"))           UseInventory("DRPGXLMedikit")
            else if (CheckInventory("DRPGLargeMedikit"))        UseInventory("DRPGLargeMedikit")
            else if (CheckInventory("DRPGMedikit"))             UseInventory("DRPGMedikit")
            else if (CheckInventory("DRPGStimPack"))            UseInventory("DRPGStimPack");
        };
    };
};

// Activate Focus Mode
acscript void ToggleFocusMode() net
{
    Player.Focusing = !Player.Focusing;
    
    if (Player.Focusing)
        FocusMode();
};

// Focus Mode Script
acscript void FocusMode()
{
    int PrevEP = Player.EP;
    int RegenWindupSpeed = ((35 * 60) - ((35 * 50) * Player.Regeneration / 200));
    if (RegenWindupSpeed < 35) // Enforce a 1-second wind-up at least
        RegenWindupSpeed = 35;
    int RegenDelay = RegenWindupSpeed / 15;
    
    // Return if you're already at max EP
    if (Player.EP >= Player.EPMax) return;
    
    SetPlayerProperty(0, 1, PROP_FROZEN);
    
    while (Player.Focusing)
    {
        int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
        int Percent = (int)(((fixed)(Player.EP - PrevEP) / (fixed)(Player.EPMax - PrevEP)) * 100.0);
        fixed X = GetActorX(0) + Cos((fixed)Timer() / 64.0) * 32.0;
        fixed Y = GetActorY(0) + Sin((fixed)Timer() / 64.0) * 32.0;
        fixed Z = GetActorZ(0);
        
        Player.EPRate = 0;
        
        PlaySound(0, "misc/epfocus", CHAN_BODY, 0.5, true, ATTN_NORM);
        SpawnForced("DRPGEPFocusAura", X, Y, Z, 0, 0);
        DrawProgressBar("\cnFocusing", Percent);
        
        if (RegenWindupSpeed > 1)
            RegenWindupSpeed--;
        
        if (RegenDelay > 0)
            RegenDelay--
        else
        {
            Player.EP++;
            RegenDelay = RegenWindupSpeed / 15;
        };
        
        if (Buttons > 0 || Player.EP >= Player.EPMax)
            Player.Focusing = false;
        
        Delay(1);
    };
    
    PlaySound(0, "misc/epfocusdone", CHAN_BODY, 0.5, false, ATTN_NORM);
    SetPlayerProperty(0, 0, PROP_FROZEN);
};

// Death Script
script void Dead() death
{
    // Reset menu vars
    Player.InMenu = false;
    Player.InShop = false;
    Player.OutpostMenu = 0;
    
    // Remove Aura
    RemoveAura();
    
    // Remove Shield
    SetInventory("DRPGShield", 0);
    
    // Remove Status Effect
    for (int i = 0; i < SE_MAX; i++)
        Player.StatusTimer[i] = 0;
    
    // Drop Credits
    if (GetCVar("drpg_multi_dropcredits") && CheckInventory("DRPGCredits") > 0)
    {
        int DropAmount = CheckInventory("DRPGCredits") / 100 * GetCVar("drpg_multi_dropcredits_percent");
        
        // Cap out at a million so if you have stupid amounts of Credits you don't freeze/nuke the game
        if (DropAmount > 1000000) DropAmount = 1000000;
        
        TakeInventory("DRPGCredits", DropAmount);
        DropMoney(PlayerNumber(), 0, DropAmount);
    };
    
    // Drop Inventory
    if (GetCVar("drpg_multi_dropinv"))
        DropInventory();
    
    // VENG-R Accessory
    for (int i = 0; i < MAX_PLAYERS; i++)
    {
        // Skip yourself
        if (Player.TID == Players(i).TID) continue;
        
        if (Players(i).Shield.Accessory && Players(i).Shield.Accessory->PassiveEffect == SHIELD_PASS_AVENGER)
        {
            GiveActorInventory(Players(i).TID, "DRPGShieldAvengerDamage", 1);
            GiveActorInventory(Players(i).TID, "DRPGShieldAvengerDefense", 1);
        };
    };
    
    // Remove TID
    Thing_ChangeTID(Player.TID, 0);
};

// Respawn
script void Respawn() respawn
{
    // Reassign TID
    if (CompatMode == COMPAT_DRLA)
        Delay(4);
    AssignTIDs();
    
    // Heal to max health
    Player.ActualHealth = Player.HealthMax;
    SetActorProperty(0, APROP_Health, Player.ActualHealth);
    
    // XP/Rank Penalty
    if (GetCVar("drpg_multi_takexp"))
    {
        long int XPPenalty = (long int)(XPTable[Player.Level] * GetCVar("drpg_multi_takexp_percent") / 100);
        long int RankPenalty = (long int)(RankTable[Player.RankLevel] * GetCVar("drpg_multi_takexp_percent") / 100);
        
        if (XPPenalty > 0 || RankPenalty > 0)
        {
            Player.XP -= XPPenalty;
            Player.Rank -= RankPenalty;
            SetFont("BIGFONT");
            HudMessage("\cjXP -%d\n\ckRank -%d\n", XPPenalty, RankPenalty, HUDMSG_FADEOUT | HUDMSG_LOG, 0, CR_WHITE, 1.5, 0.75, 2.0, 2.0);
        };
    };
    
    // Restore EP if CVAR is set
    if (GetCVar("drpg_multi_restoreep"))
        Player.EP = Player.EPMax;
    
    // Give a box of ammo if a specific ammo type is empty if the CVAR is set
    if (GetCVar("drpg_multi_restoreammo"))
    {
        if (CheckInventory("Clip") < GetAmmoAmount("Clip") * (Player.Capacity / 10))
            SetInventory("Clip", GetAmmoAmount("Clip") * (Player.Capacity / 10));
        if (CheckInventory("Shell") < GetAmmoAmount("Shell") * (Player.Capacity / 10))
            SetInventory("Shell", GetAmmoAmount("Shell") * (Player.Capacity / 10));
        if (CheckInventory("RocketAmmo") < GetAmmoAmount("RocketAmmo") * (Player.Capacity / 10))
            SetInventory("RocketAmmo", GetAmmoAmount("RocketAmmo") * (Player.Capacity / 10));
        if (CheckInventory("Cell") < GetAmmoAmount("Cell") * (Player.Capacity / 10))
            SetInventory("Cell", GetAmmoAmount("Cell") * (Player.Capacity / 10));
    };
    
    // Apply camera textures and vars
    SetCameraToTexture(Player.TID, StrParam("P%iVIEW\n", PlayerNumber() + 1), 110);
    SetCameraToTexture(Player.TID, StrParam("P%iSVIEW\n", PlayerNumber() + 1), 90);
    Player.PlayerView = PlayerNumber();
    
    // Run Scripts
    Loop();
    PlayerDamage();
    DamageNumbers();
    InfoPopoffs();
    HealthBars();
    ShieldTimer();
    
    // GUI
	CheckGUI();
	CheckCursor();
    // UpdateMenu();
    
    // NU-YU Accessory
    if (Player.Shield.Accessory && Player.Shield.Accessory->PassiveEffect == SHIELD_PASS_HYPERION)
    {
        ActivatorSound("shield/newu", 127);
        Player.Shield.Active = true;
        Player.Shield.Charge = Player.Shield.Capacity;
        GiveInventory("DRPGShieldNewUProtection", 1);
    };
    
    // Quick Teleport
    if (GetPlayerInput(PlayerNumber(), INPUT_BUTTONS) & BT_SPEED)
    {
        int PlayerNum = -1;
        while (PlayerNum == -1 || PlayerNum == PlayerNumber() || !PlayerInGame(PlayerNum))
            PlayerNum = Random(0, PlayerCount());
        PlayerTeleport(PlayerNum);
        Player.EP -= ScaleEPCost(50);
    };
};

// Gives the player their default loadout based on their starting options
script void DefaultLoadout()
{
    static str[LOADOUT_WEAPONS][3] WeaponInfo =
    {
        // CVAR, Vanilla Actor, Extras Actor
        
        { "drpg_start_weapon_chainsaw";         "DRPGChainsaw";         "Chainsaw1";        };
        { "drpg_start_weapon_pistol";           "DRPGPistol";           "Pistol1";          };
        { "drpg_start_weapon_shotgun";          "DRPGShotgun";          "Shotgun1";         };
        { "drpg_start_weapon_ssg";              "DRPGSuperShotgun";     "SSG";              };
        { "drpg_start_weapon_chaingun";         "DRPGChaingun";         "Chaingun1";        };
        { "drpg_start_weapon_rocketlauncher";   "DRPGRocketLauncher";   "RocketLauncher1";  };
        { "drpg_start_weapon_plasmarifle";      "DRPGPlasmaRIfle";      "Plasmagun";        };
        { "drpg_start_weapon_bfg";              "DRPGBFG9001";          "BFG9001";          };
    };
    static str[LOADOUT_ARMORS] ArmorInfo =
    {
        "DRPGGreenArmorEffect";
        "DRPGBlueArmorEffect";
        "DRPGYellowArmorEffect";
        "DRPGRedArmorEffect";
        "DRPGWhiteArmorEffect";
        "DRPGReinforcedGreenArmorEffect";
        "DRPGReinforcedBlueArmorEffect";
        "DRPGReinforcedYellowArmorEffect";
        "DRPGReinforcedRedArmorEffect";
        "DRPGReinforcedWhiteArmorEffect";
    };
    static str[LOADOUT_SHIELDPARTS] ShieldPartColorCodes =
    {
        "\cv";  // Capacity +
        "\ch";  // Capacity -
        "\cd";  // Charge Rate +
        "\cq";  // Charge Rate -
        "\cr";  // Delay Rate +
        "\cg";  // Delay Rate -
    };
    static str[LOADOUT_SHIELDACCS] ShieldAccessoryColorCodes =
    {
        "\cg";  // Strength / Damage
        "\ca";  // Vitality / Health
        "\ci";  // Agility / Speed
        "\cd";  // Defense / Armor
        "\cf";  // Luck / Money
        "\cn";  // Energy
        "\ch";  // Capacity / Ammo
        "\ck";  // Rank
        "\cc";  // Augmentation
        "\cq";  // Powerup
        "\cj";  // Misc / Multi-Category
    };
    static str[LOADOUT_DRLAWEAPONS] DRLAWeaponColorCodes =
    {
        "";     // Common
        "\cv";  // Assembled
        "\ct";  // Exotic
        "\ci";  // Superior
        "\cd";  // Unique
        "\cg";  // Demonic
        "\cf";  // Legendary
    };
    static str[LOADOUT_DRLAARMORS] DRLAArmorColorCodes =
    {
        "";     // Common
        "\cv";  // Assembled
        "\ct";  // Exotic
        "\cc";  // Onyx
        "\cd";  // Unique
        "\cf";  // Legendary
    };
    static str[LOADOUT_DRLAMODPACKS] DRLAModPacks =
    {
        "RLPowerModItem";
        "RLBulkModItem";
        "RLAgilityModItem";
        "RLTechnicalModItem";
        "RLSniperModItem";
        "RLFirestormModItem";
        "RLNanoModItem";
        "RLOnyxModItem";
    };
    
    // Shield Databases
    ItemInfoPtr[LOADOUT_SHIELDPARTS][MAX_BODIES] ShieldBodyTypes;
    ItemInfoPtr[LOADOUT_SHIELDPARTS][MAX_BATTERIES] ShieldBatteryTypes;
    ItemInfoPtr[LOADOUT_SHIELDPARTS][MAX_CAPACITORS] ShieldCapacitorTypes;
    ItemInfoPtr[LOADOUT_SHIELDACCS][MAX_ACCESSORIES] ShieldAccessoryTypes;
    static int[LOADOUT_SHIELDPARTS] BodyCount;
    static int[LOADOUT_SHIELDPARTS] BatteryCount;
    static int[LOADOUT_SHIELDPARTS] CapacitorCount;
    int[LOADOUT_SHIELDACCS] AccessoryCount;
    
    // DRLA Databases
    ItemInfoPtr[LOADOUT_DRLAWEAPONS][ITEM_MAX] DRLAWeaponTypes;
    ItemInfoPtr[LOADOUT_DRLAARMORS][ITEM_MAX] DRLAArmorTypes;
    ItemInfoPtr[LOADOUT_DRLAARMORS][ITEM_MAX] DRLABootsTypes;
    int[LOADOUT_DRLAWEAPONS] DRLAWeaponCount;
    int[LOADOUT_DRLAARMORS] DRLAArmorCount;
    int[LOADOUT_DRLAARMORS] DRLABootsCount;
    
    // Delay to allow for item data to initialize
    Delay(4);
    
    // Build Shield and DRLA item databases based on their respective items' color codes
    for (int i = 0; i < MAX_BODIES; i++)
        for (int j = 0; j < LOADOUT_SHIELDPARTS; j++)
            if (Contains(ShieldParts[0][i].Name, ShieldPartColorCodes[j]))
            {
                ShieldBodyTypes[j][BodyCount[j]++] = FindItemInCategory(ShieldParts[0][i].Actor, 5);
                break;
            };
    for (int i = 0; i < MAX_BATTERIES; i++)
        for (int j = 0; j < LOADOUT_SHIELDPARTS; j++)
            if (Contains(ShieldParts[1][i].Name, ShieldPartColorCodes[j]))
            {
                ShieldBatteryTypes[j][BatteryCount[j]++] = FindItemInCategory(ShieldParts[1][i].Actor, 5);
                break;
            };
    for (int i = 0; i < MAX_CAPACITORS; i++)
        for (int j = 0; j < LOADOUT_SHIELDPARTS; j++)
            if (Contains(ShieldParts[2][i].Name, ShieldPartColorCodes[j]))
            {
                ShieldCapacitorTypes[j][CapacitorCount[j]++] = FindItemInCategory(ShieldParts[2][i].Actor, 5);
                break;
            };
    for (int i = 0; i < MAX_ACCESSORIES; i++)
        for (int j = 0; j < LOADOUT_SHIELDACCS; j++)
            if (Contains(ShieldAccessories[i].Name, ShieldAccessoryColorCodes[j]))
            {
                ShieldAccessoryTypes[j][AccessoryCount[j]++] = FindItemInCategory(ShieldAccessories[i].Actor, 5);
                break;
            };
    for (int i = 0; i < ItemMax[0]; i++)
        for (int j = 1; j < LOADOUT_DRLAWEAPONS; j++)
        {
            if (i < 10) // Special catch for Common
            {
                DRLAWeaponTypes[0][DRLAWeaponCount[0]++] = &ItemData[0][i];
                break;
            }
            else if (Contains(ItemData[0][i].Name, DRLAWeaponColorCodes[j]))
            {
                DRLAWeaponTypes[j][DRLAWeaponCount[j]++] = &ItemData[0][i];
                break;
            };
        };
    for (int i = 1; i < ItemMax[3]; i++)
        for (int j = 1; j < LOADOUT_DRLAARMORS; j++)
        {
            // Onyx is handled below
            if (j == 3) continue;
            
            if (i < 4) // Special catch for Common
            {
                DRLAArmorTypes[0][DRLAArmorCount[0]++] = &ItemData[3][i];
                break;
            }
            else if (Contains(ItemData[3][i].Name, "Onyx")) // Special catch for Onyx
            {
                DRLAArmorTypes[3][DRLAArmorCount[3]++] = &ItemData[3][i];
                break;
            }
            else if (Contains(ItemData[3][i].Name, DRLAArmorColorCodes[j]))
            {
                DRLAArmorTypes[j][DRLAArmorCount[j]++] = &ItemData[3][i];
                break;
            };
        };
    for (int i = 0; i < ItemMax[9]; i++)
        for (int j = 1; j < LOADOUT_DRLAARMORS; j++)
        {
            if (i < 3) // Special catch for Common
            {
                DRLABootsTypes[0][DRLABootsCount[0]++] = &ItemData[9][i];
                break;
            }
            else if (Contains(ItemData[9][i].Name, DRLAArmorColorCodes[j]))
            {
                DRLABootsTypes[j][DRLABootsCount[j]++] = &ItemData[9][i];
                break;
            };
        };
    
    // Weapons
    if (CompatMode != COMPAT_DRLA)
    {
        for (int i = 0; i < 8; i++)
        {
            if (GetActivatorCVar(WeaponInfo[i][0]))
            {
                if (CompatMode == COMPAT_EXTRAS)
                {
                    GiveInventory(WeaponInfo[i][2], 1);
                    SetWeapon(WeaponInfo[i][2]);
                }
                else
                {
                    GiveInventory(WeaponInfo[i][1], 1);
                    SetWeapon(WeaponInfo[i][1]);
                };
            }
            else
            {
                if (CompatMode == COMPAT_EXTRAS)
                    TakeInventory(WeaponInfo[i][2], 1)
                else
                    TakeInventory(WeaponInfo[i][1], 1);
            };
        };
    };
    
    // Ammo
    SetInventory("Clip", GetActivatorCVar("drpg_start_ammo_bullet"));
    SetInventory("Shell", GetActivatorCVar("drpg_start_ammo_shell"));
    SetInventory("RocketAmmo", GetActivatorCVar("drpg_start_ammo_rocket"));
    SetInventory("Cell", GetActivatorCVar("drpg_start_ammo_cell"));
    
    // Armor
    if (CompatMode != COMPAT_DRLA && GetActivatorCVar("drpg_start_armor") >= 0)
        GiveInventory(ArmorInfo[GetActivatorCVar("drpg_start_armor")], 1);
    
    // Active Augs
    int ActiveAugs = 0;
    while (ActiveAugs < GetActivatorCVar("drpg_start_aug_amount"))
    {
        int Type = Random(0, AUG_MAX - 1);
        
        // Skip this aug if it's already active
        if (Player.Augs.Level[Type] > 0) continue;
        
        Player.Augs.Level[Type]++;
        ActiveAugs++;
        
        if ((Timer() % 5) == 0)
            Delay(1);
    };
    
    // Aug Canisters/Upgrades
    int AugItems = 0;
    while (AugItems < GetActivatorCVar("drpg_start_aug_upgrades"))
    {
        int Type = Random(0, 1);
        
        // Check if one or the other is max
        if (CheckInventory("DRPGAugCanister") >= 100)
            Type = 1;
        if (CheckInventory("DRPGAugUpgradeCanister") >= 100)
            Type = 0;
        
        if (Type == 1)
            GiveInventory("DRPGAugCanister", 1)
        else
            GiveInventory("DRPGAugUpgradeCanister", 1);
        
        AugItems++;
        
        if ((Timer() % 5) == 0)
            Delay(1);
    };
    
    // Aug Slots
    Player.Augs.BaseSlots = GetActivatorCVar("drpg_start_aug_slots");
    
    // Shield Parts
    for (int i = 0; i < 3; i++)
    {
        int *[3] Count =
        {
            &BodyCount[0];
            &BatteryCount[0];
            &CapacitorCount[0];
        };
        int[3] Total =
        {
            GetActivatorCVar("drpg_start_shield_amount_body");
            GetActivatorCVar("drpg_start_shield_amount_battery");
            GetActivatorCVar("drpg_start_shield_amount_capacitor");
        };
        int[3] Current;
        
        // Calculate total if we specify a type so that overflows or explosions don't happen
        if (GetActivatorCVar("drpg_start_shield_type") >= 0)
            if (Total[i] > Count[i][GetActivatorCVar("drpg_start_shield_type")])
                Total[i] = Count[i][GetActivatorCVar("drpg_start_shield_type")];
        
        while (Current[i] < Total[i])
        {
            int Category = Random(0, LOADOUT_SHIELDPARTS - 1);
            
            // Force the category type if the CVAR is set
            if (GetActivatorCVar("drpg_start_shield_type") >= 0)
                Category = GetActivatorCVar("drpg_start_shield_type");
            
            for (int j = 0; j < Count[i][Category]; j++)
            {
                ItemInfoPtr *[3] Type =
                {
                    &ShieldBodyTypes[Category][j];
                    &ShieldBatteryTypes[Category][j];
                    &ShieldCapacitorTypes[Category][j];
                };
                
                if (!CheckInventory((*(Type[i]))->Actor))
                {
                    GiveInventory((*(Type[i]))->Actor, 1);
                    Current[i]++;
                    break;
                };
            };
            
            if ((Timer() % 5) == 0)
                Delay(1);
        };
    };

    // Shield Accessories
    int TotalAccessories = GetActivatorCVar("drpg_start_shield_amount_acc");
    int Accessories = 0;
    if (GetActivatorCVar("drpg_start_shield_type_acc") >= 0)
        if (TotalAccessories > AccessoryCount[GetActivatorCVar("drpg_start_shield_type_acc")])
            TotalAccessories = AccessoryCount[GetActivatorCVar("drpg_start_shield_type_acc")];
    while (Accessories < TotalAccessories)
    {
        int Category = Random(0, LOADOUT_SHIELDACCS - 1);
        
        // Force the category type if the CVAR is set
        if (GetActivatorCVar("drpg_start_shield_type_acc") >= 0)
            Category = GetActivatorCVar("drpg_start_shield_type_acc");
        
        for (int i = 0; i < AccessoryCount[Category]; i++)
        {
            ItemInfoPtr *Type = &ShieldAccessoryTypes[Category][i];
            
            if (!CheckInventory((*Type)->Actor))
            {
                GiveInventory((*Type)->Actor, 1);
                Accessories++;
                break;
            };
        };
        
        if ((Timer() % 5) == 0)
            Delay(1);
    };
    
    // Stims
    int Injectors = 0;
    int Vials = 0;
    while (Injectors < GetActivatorCVar("drpg_start_stim_injectors"))
    {
        switch (Random(1, 4))
        {
        case 1: // Small
            if (CheckInventory("DRPGStimSmall") >= 1000) continue;
            GiveInventory("DRPGStimSmall", 1);
            break;
        case 2: // Medium
            if (CheckInventory("DRPGStimMedium") >= 1000) continue;
            GiveInventory("DRPGStimMedium", 1);
            break;
        case 3: // Large
            if (CheckInventory("DRPGStimLarge") >= 1000) continue;
            GiveInventory("DRPGStimLarge", 1);
            break;
        case 4: // XL
            if (CheckInventory("DRPGStimXL") >= 1000) continue;
            GiveInventory("DRPGStimXL", 1);
            break;
        };
        
        Injectors++;
        
        if ((Timer() % 5) == 0)
            Delay(1);
    };
    while (Vials < GetActivatorCVar("drpg_start_stim_vials"))
    {
        int Type = Random(0, STIM_MAX - 1);
        bool Maxed = true;
        
        // Check to make sure all the vials aren't maxed
        for (int i = 0; i < STIM_MAX; i++)
            if (Player.Stim.Vials[i] < Player.Capacity * 10)
                Maxed = false;
        if (Maxed) break;
        
        // Don't include Boosters
        if (!GetActivatorCVar("drpg_start_stim_boosters") && Type >= StimStatsEnd && Type <= StimPowerupStart) continue;
        
        // Don't include Powerups
        if (!GetActivatorCVar("drpg_start_stim_powerups") && Type >= StimPowerupStart && Type <= StimPowerupEnd) continue;
        
        // Skip this one if this vial type is full
        if (Player.Stim.Vials[Type] >= Player.Capacity * 10) continue;
        
        Player.Stim.Vials[Type]++;
        Vials++;
        
        if ((Timer() % 5) == 0)
            Delay(1);
    };
    
    // Minigame Chips
    int Chips = 0;
    while (Chips < GetActivatorCVar("drpg_start_bonus_chips"))
    {
        int Type = Random(0, 1);
        
        // Check if one or the other is max
        if (CheckInventory("DRPGChipGold") >= 1000)
            Type = 1;
        if (CheckInventory("DRPGChipPlatinum") >= 1000)
            Type = 0;
        
        if (Type == 1)
            GiveInventory("DRPGChipGold", 1)
        else
            GiveInventory("DRPGChipPlatinum", 1);
        
        Chips++;
        
        if ((Timer() % 5) == 0)
            Delay(1);
    };
    
    // UAC Shop Card
    if (GetActivatorCVar("drpg_start_bonus_shopcard") > 0)
    {
        // Diamond Card
        if (GetActivatorCVar("drpg_start_bonus_shopcard") >= 5)
            GiveInventory("DRPGDiamondUACCard", 1)
        else
            GiveInventory("DRPGUACCard", GetActivatorCVar("drpg_start_bonus_shopcard"));
    };
    
    // Payout Bonus
    Player.PayBonus = GetActivatorCVar("drpg_start_bonus_payout");
    
    // DRLA
    if (CompatMode == COMPAT_DRLA)
    {
        bool FoundWeapon = false;
        bool FoundArmor = false;
        bool FoundBoots = false;
        int WeaponCount = 0;
        int MaxWeaponCount = GetActivatorCVar("drpg_start_drla_weapon_amount");
        int ModPackCount = 0;
        int ModPackMax = GetActivatorCVar("drpg_start_drla_modpacks");
        
        // Weapons
        if (GetActivatorCVar("drpg_start_drla_weapon_type") > 0)
            if (MaxWeaponCount > DRLAWeaponCount[GetActivatorCVar("drpg_start_drla_weapon_type") - 1])
                MaxWeaponCount = DRLAWeaponCount[GetActivatorCVar("drpg_start_drla_weapon_type") - 1];
        while (WeaponCount < MaxWeaponCount)
        {
            for (int i = 0; i < LOADOUT_DRLAWEAPONS; i++)
            {
                FoundWeapon = false;
                
                // Force the weapon type if the CVAR is set
                if (GetActivatorCVar("drpg_start_drla_weapon_type") >= 0)
                    i = GetActivatorCVar("drpg_start_drla_weapon_type");
                
                for (int j = 0; j < DRLAWeaponCount[i]; j++)
                    if (Random(1, 10) == 1 && !CheckInventory(DRLAWeaponTypes[i][j]->Actor))
                    {
                        GiveInventory(DRLAWeaponTypes[i][j]->Actor, 1);
                        WeaponCount++;
                        FoundWeapon = true;
                        break;
                    };
                
                if (FoundWeapon)
                    break;
                
                if ((Timer() % 5) == 0)
                    Delay(1);
            };
        };
        GiveInventory("RLWeaponLimit", WeaponCount);
        
        // Armor
        if (GetActivatorCVar("drpg_start_drla_armor") >= -1)
            while (!FoundArmor)
            {
                for (int i = 0; i < LOADOUT_DRLAARMORS; i++)
                {
                    // If there is nothing in this category, skip it
                    if (GetActivatorCVar("drpg_start_drla_armor") >= 0 && DRLAArmorCount[GetActivatorCVar("drpg_start_drla_armor")] == 0)
                    {
                        FoundArmor = true;
                        continue;
                    }
                    else if (GetActivatorCVar("drpg_start_drla_armor") >= 0) // Force the armor type if the CVAR is setup
                        i = GetActivatorCVar("drpg_start_drla_armor");
                    
                    for (int j = 0; j < DRLAArmorCount[i]; j++)
                        if (Random(1, 10) == 1)
                        {
                            GiveInventory(DRLAArmorTypes[i][j]->Actor, 1);
                            FoundArmor = true;
                            break;
                        };
                    
                    if (FoundArmor)
                        break;
                    
                    if ((Timer() % 5) == 0)
                        Delay(1);
                };
            };
            
        // Boots
        if (GetActivatorCVar("drpg_start_drla_boots") >= -1)
            while (!FoundBoots)
            {
                for (int i = 0; i < LOADOUT_DRLAARMORS; i++)
                {
                    // If there is nothing in this category, skip it
                    if (GetActivatorCVar("drpg_start_drla_boots") >= 0 && DRLABootsCount[GetActivatorCVar("drpg_start_drla_boots")] == 0)
                    {
                        FoundBoots = true;
                        continue;
                    }
                    else if (GetActivatorCVar("drpg_start_drla_boots") >= 0) // Force the boots type if the CVAR is setup
                        i = GetActivatorCVar("drpg_start_drla_boots");
                    
                    for (int j = 0; j < DRLABootsCount[i]; j++)
                        if (Random(1, 10) == 1)
                        {
                            GiveInventory(DRLABootsTypes[i][j]->Actor, 1);
                            FoundBoots = true;
                            break;
                        };
                    
                    if (FoundBoots)
                        break;
                    
                    if ((Timer() % 5) == 0)
                        Delay(1);
                };
            };
        
        // Mod Packs
        if (PlayerClass(0) == 2)
            ModPackMax--;
        while (ModPackCount < ModPackMax)
        {
            int Type = Random(0, LOADOUT_DRLAMODPACKS - 1);
            
            // Stop if we're not the technician and we try and give over 4 mods
            if (PlayerClass(0) != 2 && ModPackCount >= 4) break;
            
            // Skip exotic modpacks if the selection is disabled
            if (Type > 3 && !GetActivatorCVar("drpg_start_drla_modpacks_amount")) continue;
            
            GiveInventory(DRLAModPacks[Type], 1);
            ModPackCount++;
            
            if ((Timer() % 5) == 0)
                Delay(1);
        };
    };
};

// Apply values to global vars visible on the HUD
function void CheckHUD()
{
    int PlayerNum = PlayerNumber();
    
    // EP
    EP[PlayerNum] = Player.EP;
    
    // EP Bar on HUD
    if (Player.EPMax <= 0)
        SetInventory("DRPGEP", 0)
    else
        SetInventory("DRPGEP", Player.EP * 100 / Player.EPMax);
    
    // Shield
    if (Player.Shield.Capacity > 0)
    {
        Shield[PlayerNum] = Player.Shield.Charge;
        ShieldCapacity[PlayerNum] = Player.Shield.Capacity;
        ShieldHealth[PlayerNum] = Player.ActualHealth;
        SetInventory("DRPGShieldCapacity", (int)(((fixed)Player.Shield.Charge / (fixed)Player.Shield.Capacity) * 100.0));
        SetInventory("DRPGShield", Player.Shield.Active);
    };
    
    // Items
    Credits[PlayerNum] = CheckInventory("DRPGCredits");
};

function void CheckKeys()
{
    static str[6] Keycards =
    {
        "DRPGRedCard";
        "DRPGYellowCard";
        "DRPGBlueCard";
        "DRPGRedSkull";
        "DRPGYellowSkull";
        "DRPGBlueSkull";
    };
    
    // Don't bother if we're not in multiplayer
    if (!InMultiplayer) return;
    
    for (int i = 0; i < 6; i++)
    {
        if (CheckInventory(Keycards[i]))
        {
            for (int j = 0; j < MAX_PLAYERS; j++)
            {
                if (!PlayerInGame(j)) continue;
                if (CheckActorInventory(Players(j).TID, Keycards[i])) continue;
                
                SetActorInventory(Players(j).TID, Keycards[i], 1);
            };
        };
    };
};

function void CheckCompatibility()
{
    bool Success = false;
    int TID = UniqueTID();
    
    if (GetCVar("drpg_debug"))
        Log("\cdDEBUG: \c-Checking Compatibility...\n");
    
    CompatMode = COMPAT_NONE;
    
    // Extras
    Success = SpawnForced("DRPGExtrasIsLoaded", 0, 0, 0, TID, 0);
    if (Success)
    {
        if (GetCVar("drpg_debug"))
            Log("\cdDEBUG: \caExtras\c- detected\n");
        CompatMode = COMPAT_EXTRAS;
        Thing_Remove(TID);
        return;
    };
    
    // DoomRL
    Success = SpawnForced("RLKateMatterstormHarnessArmorToken", 0, 0, 0, TID, 0);
    if (Success)
    {
        if (GetCVar("drpg_debug"))
            Log("\cdDEBUG: \cdDoomRL \c-detected\n");
        CompatMode = COMPAT_DRLA;
        SetInventory("DRPGDRLAActive", 1);
        Thing_Remove(TID);
        
        Success = SpawnForced("RLBaronOfHell", 0, 0, 0, TID, 0);
        if (!Success)
        {
            SetHudSize(640, 480);
            FadeRange(0, 0, 0, 1.0, 0, 0, 0, 1.0, 0);
            
            HudMessageBold("\cgLISTEN UP\n\n\c-You currently have \cfDoomRL Arsenal\c- loaded. In order for this mod to work with DoomRPG, you also need the \cfDoomRL Monster Pack\c-. Go here to download it: http://tinyurl.com/DoomRLArsenal\n\nThanks.\n", HUDMSG_PLAIN | HUDMSG_LOG, 772, CR_WHITE, 0.1, 0.1, 0.01);
            
            SetFont("BigFont");
            HudMessageBold("LISTEN UP\n", HUDMSG_PLAIN, 772, CR_RED, 320.4, 200.0, 0.01);
            
            SetFont("SmallFont");
            HudMessageBold("You currently have \cfDoomRL Arsenal\c- loaded. In order for this mod to work with DoomRPG, you also need the \cfDoomRL Monster Pack\c-. Go here to download it: http://tinyurl.com/DoomRLArsenal\n\nThanks.\n", HUDMSG_PLAIN, 773, CR_WHITE, 320.4, 232.0, 0.01);
        }
        else
        {
            Thing_Remove(TID);
            return;
        };
    };
};

function void AssignTIDs()
{
    Player.TID = PLAYER_TID + PlayerNumber();
    Thing_ChangeTID(0, Player.TID);
    
    if (GetCVar("drpg_debug"))
        Log("\cdDEBUG: Player TID: %d\n", Player.TID);
};
