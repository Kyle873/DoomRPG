/* HEALING ITEMS

    Health Bonus        - +1 HP
    Stimpack            - +10 Medkit
    Medikit             - +25 Medkit
    Large Medikit       - +50 Medkit
    Extra-Large Medikit - +75 Medkit
    Med-Pack            - +100 Medkit
    Surgery Kit         - +200 Medkit
    
    Medkit Refill       - Full Medkit (Only available in the shop)
    
    Soulspheres         - +100-300 HP
    
    1-Up                - Increases Lives by 1
    3-Up                - Increases Lives by 3
    5-Up                - Increases Lives by 5

*/

// Basic Health
actor DRPGBasicHealth : Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 0x7FFFFFFF
}

// Health Bonus - Health Bonuses are special and spew out random items
actor DRPGHealthBonus : CustomInventory
{
    +COUNTITEM
    +INVENTORY.ALWAYSPICKUP
    
    Inventory.PickupMessage "$GOTHTHBONUS"
    inventory.PickupSound "items/vial"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        BON1 ABCDCB 6 Bright
        Goto Spawn+1
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("AddHealthDirect", 1, 200)
        Stop
    }
}

// Stimpack
ACTOR DRPGStimpack : CustomInventory
{
    Inventory.PickupMessage "You got a Stimpack!"
    inventory.pickupsound "items/health"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        STIM A -1
        Stop
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("CheckMedkitMax"), "PickupFail")
        TNT1 A 0 ACS_NamedExecuteAlways("AddMedkit", 0, 10)
        Stop
    PickupFail:
        TNT1 A 0
        Fail
    }
}

// Medikit
ACTOR DRPGMedikit : CustomInventory
{
    Inventory.PickupMessage "You got a medium Medikit!"
    inventory.pickupsound "items/health"

    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        MEDI A -1
        Stop
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("CheckMedkitMax"), "PickupFail")
        TNT1 A 0 ACS_NamedExecuteAlways("AddMedkit", 0, 25)
        Stop
    PickupFail:
        TNT1 A 0
        Fail
    }
}

// Large Medikit
actor DRPGLargeMedikit : CustomInventory 20030
{
    Inventory.PickupMessage "You got a large Medikit!"
    inventory.pickupsound "items/health"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        MEDL A -1
        Stop
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("CheckMedkitMax"), "PickupFail")
        TNT1 A 0 ACS_NamedExecuteAlways("AddMedkit", 0, 50)
        Stop
    PickupFail:
        TNT1 A 0
        Fail
    }
}

// Extra-Large Medikit
actor DRPGXLMedikit : CustomInventory 20031
{
    Inventory.PickupMessage "You got an Extra-large Medikit!"
    inventory.pickupsound "items/health"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        MEDX A -1
        Stop
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("CheckMedkitMax"), "PickupFail")
        TNT1 A 0 ACS_NamedExecuteAlways("AddMedkit", 0, 75)
        Stop
    PickupFail:
        TNT1 A 0
        Fail
    }
}

// Med Pack
actor DRPGMedPack : CustomInventory 20032
{
    Inventory.PickupMessage "You got a Med Pack!"
    inventory.pickupsound "items/health"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        MEDP A -1
        Stop
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("CheckMedkitMax"), "PickupFail")
        TNT1 A 0 ACS_NamedExecuteAlways("AddMedkit", 0, 100)
        Stop
    PickupFail:
        TNT1 A 0
        Fail
    }
}

// Surgery Kit
actor DRPGSurgeryKit : CustomInventory 20033
{
    Inventory.PickupMessage "You got a Surgery Kit!"
    inventory.pickupsound "items/health"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        MEDS AB 8
        Goto Spawn+1
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("CheckMedkitMax"), "PickupFail")
        TNT1 A 0 ACS_NamedExecuteAlways("AddMedkit", 0, 200)
        Stop
    PickupFail:
        TNT1 A 0
        Fail
    }
}

// Medkit Refill
ACTOR DRPGMedkitRefill : CustomInventory
{
    inventory.pickupsound "items/health"

    States
    {
    Spawn:
        TNT1 A -1
        Stop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddMedkit", 0, 1000000)
        Stop
    }
}

// Soulspheres
ACTOR DRPGSoulsphere : CustomInventory
{
    +FLOATBOB
    -COUNTITEM
    
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ALWAYSPICKUP
    +INVENTORY.FANCYPICKUPSOUND
    +INVENTORY.BIGPOWERUP
    
    Inventory.PickupMessage "You got a Soulsphere!"
    Inventory.PickupSound "powerups/soulsphere"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        SOUL ABCDCB 6 Bright
        Goto Spawn+1
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("AddHealthDirect", 100, 200)
        TNT1 A 0 ACS_NamedExecuteWithResult("ClearStatusEffects")
        Stop
    }
}

ACTOR DRPGSoulsphere2 : DRPGSoulsphere 20034
{
    Translation "192 : 207 = 112 : 127", "240 : 247 = 127 : 127"
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("AddHealthDirect", 200, 200)
        TNT1 A 0 ACS_NamedExecuteWithResult("ClearStatusEffects")
        Stop
    }
}

ACTOR DRPGSoulsphere3 : DRPGSoulsphere 20035
{
    Translation "192 : 207 = 172 : 187", "240 : 247 = 191 : 191"
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("AddHealth", 200, 200)
        TNT1 A 0 ACS_NamedExecuteWithResult("ClearStatusEffects")
        Stop
    }
}

// 1-Ups
ACTOR DRPGLife : Inventory 20036
{
    -INVBAR
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Life!"
    Inventory.PickupSound "health/1up"
}

ACTOR DRPG1Up : CustomInventory 20036
{
    -INVBAR
    +FLOATBOB
    +INVENTORY.FANCYPICKUPSOUND
    
    Inventory.PickupMessage "You got a \cd1-Up!"
    Inventory.PickupSound "health/1up"
    
    Scale 0.5
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        P1UP ABCD 4 Bright
        TNT1 A 0 A_Jump(232, 2)
        TNT1 A 0 A_PlaySound("hums/extralife", CHAN_6, 4.0, false, 0.5)
        TNT1 A 0
        Goto Spawn+1
    Pickup:
        TNT1 A 0 A_GiveInventory("DRPGLife", 1)
        Stop
    }
}

ACTOR DRPG3Up : DRPG1Up
{
    Inventory.PickupMessage "You got a \ch3-Up!"
    Inventory.PickupSound "health/3up"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        P3UP ABCD 4 Bright
        TNT1 A 0 A_Jump(232, 2)
        TNT1 A 0 A_PlaySound("hums/extralife", CHAN_6, 4.0, false, 0.5)
        TNT1 A 0
        Goto Spawn+1
    Pickup:
        TNT1 A 0 A_GiveInventory("DRPGLife", 3)
        Stop
    }
}

ACTOR DRPG5Up : DRPG1Up
{
    Inventory.PickupMessage "You got a \cg5-Up!"
    Inventory.PickupSound "health/5up"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        P5UP ABCD 4 Bright
        TNT1 A 0 A_Jump(232, 2)
        TNT1 A 0 A_PlaySound("hums/extralife", CHAN_6, 4.0, false, 0.5)
        TNT1 A 0
        Goto Spawn+1
    Pickup:
        TNT1 A 0 A_GiveInventory("DRPGLife", 5)
        Stop
    }
}

// EP Capsule
actor DRPGEPCapsule : CustomInventory 20037
{
    inventory.PickupMessage "Got an EP Capsule!"
    inventory.pickupsound "health/epcapsule"
    
    Scale 0.5
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
    SpawnLoop:
        EPUP ABCDEFGHIHGFEDCB 3 Bright
        Loop
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("GetEP") >= CallACS("GetEPMax"), "SpawnLoop")
        TNT1 A 0 ACS_NamedExecuteAlways("AddEP", 0, CallACS("GetEPMax") / 10)
        Stop
    }
}

// Big EP Capsule
actor DRPGBigEPCapsule : DRPGEPCapsule 20038
{
    Scale 0.75
    
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("GetEP") >= CallACS("GetEPMax"), "SpawnLoop")
        TNT1 A 0 ACS_NamedExecuteAlways("AddEP", 0, CallACS("GetEPMax") / 4)
        Stop
    }
}
