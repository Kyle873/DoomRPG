ACTOR DRPGMarineBFG9000
{
  Health 100
  GibHealth -50
  Radius 20
  Height 56
  Speed 5
  PainChance 256
  DamageFactor "Crush", 10
  Monster
  +FLOORCLIP
  +AVOIDMELEE
  +MISSILEMORE
  -COUNTKILL
  +AMBUSH
  +NEVERRESPAWN
  +QUICKTORETALIATE
  +NOBLOCKMONST
  +NOINFIGHTING
  +DONTHARMSPECIES
  Obituary "%o was cremated to UAC standards."
  Species "Human"
  Tag "Marine"
  SeeSound "drpgmarinemale2/sight"
  PainSound "drpgmarinemale2/pain"
  DeathSound "drpgmarinemale2/death"
  ActiveSound "drpgmarinemale/active"
  AttackSound ""
  States
  {
  Spawn:
    TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NEGATIVEXP | MF_NOPAYKILL | MF_NODROPS)
    TNT1 A 0
	Goto SpawnSearch
  SpawnSearch:
    NMA9 A 3 A_Wander
    TNT1 A 0 A_Look
    NMA9 A 3 A_Wander
    TNT1 A 0 A_Look
    NMA9 B 3 A_Wander
    TNT1 A 0 A_Look
    NMA9 B 3 A_Wander
    TNT1 A 0 A_Look
    NMA9 C 3 A_Wander
    TNT1 A 0 A_Look
    NMA9 C 3 A_Wander
    TNT1 A 0 A_Look
    NMA9 D 3 A_Wander
    TNT1 A 0 A_Look
    NMA9 D 3 A_Wander
    TNT1 A 0 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineBFG9000ShotCounter", 2, "Reload")
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_JumpIfHealthLower (30, "SeeScared")
    NMA9 AABBCCDD 3 A_Chase
    Loop
  SeeScared:
    TNT1 A 0 A_JumpIfHealthLower (30, 1)
	Goto See
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    NMA9 AABBCCDD 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA9 A 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA9 B 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA9 B 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA9 C 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA9 C 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA9 D 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA9 D 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, "SeeScared")
	Goto Overwatch
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineBFG9000ShotCounter", 2, "Reload")
    TNT1 A 0 A_PlaySound("weapons/bfgf", CHAN_WEAPON)
    NMA9 EEEEEEEEEEEEEEEEEEEE 1 A_FaceTarget (10)
    NMA9 FFFFFFFFFF 1 Bright A_FaceTarget
    TNT1 A 0 A_GiveInventory ("DRPGMarineBFG9000ShotCounter", 1)
    NMA9 F 7 Bright A_CustomMissile ("DRPGMarineBFGBall", 40, 11, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    NMA9 E 23
    Goto See
  Overwatch:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineBFG9000ShotCounter", 2, "Reload")
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_Chase ("", "Missile", CHF_DONTMOVE)
    NMA9 E 4 A_FaceTarget
    TNT1 A 0 A_Chase ("", "Missile", CHF_DONTMOVE)
    NMA9 E 4 A_FaceTarget
    TNT1 A 0 A_JumpIfHealthLower (30, 2)
    TNT1 A 0 A_Jump (5, 2)
    TNT1 A 0
    Loop
    TNT1 A 0
    Goto See
  Reload:
    NMA9 E 40
    TNT1 A 0 A_PlaySound("drpgmarines/bfg9000unload", 7)
    NMA9 E 35
    TNT1 A 0 A_PlaySound("drpgmarines/bfg9000load", 7)
    TNT1 A 0 A_TakeInventory ("DRPGMarineBFG9000ShotCounter", 2)
    NMA9 E 27
    Goto See
  Pain:
    NMA9 G 3
    NMA9 G 3 A_Pain
    Goto See
  Death:
    NMA9 H 4
    TNT1 A 0 A_PlaySound("drpgmarines/smallexp", CHAN_WEAPON)
    NMA9 I 4 A_Scream
    NMA9 J 4 A_NoBlocking
    NMA9 K 4
    TNT1 A 0 A_SpawnItem ("DRPGBodyThud", 0, 0, 0, 0)
    NMA9 LM 4
    NMA9 N -1
    Stop
  XDeath:
    TNT1 A 0 A_PlaySound("drpgmarines/smallexp", CHAN_WEAPON)
    NMA9 O 4
    NMA9 P 4 A_XScream
    NMA9 Q 4 A_NoBlocking
    NMA9 PQ 4
    TNT1 A 0 A_SpawnItem ("DRPGGibThud", 0, 0, 0, 0)
    NMA9 RSTUV 4
    NMA9 W -1
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("DRPGCrushedMarine",0,0,0,0)
    Stop
  }
}

ACTOR DRPGMarineBFGBall : BFGBall
{
  Obituary "%o was cremated to UAC standards."
  DamageType "Plasma"
  +MTHRUSPECIES
  +DONTREFLECT
  +SKYEXPLODE
  States
  {
  Spawn:
    BFS1 AB 4 Bright
    Loop
  Death:
    TNT1 A 0 A_Quake (2, 40, 0, 1024, "")
    TNT1 A 0 A_Quake (6, 32, 0, 300, "")
    BFE1 AB 8 Bright
    TNT1 A 0 A_RadiusGive ("DRPGMarineBFGEffect", 256, RGF_PLAYERS, 1) 
    BFE1 C 8 Bright A_Explode (92, 256, 0, 1, 256)
    BFE1 DEF 8 Bright
    Stop
  }
}

ACTOR DRPGMarineBFGEffect : CustomInventory
{
  -COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage ""
  Inventory.PickupSound ""
  States
  {
  Spawn:
    TNT1 A 1
    Stop
  Pickup:
    TNT1 A 0 
    TNT1 A 0 A_SpawnItemEx("BFGExtra", 0, 0, 16, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  }
}

Actor DRPGMarineBFG9000ShotCounter : Inventory
{
inventory.maxamount 2
}
