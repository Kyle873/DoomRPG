ACTOR DRPGPowerSuit
{
  Health 8000
  Radius 40
  Height 110
  Mass 0x7fffffff
  Speed 12
  PainThreshold 100
  PainChance 90
  Monster
  MinMissileChance 60
  MeleeRange 90
  +BOSS
  +MISSILEMORE
  +FLOORCLIP
  +DONTMORPH
  +NOTELEOTHER
  +NOBLOOD
  +QUICKTORETALIATE
  +NOINFIGHTING
  +NOFEAR
  +SEEINVISIBLE
  +DONTHARMSPECIES
  +DONTRIP
  +DONTDRAIN
  +EXTREMEDEATH
  +NOTIMEFREEZE
  +NOTELEFRAG
  DamageFactor "Normal", 0.2
  DamageFactor "Melee", 0.2
  DamageFactor "Bullet", 0.2
  DamageFactor "Fire", 0.6
  DamageFactor "Crush", 10
  SeeSound ""
  ActiveSound "drpgpowersuit/whirr"
  PainSound "drpgpowersuit/pain"
  DeathSound "drpgpowersuit/death"
  Obituary "The UAC showed off it's powersuit tech to %o."
  Species "Marine"
  Tag "UAC Powersuit"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NEGATIVEXP | MF_NOPAYKILL | MF_NODROPS | MF_NOAURA)
    TNT1 A 0 A_PlaySound("drpgpowersuit/hum", 6, 0.3, 1, 1.3)
    DPS1 E 10 A_Look
    Loop
  See:
    TNT1 A 0 A_PlaySound("drpgpowersuit/sight", CHAN_VOICE, 1, 0, 0.4)
    TNT1 A 0 A_AlertMonsters
    DPS1 UUUUUU 10 A_FaceTarget
  WalkLoop:
    TNT1 A 0 A_PlaySound("drpgpowersuit/hum", 6, 0.3, 1, 1.3)
    TNT1 A 0 A_PlaySound("drpgpowersuit/vree", 7, 0.4, 0, 2)
    DPS1 DD 4 A_Chase
    TNT1 A 0 A_PlaySound("drpgpowersuit/step", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 300, "")
    DPS1 AA 4 A_Chase ("Melee", "Missile", CHF_DONTMOVE)
    TNT1 A 0 A_PlaySound("drpgpowersuit/vree", 7, 0.4, 0, 2)
    DPS1 BB 4 A_Chase
    TNT1 A 0 A_PlaySound("drpgpowersuit/step", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 300, "")
    DPS1 CC 4 A_Chase ("Melee", "Missile", CHF_DONTMOVE)
    Loop
  Missile:
    DPS1 VV 6 A_FaceTarget
    TNT1 A 0 A_JumpIfTargetInsideMeleeRange("Melee")
    DPS1 Y 3 Bright A_CustomMissile ("DRPGPowersuitPlasmaCannon", 45, 30, frandom(4,-4), CMF_OFFSETPITCH, frandom(1,-1))
    DPS1 Z 2 Bright A_FaceTarget
    DPS1 V 2 A_FaceTarget
    TNT1 A 0 A_MonsterRefire(0, "WalkLoop")
    Goto Missile+2
  Melee:
    TNT1 A 0 A_PlaySound("drpgpowersuit/chainsawstart", CHAN_WEAPON, 1, 0, 1.1)
    TNT1 A 0 A_Jump(80, "MeleeConstant")
  MeleeContinue:
    DPS1 VV 4 A_FaceTarget
    TNT1 A 0 A_FaceTarget
    DPS1 W 4 A_CustomMeleeAttack (random(1,3)*10,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    DPS1 W 4 A_CustomMeleeAttack (random(1,3)*10,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    DPS1 X 4 A_CustomMeleeAttack (random(1,3)*10,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    DPS1 X 4 A_CustomMeleeAttack (random(1,3)*10,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    DPS1 X 4 A_CustomMeleeAttack (random(1,3)*10,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    DPS1 X 4 A_CustomMeleeAttack (random(1,3)*10,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_MonsterRefire(0, "WalkLoop")
    TNT1 A 0 A_JumpIfTargetInsideMeleeRange("MeleeContinue")
    Goto WalkLoop
  MeleeConstant:
    DPS1 VV 3 A_FaceTarget
    TNT1 A 0 A_FaceTarget
    DPS1 U 4 A_CustomMeleeAttack (random(1,3)*8,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_MonsterRefire(0, "WalkLoop")
    TNT1 A 0 A_JumpIfTargetOutsideMeleeRange("MeleeConstantEnd")
    Goto MeleeConstant+3
  MeleeConstantEnd:
    TNT1 A 0 A_Jump(256, 1,3,5)
    DPS1 U 2 A_CustomMeleeAttack (random(1,3)*8,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    DPS1 U 2 A_CustomMeleeAttack (random(1,3)*8,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    DPS1 U 2 A_CustomMeleeAttack (random(1,3)*8,"drpgpowersuit/chainsawhit","drpgpowersuit/chainsawmiss", "Melee", 1)
    TNT1 A 0 A_FaceTarget
    Goto WalkLoop
  Pain:
    DPS1 E 10 A_Pain
    Goto WalkLoop
  Death:
    DPS1 E 5
    DPS1 E 10 A_Scream
    DPS1 E 40
    TNT1 A 0 A_StopSound(6)
    TNT1 A 0 A_NoBlocking
    TNT1 A 10 A_SpawnItemEx ("DRPGPowersuitExplosion", 0, 0, 50, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR DRPGPowersuitPlasmaCannon : BFGBall
{
  Radius 13
  Height 8
  Speed 25
  Damage 10
  DamageType "Plasma"
  Projectile
  +RANDOMIZE
  +THRUGHOST
  RenderStyle Add
  Alpha 0.85
  SeeSound "drpgpowersuit/plasmacannon"
  DeathSound "weapons/bfgx"
  States
  {
  Spawn:
    WPLZ AB 4 Bright
    Loop
  Death:
    WPLZ CDE 4 Bright
    Stop
  }
}

actor DRPGPowersuitExplosion
{
  Radius 2
  Height 2
  Speed 0
  Damage 0
  Scale 3.75
  renderstyle Add
  Projectile
  +NOCLIP
  States
  {
  Spawn:
  Death:
    TNT1 A 0
    TNT1 A 0 A_PlaySound ("drpgpowersuit/explosion")
    MISL B 8 bright
    MISL C 6 bright
    MISL D 4 bright
    stop
  }
}

ACTOR DRPGPowerSuitSlackingOffDownstairs : DRPGPowerSuit
{
  -BOSS
  -COUNTKILL
  +NODAMAGE
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 A_PlaySound("drpgpowersuit/hum", 6, 0.3, 1, 1.3)
    DPS1 A 10
    TNT1 A 0 A_Jump(16, "WanderLoop")
    TNT1 A 0 A_Jump(253, "Spawn")
    TNT1 A 0 A_PlaySound("drpgpowersuit/active", CHAN_VOICE, 1, 0, 0.8)
    Loop
  WanderLoop:
    TNT1 A 0 A_PlaySound("drpgpowersuit/hum", 6, 0.3, 1, 1.3)
    TNT1 A 0 A_PlaySound("drpgpowersuit/vree", 7, 0.4, 0, 2)
    DPS1 DD 4 A_Wander
    TNT1 A 0 A_PlaySound("drpgpowersuit/step", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 300, "")
    DPS1 AA 4 A_Wander
    TNT1 A 0 A_Jump(20, "Spawn")
    TNT1 A 0 A_PlaySound("drpgpowersuit/vree", 7, 0.4, 0, 2)
    DPS1 BB 4 A_Wander
    TNT1 A 0 A_PlaySound("drpgpowersuit/step", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 300, "")
    DPS1 CC 4 A_Wander
    TNT1 A 0 A_Jump(20, "Spawn")
    TNT1 A 0 A_Jump(253, "WanderLoop")
    TNT1 A 0 A_PlaySound("drpgpowersuit/active", CHAN_VOICE, 1, 0, 0.8)
    Loop
  Enraged:
    TNT1 A 0 A_SpawnItemEx("DRPGBigTeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 35
    Stop
  }
}

ACTOR DRPGBigTeleportFog
{
  +NOBLOCKMAP
  +NOTELEPORT
  +NOGRAVITY
  RenderStyle Add
  Scale 2.5
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_PlaySound("misc/teleport", CHAN_VOICE, 1, 0, 0.5)
    TFOG ABABCDEFGHIJ 6 Bright
    Stop
  }
}

ACTOR DRPGHugeTeleportFog : DRPGBigTeleportFog
{
  Scale 4
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_PlaySound("misc/teleport", CHAN_VOICE, 1, 0, 0.25)
    TFOG ABABCDEFGHIJ 6 Bright
    Stop
  }
}

ACTOR DRPGHeavyPowerSuit
{
  Health 50000
  Radius 68
  Height 140
  Mass 0x7fffffff
  Speed 12
  PainThreshold 500
  PainChance 256
  Monster
  MinMissileChance 0
  Scale 0.75
  +BOSS
  +MISSILEMORE
  +MISSILEEVENMORE
  +FLOORCLIP
  +DONTMORPH
  +NOTELEOTHER
  +NOBLOOD
  +LOOKALLAROUND
  +QUICKTORETALIATE
  +NOINFIGHTING
  +NOFEAR
  +SEEINVISIBLE
  +DONTHARMSPECIES
  +DONTRIP
  +DONTDRAIN
  +NOTIMEFREEZE
  +NOTELEFRAG
  DamageFactor "Normal", 0.2
  DamageFactor "Melee", 0.2
  DamageFactor "Bullet", 0.2
  DamageFactor "Fire", 0.6
  DamageFactor "Crush", 10
  SeeSound ""
  ActiveSound "drpgpowersuit/whirr"
  PainSound "drpgheavypowersuit/pain"
  DeathSound "drpgpowersuit/death"
  Obituary "The UAC ran out of patience with %o."
  Species "Marine"
  Tag "UAC Heavy Powersuit"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NEGATIVEXP | MF_NOPAYKILL | MF_NODROPS | MF_NOAURA)
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/hum", 6, 0.3, 1, 1.3)
    TNT1 A 0 HealThing(250)
    DPS2 A 10 A_Look
    Goto SearchWalkLoop
  SearchWalkLoop:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/hum", 6, 0.3, 1, 1.3)
    TNT1 A 0 A_Look
    DPS2 E 4 A_Wander
    TNT1 A 0 A_Look
    DPS2 E 4 A_Wander
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/step", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 700, "")
    TNT1 A 0 A_Quake(4, 6, 0, 300, "")
    DPS2 BBB 4 A_Look
    TNT1 A 0 A_Look
    DPS2 C 4 A_Wander
    TNT1 A 0 A_Look
    DPS2 C 4 A_Wander
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/step", 7, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 700, "")
    TNT1 A 0 A_Quake(4, 6, 0, 300, "")
    DPS2 DDD 4 A_Look
    TNT1 A 0 A_Jump(10, "PlasmaDisruptorSpray", "MissilePack")
    Loop
  See:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/sight", CHAN_VOICE, 1, 0, 0.4)
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_JumpIfInventory("DRPGHeavyPowersuitActivated", 1, "WalkLoop")
    TNT1 A 0 A_GiveInventory("DRPGHeavyPowersuitActivated", 1)
    DPS2 AAAAAA 10 A_FaceTarget
  WalkLoop:
    TNT1 A 0 A_JumpIfTargetInLOS(7, 0)
    TNT1 A 0 A_Jump(200, 6)
    TNT1 A 0 A_JumpIfInTargetInventory("RLPlasmaShieldArmorToken", 1, "MissilePack")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemPlasma", 1, "MissilePack")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemFire", 1, "PlasmaDisruptorSpray")
    TNT1 A 0 A_JumpIfInTargetInventory("RLInquisitorsSetBonusActive", 1, "PlasmaDisruptorSpray")
    TNT1 A 0 A_Jump(256, "MissilePack", "PlasmaDisruptorSpray")
	Goto MissilePack
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/hum", 6, 0.3, 1, 1.3)
    DPS2 EE 4 A_Chase
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/step", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 700, "")
    TNT1 A 0 A_Quake(4, 6, 0, 300, "")
    DPS2 BBB 4 A_Chase ("", "Missile", CHF_DONTMOVE)
    DPS2 CC 4 A_Chase
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/step", 7, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 700, "")
    TNT1 A 0 A_Quake(4, 6, 0, 300, "")
    DPS2 DDD 4 A_Chase ("", "Missile", CHF_DONTMOVE)
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInTargetInventory("PowerInvulnerable", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityBullet", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityMelee", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityFire", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityPlasma", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("RLPlasmaShieldArmorToken", 1, "PlasmaResistance")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemBullet", 1, "BulletResistance")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemFire", 1, "FireResistance")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemPlasma", 1, "PlasmaResistance")
    TNT1 A 0 A_JumpIfInTargetInventory("RLInquisitorsSetBonusActive", 1, "FireResistance")
    TNT1 A 0 A_Jump(170, "Miniguns", "PlasmaDisruptor")
    Goto MissilePack
  PlasmaResistance:
    TNT1 A 0 A_Jump(256, "Miniguns", "MissilePack")
    Goto WalkLoop
  BulletResistance:
    TNT1 A 0 A_Jump(256, "PlasmaDisruptor", "MissilePack")
    Goto WalkLoop
  FireResistance:
    TNT1 A 0 A_Jump(256, "Miniguns", "PlasmaDisruptor")
    Goto WalkLoop
  PlasmaDisruptor:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/chargeup", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 GGGG 8 A_FaceTarget
    TNT1 A 0 A_StopSound(CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInTargetInventory("PowerInvulnerable", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityBullet", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityMelee", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityFire", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityPlasma", 1, "AntiDemonField")
    TNT1 A 0 A_FaceTarget
    DPS2 H 2 Bright A_CustomMissile ("DRPGHeavyPowersuitPlasmaDisruptor", 60, 0, frandom(2,-2), CMF_OFFSETPITCH, frandom(2,-2))
    DPS2 G 1 A_FaceTarget
    TNT1 A 0 A_MonsterRefire(0, "WalkLoop")
    Goto PlasmaDisruptor+6
  PlasmaDisruptorSpray:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/chargeup", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 GGGG 8
    TNT1 A 0 A_StopSound(CHAN_WEAPON)
    DPS2 H 2 Bright A_CustomMissile ("DRPGHeavyPowersuitPlasmaDisruptor", 60, 0, frandom(35,-35), CMF_AIMDIRECTION, frandom(7,-7))
    DPS2 G 1
    TNT1 A 0 A_Jump(12, "CheckNewTargets")
    Goto PlasmaDisruptorSpray+6
  Miniguns:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigunwindup", 5, 1, 0, 0.8)
    DPS2 AAAAAA 4 A_FaceTarget
  MinigunsFiring:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigunspin", 5, 1, 1, 0.8)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigun", CHAN_WEAPON, 1, 0, 0.3)
    DPS2 K 1 Bright A_CustomBulletAttack (16, 5, 1, random(3,6) * 10, "DRPGMarineMinigunLowKnockbackBulletPuff", 0, CBAF_NORANDOM)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigun", CHAN_WEAPON, 1, 0, 0.3)
    DPS2 J 1 Bright A_CustomBulletAttack (16, 5, 1, random(3,6) * 10, "DRPGMarineMinigunLowKnockbackBulletPuff", 0, CBAF_NORANDOM)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigun", CHAN_WEAPON, 1, 0, 0.3)
    DPS2 I 1 Bright A_CustomBulletAttack (16, 5, 1, random(3,6) * 10, "DRPGMarineMinigunLowKnockbackBulletPuff", 0, CBAF_NORANDOM)
    TNT1 A 0 A_JumpIfInTargetInventory("PowerInvulnerable", 1, "MinigunsCeaseFire")
    TNT1 A 0 A_MonsterRefire(200, "MinigunsCeaseFire")
    Goto MinigunsFiring
  MinigunsCeaseFire:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigunwinddown", 5, 1, 0, 0.8)
    DPS2 AA 4 A_FaceTarget
	Goto WalkLoop
  MissilePack:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/missilehatch", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 L 20
    DPS2 L 12 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 10 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 8 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 6 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 4 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 2 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    TNT1 A 0 A_Jump(256, 1,2,3,4,5,6,7,8,9)
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 145, -32, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 140, -32, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 145, -40, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 140, -40, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 145, -48, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 140, -48, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 145, -56, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 140, -56, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 10
    TNT1 A 0 A_Jump(256, 10)
    TNT1 A 0 A_Jump(256, 1,2,3,4,5,6,7,8,9)
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 110, -90, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 110, -95, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 102, -90, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 102, -95, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 94, -90, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 94, -95, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 86, -90, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile", 86, -95, frandom(1,-1), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 15
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/missilehatch", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 A 10
  CheckNewTargets:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_Look
    Goto WalkLoop
  AntiDemonField:
    DPS2 FFFF 8
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitAntiDemonField", 0, 0, 16, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    DPS2 FF 8
    Goto WalkLoop
  Pain:
    TNT1 A 0 A_StopSound(5)
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_Look
    DPS2 F 10 A_Pain
    Goto WalkLoop
  Death:
    DPS2 F 5
    TNT1 A 0 A_Scream
    TNT1 A 0 A_StopSound(6)
    DPS2 FFFFFFFFFF 4 A_SpawnItemEx ("DRPGOutpostTurretExplosion", random(50,-50), random(50,-50), random(90,10), 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/death", 7, 1, 0, 0.6)
    DPS2 NOPQ 5
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/falldown", 5, 0.8, 0, 0.1)
    TNT1 A 0 A_Quake(1, 8, 0, 1600, "")
    TNT1 A 0 A_Quake(3, 8, 0, 900, "")
    TNT1 A 0 A_Quake(9, 8, 0, 300, "")
    DPS2 R -1
    Stop
  }
}

ACTOR DRPGHeavyPowerSuitHavingASiesta : DRPGHeavyPowerSuit
{
  -BOSS
  -COUNTKILL
  +NODAMAGE
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/hum", 6, 0.3, 1, 1.3)
    DPS2 F 10
    Loop
  Enraged:
    TNT1 A 0 A_SpawnItemEx("DRPGBigTeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 35
    Stop
  }
}

ACTOR DRPGHeavyPowersuitPlasmaDisruptor : BFGBall
{
  Radius 13
  Height 8
  Speed 40
  Damage (random(4,6) * 100)
  DamageType "Plasma"
  Projectile
  +RANDOMIZE
  +THRUGHOST
  RenderStyle Add
  Alpha 0.85
  Scale 0.6
  ProjectileKickBack 10
  SeeSound "drpgheavypowersuit/plasmadisruptor"
  DeathSound "drpgheavypowersuit/plasmadisruptorhit"
  States
  {
  Spawn:
    TNT1 A 0
    NMBX ABCDEFGH 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Loop
  Death:
    TNT1 A 0 A_SetScale (2)
    TNT1 A 0 A_Explode (128, 192, "None")
    NMBX IJK 4 Bright
    TNT1 A 0 A_Explode (100, 288, "None", 0, 128)
    NMBX L 2 Bright A_SetScale (2.8, 1)
    TNT1 A 0 A_Explode (80, 384, "None", 0, 144)
    NMBX L 2 Bright A_SetScale (3.6, 1)
    TNT1 A 0 A_Explode (60, 384, "None", 0, 160)
    NMBX M 2 Bright A_SetScale (4.4, 1)
    TNT1 A 0 A_Explode (30, 384, "None", 0, 176)
    NMBX M 2 Bright A_SetScale (5.2, 1)
    TNT1 A 0 A_Explode (10, 384, "None", 0, 192)
    NMBX N 2 Bright A_SetScale (6.0, 1)
    TNT1 A 0 A_Explode (5, 384, "None", 0, 208)
    NMBX N 2 Bright A_SetScale (6.8, 1)
    Stop
  }
}

ACTOR DRPGHeavyPowersuitPlasmaDisruptorGlow
{
  +NOBLOCKMAP
  +NOGRAVITY
  RenderStyle Add
  Alpha 0.6
  Scale 0.6
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_Jump(256, 1,2,3,4,5,6,7,8)
    NMBX ABCDEFGH 1 Bright A_FadeTo (0, 0.04, 1) 
    Goto Spawn+1
  }
}

ACTOR DRPGHeavyPowersuitMissile : RevenantTracer
{
   +THRUGHOST
   +SEEKERMISSILE
   +DONTSEEKINVISIBLE
   RenderStyle Normal
   Speed 16
   DamageType "Fire"
   SeeSound "drpgheavypowersuit/missile"
   DeathSound "drpgmarines/smallexp"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_PlaySound("drpgheavypowersuit/missileloop", CHAN_BODY, 0.6, 1, 1.2)
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright
	Goto MissileLockCheck
  NotSeeking:
    TNT1 A 0 A_JumpIfTargetInLOS ("Seeking", 0, JLOSF_PROJECTILE | JLOSF_DEADNOJUMP)
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/missileloop", CHAN_BODY, 0.6, 1, 1.2)
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright A_SeekerMissile (0, 0, SMF_LOOK, 256, 50)
    Goto NotSeeking
  Seeking:
    TNT1 A 0
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/missileloop", CHAN_BODY, 0.6, 1, 1.2)
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitMissileTrail", 16, 0, 8, 0, 0, 0, 180, SXF_NOCHECKPOSITION)
    CREM A 1 Bright A_SeekerMissile (4, 6, SMF_LOOK | SMF_PRECISE | SMF_CURSPEED, 256, 10)
    Goto NotSeeking
  Death:
    TNT1 A 0 A_StopSound(CHAN_BODY)
    TNT1 A 0 A_SetTranslucent (0.80, 1)
    TNT1 A 0 A_Explode (128, 128)
    MISL B 8 Bright
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  MissileLockCheck:
// Welcome to the cunning zone, thanks to FishyClockwork!
    TNT1 A 0 A_Jump (256, 1,3,5,7,9,11,13,15)
      TNT1 A 0 A_TransferPointer(AAPTR_PLAYER1, AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", "NotSeeking", AAPTR_TRACER)
	  Goto NotSeeking

      TNT1 A 0 A_TransferPointer(AAPTR_PLAYER2, AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", "NotSeeking", AAPTR_TRACER)
	  Goto NotSeeking

      TNT1 A 0 A_TransferPointer(AAPTR_PLAYER3, AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", "NotSeeking", AAPTR_TRACER)
	  Goto NotSeeking

      TNT1 A 0 A_TransferPointer(AAPTR_PLAYER4, AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", "NotSeeking", AAPTR_TRACER)
	  Goto NotSeeking

      TNT1 A 0 A_TransferPointer(AAPTR_PLAYER5, AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", "NotSeeking", AAPTR_TRACER)
	  Goto NotSeeking

      TNT1 A 0 A_TransferPointer(AAPTR_PLAYER6, AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", "NotSeeking", AAPTR_TRACER)
	  Goto NotSeeking

      TNT1 A 0 A_TransferPointer(AAPTR_PLAYER7, AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", "NotSeeking", AAPTR_TRACER)
	  Goto NotSeeking

      TNT1 A 0 A_TransferPointer(AAPTR_PLAYER8, AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", "NotSeeking", AAPTR_TRACER)
	  Goto NotSeeking
  }
}

ACTOR DRPGHeavyPowersuitMissileTrail
{
   Projectile
   Radius 8
   Height 8
   Speed 1
   Scale 0.4
   Damage 0
   RENDERSTYLE ADD
   Alpha 0.75
   +NOCLIP
   +NOINTERACTION
   +NOTIMEFREEZE
   States
   {
   Spawn:
   CRES ABCDE 3 A_SetScale (ScaleX+0.2)
   Stop
   }
}

ACTOR DRPGHeavyPowersuitAntiDemonField
{
   Radius 8
   Height 8
   Speed 0
   reactiontime 2100
   +NOCLIP
   +NOINTERACTION
   +NOTIMEFREEZE
   States
   {
   Spawn:
   TNT1 A 0 NoDelay A_PlaySound("drpgheavypowersuit/antidemonfield", CHAN_BODY, 1, 0, 0.6)
   TNT1 A 1 A_CountDown
   TNT1 AAAAA 0 A_SpawnItemEx ("DRPGAntiDemonFieldParticle", random(1000,-1000), random(1000,-1000), random(62,1), 0, 0, 0, 0, SXF_NOCHECKPOSITION)
   Goto Spawn+1
   Death:
   TNT1 A 5
   Stop
   }
}

ACTOR DRPGAntiDemonFieldEffect : CustomInventory
{
  -COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage ""
  Inventory.PickupSound ""
  States
  {
  Spawn:
    TNT1 A 1
    Stop
  Pickup:
    TNT1 A 0
    TNT1 A 0 A_TakeInventory("PowerInvulnerable", 1)
    TNT1 A 0 ACS_NamedExecuteAlways("DestroyImmunity", 0)
    Stop
  }
}

ACTOR DRPGAntiDemonFieldParticle
{
   Projectile
   Radius 8
   Height 8
   VSpeed 1
   Damage 0
   RENDERSTYLE ADD
   Alpha 0.85
   +NOCLIP
   +NOINTERACTION
   +NOTIMEFREEZE
   States
   {
   Spawn:
   TNT1 A 0 NoDelay A_SetScale (frandom(2.8,0.6))
   TNT1 A 0 A_RadiusGive("DRPGAntiDemonFieldEffect", 1000, RGF_PLAYERS, 1)
   ADFP AAABBBBCCCCCD 2 Bright A_FadeTo (0, frandom(0.12,0.01), 1)
   SpawnLoop:
   ADFP D 2 Bright A_FadeTo (0, frandom(0.12,0.01), 1)
   Goto SpawnLoop
   }
}

Actor DRPGHeavyPowersuitActivated : Inventory
{
inventory.maxamount 1
}

ACTOR DRPGSuperPowerSuit
{
  Health 1000000
  Radius 68
  Height 140
  Mass 0x7fffffff
  Speed 12
  PainThreshold 1500
  PainChance 30
  Monster
  MinMissileChance 0
  Scale 0.75
  +BOSS
  +MISSILEMORE
  +MISSILEEVENMORE
  +FLOORCLIP
  +DONTMORPH
  +NOTELEOTHER
  +NOBLOOD
  +LOOKALLAROUND
  +QUICKTORETALIATE
  +NOINFIGHTING
  +NOFEAR
  +SEEINVISIBLE
  +DONTHARMSPECIES
  +DONTRIP
  +DONTDRAIN
  +ALWAYSFAST
  +NOTIMEFREEZE
  +NOTELEFRAG
  DamageFactor "Normal", 0.2
  DamageFactor "Melee", 0.02
  DamageFactor "Bullet", 0.02
  DamageFactor "Fire", 0.2
  DamageFactor "Plasma", 0.4
  DamageFactor "Crush", 10
  SeeSound ""
  ActiveSound "drpgpowersuit/whirr"
  PainSound "drpgheavypowersuit/pain"
  DeathSound "drpgpowersuit/death"
  Obituary "%o thought %g was invincible. Well, %g thought wrong."
  Species "Marine"
  Tag "UAC Heavy Powersuit Mk. II"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NEGATIVEXP | MF_NOPAYKILL | MF_NODROPS | MF_NOAURA)
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/hum", 6, 0.3, 1, 1.3)
    TNT1 A 0 HealThing(random(25,2000))
    DPS2 A 1 A_Look
    Goto SearchWalkLoop
  SearchWalkLoop:
    TNT1 A 0 HealThing(random(25,500))
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/hum", 6, 0.3, 1, 1.3)
    TNT1 A 0 A_Look
    DPS2 E 4 A_Wander
    TNT1 A 0 A_Look
    DPS2 E 4 A_Wander
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/step", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 700, "")
    TNT1 A 0 A_Quake(4, 6, 0, 300, "")
    DPS2 BBB 4 A_Look
    TNT1 A 0 A_Look
    DPS2 C 4 A_Wander
    TNT1 A 0 A_Look
    DPS2 C 4 A_Wander
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/step", 7, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 700, "")
    TNT1 A 0 A_Quake(4, 6, 0, 300, "")
    DPS2 DDD 4 A_Look
    TNT1 A 0 A_Jump(75, "PlasmaDisruptorSpray", "MissilePack")
    Loop
  See:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/sight", CHAN_VOICE, 1, 0, 0.4)
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_JumpIfInventory("DRPGHeavyPowersuitActivated", 1, "WalkLoop")
    TNT1 A 0 A_GiveInventory("DRPGHeavyPowersuitActivated", 1)
    DPS2 AAAAAA 10 A_FaceTarget
  WalkLoop:
    TNT1 A 0 A_JumpIfTargetInLOS(7, 0)
    TNT1 A 0 A_Jump(200, 6)
    TNT1 A 0 A_JumpIfInTargetInventory("RLPlasmaShieldArmorToken", 1, "MissilePack")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemPlasma", 1, "MissilePack")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemFire", 1, "PlasmaDisruptorSpray")
    TNT1 A 0 A_JumpIfInTargetInventory("RLInquisitorsSetBonusActive", 1, "PlasmaDisruptorSpray")
    TNT1 A 0 A_Jump(220, "PlasmaDisruptorSpray")
	Goto MissilePack
    TNT1 A 0 HealThing(random(25,500))
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/hum", 6, 0.3, 1, 1.3)
    DPS2 EE 3 A_Chase
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/step", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 700, "")
    TNT1 A 0 A_Quake(4, 6, 0, 300, "")
    DPS2 BBB 3 A_Chase ("", "Missile", CHF_DONTMOVE)
    DPS2 CC 3 A_Chase
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/step", 7, 1, 0, 0.7)
    TNT1 A 0 A_Quake(1, 6, 0, 700, "")
    TNT1 A 0 A_Quake(4, 6, 0, 300, "")
    DPS2 DDD 3 A_Chase ("", "Missile", CHF_DONTMOVE)
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInTargetInventory("PowerInvulnerable", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityBullet", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityMelee", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityFire", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityPlasma", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfHealthLower(50000, "PlasmaScattershot")
    TNT1 A 0 A_JumpIfInTargetInventory("RLPlasmaShieldArmorToken", 1, "PlasmaResistance")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemBullet", 1, "BulletResistance")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemFire", 1, "FireResistance")
    TNT1 A 0 A_JumpIfInTargetInventory("PowerRLReactiveShieldSystemPlasma", 1, "PlasmaResistance")
    TNT1 A 0 A_JumpIfInTargetInventory("RLInquisitorsSetBonusActive", 1, "FireResistance")
    TNT1 A 0 A_Jump(160, "PlasmaDisruptor")
    TNT1 A 0 A_Jump(210, "Miniguns", "PlasmaDisruptor")
    Goto MissilePack
  PlasmaResistance:
    TNT1 A 0 A_Jump(256, "Miniguns", "MissilePack")
    Goto WalkLoop
  BulletResistance:
    TNT1 A 0 A_Jump(256, "PlasmaDisruptor", "MissilePack")
    Goto WalkLoop
  FireResistance:
    TNT1 A 0 A_Jump(256, "Miniguns", "PlasmaDisruptor")
    Goto WalkLoop
  PlasmaDisruptor:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/chargeup", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 G 8 A_FaceTarget
    TNT1 A 0 A_StopSound(CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInTargetInventory("PowerInvulnerable", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityBullet", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityMelee", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityFire", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityPlasma", 1, "AntiDemonField")
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("drpgheavypowersuitmkii/plasmadisruptormkii", 7, 1, 0, 0.4)
    DPS2 H 6 Bright A_CustomMissile ("DRPGHeavyPowersuitPlasmaDisruptor2", 60, 0, frandom(2,-2), CMF_OFFSETPITCH, frandom(2,-2))
    DPS2 G 8 A_FaceTarget
    Goto WalkLoop
  PlasmaDisruptorSpray:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/chargeup", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 G 8
    TNT1 A 0 A_StopSound(CHAN_WEAPON)
    TNT1 A 0 A_PlaySound("drpgheavypowersuitmkii/plasmadisruptormkii", 7, 1, 0, 0.4)
    DPS2 H 6 Bright A_CustomMissile ("DRPGHeavyPowersuitPlasmaDisruptor2", 60, 0, frandom(2,-2), CMF_AIMDIRECTION, frandom(9,-9))
    TNT1 A 0 A_MonsterRefire(0, "PlasmaDisruptorSprayContinue")
	Goto PlasmaDisruptor
  PlasmaDisruptorSprayContinue:
    DPS2 GG 8
    TNT1 A 0 A_Jump(256, 1,2)
    TNT1 A 0 A_SetAngle(Angle+random(10,45))
	Goto PlasmaDisruptorSprayContinuePos
    TNT1 A 0 A_SetAngle(Angle-random(10,45))
	Goto PlasmaDisruptorSprayContinueNeg
  PlasmaDisruptorSprayContinuePos:
    DPS2 G 8
    TNT1 A 0 A_SetAngle(Angle+random(10,45))
	Goto PlasmaDisruptorSprayContinueEnd
  PlasmaDisruptorSprayContinueNeg:
    DPS2 G 8
    TNT1 A 0 A_SetAngle(Angle-random(10,45))
	Goto PlasmaDisruptorSprayContinueEnd
  PlasmaDisruptorSprayContinueEnd:
    DPS2 GG 8
    TNT1 A 0 A_Jump(50, "CheckNewTargets")
    Goto PlasmaDisruptorSpray+3
  PlasmaScattershot:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/chargeup", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 G 8 A_FaceTarget
    TNT1 A 0 A_StopSound(CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInTargetInventory("PowerInvulnerable", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityBullet", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityMelee", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityFire", 1, "AntiDemonField")
    TNT1 A 0 A_JumpIfInTargetInventory("DRPGPowerImmunityPlasma", 1, "AntiDemonField")
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("drpgheavypowersuitmkii/plasmadisruptormkii", 7, 1, 0, 0.4)
    TNT1 AAAA 0 A_CustomMissile ("DRPGHeavyPowersuitPlasmaDisruptor2", 60, 0, frandom(15,-15), CMF_OFFSETPITCH, frandom(10,-10))
    DPS2 H 6 Bright A_CustomMissile ("DRPGHeavyPowersuitPlasmaDisruptor2", 60, 0, frandom(2,-2), CMF_OFFSETPITCH, frandom(2,-2))
    DPS2 GGGGGGGGGGGG 8 A_FaceTarget
    TNT1 A 0 A_MonsterRefire(0, "PlasmaDisruptorSpray")
    Goto PlasmaScattershot+3
  Miniguns:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigunwindup", 5, 1, 0, 0.8)
    DPS2 AA 4 A_FaceTarget
  MinigunsFiring:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigunspin", 5, 1, 1, 0.8)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigun", CHAN_WEAPON, 1, 0, 0.3)
    DPS2 K 1 Bright A_CustomBulletAttack (8, 3, 1, random(110,865) * 10, "DRPGMarineMinigunLowKnockbackBulletPuff", 0, CBAF_NORANDOM)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigun", CHAN_WEAPON, 1, 0, 0.3)
    DPS2 J 1 Bright A_CustomBulletAttack (8, 3, 1, random(110,865) * 10, "DRPGMarineMinigunLowKnockbackBulletPuff", 0, CBAF_NORANDOM)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigun", CHAN_WEAPON, 1, 0, 0.3)
    DPS2 I 1 Bright A_CustomBulletAttack (8, 3, 1, random(110,865) * 10, "DRPGMarineMinigunLowKnockbackBulletPuff", 0, CBAF_NORANDOM)
    TNT1 A 0 A_JumpIfInTargetInventory("PowerInvulnerable", 1, "MinigunsCeaseFire")
    TNT1 A 0 A_MonsterRefire(200, "MinigunsCeaseFire")
    Goto MinigunsFiring
  MinigunsCeaseFire:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/minigunwinddown", 5, 1, 0, 0.8)
    DPS2 AA 4 A_FaceTarget
	Goto WalkLoop
  MissilePack:
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/missilehatch", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 L 2
    DPS2 L 12 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 10 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 8 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 6 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 4 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 2 A_PlaySound("drpgheavypowersuit/missilelock", 5, 0.7, 0, 1.6)
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -32, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -32, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -40, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -40, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -48, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -48, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -56, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -56, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 10
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 110, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 110, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 102, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 102, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 94, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 94, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 86, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 86, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 10 A_MonsterRefire(0, "MissilePackContinue1")
	Goto MissilePackEnd
  MissilePackContinue1:
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -32, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -32, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -40, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -40, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -48, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -48, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -56, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -56, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 10 A_MonsterRefire(0, "MissilePackContinue2")
	Goto MissilePackEnd
  MissilePackContinue2:
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 110, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 110, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 102, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 102, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 94, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 94, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 86, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 86, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 10 A_MonsterRefire(0, "MissilePackContinue3")
	Goto MissilePackEnd
  MissilePackContinue3:
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -32, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -32, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -40, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -40, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -48, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -48, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 145, -56, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 140, -56, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 10 A_MonsterRefire(0, "MissilePackContinue4")
	Goto MissilePackEnd
  MissilePackContinue4:
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 110, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 110, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 102, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 102, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 94, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 94, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 86, -90, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
    DPS2 L 4 Bright A_CustomMissile ("DRPGHeavyPowersuitMissile2", 86, -95, frandom(8,-8), CMF_AIMDIRECTION, frandom(1,-1))
  MissilePackEnd:
    DPS2 L 10
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/missilehatch", CHAN_WEAPON, 1, 0, 0.6)
    DPS2 A 2
  CheckNewTargets:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_Look
    Goto WalkLoop
  AntiDemonField:
    DPS2 FFFF 8
    TNT1 A 0 A_SpawnItemEx ("DRPGHeavyPowersuitAntiDemonField", 0, 0, 16, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    DPS2 FF 8
    Goto WalkLoop
  Pain:
    TNT1 A 0 A_StopSound(5)
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_Look
    DPS2 F 10 A_Pain
    Goto WalkLoop
  Death:
    DPS2 F 5
    TNT1 A 0 A_Scream
    TNT1 A 0 A_StopSound(6)
    DPS2 FFFFFFFFFF 4 A_SpawnItemEx ("DRPGOutpostTurretExplosion", random(50,-50), random(50,-50), random(90,10), 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/death", 7, 1, 0, 0.6)
    DPS2 NOPQ 5
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_PlaySound("drpgheavypowersuit/falldown", 5, 0.8, 0, 0.1)
    TNT1 A 0 A_Quake(1, 8, 0, 1600, "")
    TNT1 A 0 A_Quake(3, 8, 0, 900, "")
    TNT1 A 0 A_Quake(9, 8, 0, 300, "")
    DPS2 R -1
    Stop
  }
}

ACTOR DRPGHeavyPowersuitPlasmaDisruptor2 : DRPGHeavyPowersuitPlasmaDisruptor
{
  +NOTIMEFREEZE
  Speed 28
  Damage (random(66,666) * 666)
  SeeSound ""
  DeathSound ""
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_PlaySound("drpgheavypowersuitmkii/plasmadisruptorloopmkii", 6, 0.8, 1, 0.8)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX A 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX B 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX C 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX D 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX E 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX F 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX G 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX H 1 Bright A_SpawnItemEx("DRPGHeavyPowersuitPlasmaDisruptorGlow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Loop
  Death:
    TNT1 A 0 A_PlaySound("drpgheavypowersuitmkii/plasmadisruptorhitmkii", CHAN_WEAPON, 1, 0, 0.1)
    TNT1 A 0 A_StopSound(6)
    TNT1 A 0 A_Quake(4, 14, 0, 2048, "")
    TNT1 A 0 A_Quake(9, 14, 0, 512, "")
    TNT1 A 0 A_SetScale (4)
    TNT1 A 0 A_Explode (1280, 768, "None")
	TNT1 AAAAAAA 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX I 2 Bright
    TNT1 A 0 A_Explode (1280, 768, "None")
	TNT1 AAAA 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX I 2 Bright
    TNT1 A 0 A_Explode (1280, 768, "None")
	TNT1 AA 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX J 2 Bright
    TNT1 A 0 A_Explode (1280, 768, "None")
	TNT1 AA 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX J 2 Bright
    TNT1 A 0 A_Explode (1280, 768, "None")
	TNT1 AAA 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX K 2 Bright
    TNT1 A 0 A_Explode (1280, 768, "None")
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX K 2 Bright
    TNT1 A 0 A_Explode (1000, 512, "None", 0, 128)
	TNT1 AA 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX L 2 Bright A_SetScale (5.8, 1)
    TNT1 A 0 A_Explode (800, 512, "None", 0, 144)
	TNT1 A 0 A_CustomRailgun (0, 0, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 0, 25, "DRPGPlasmaDisruptorLightningPuff", frandom(0,359), frandom(-45,45), random(64,512), 1, 0.2, 0, "None", 0)
    NMBX L 2 Bright A_SetScale (6.6, 1)
    TNT1 A 0 A_Explode (600, 512, "None", 0, 160)
    NMBX M 2 Bright A_SetScale (7.4, 1)
    TNT1 A 0 A_Explode (300, 512, "None", 0, 176)
    NMBX M 2 Bright A_SetScale (8.2, 1)
    TNT1 A 0 A_Explode (100, 512, "None", 0, 192)
    NMBX N 2 Bright A_SetScale (9.0, 1)
    TNT1 A 0 A_Explode (50, 512, "None", 0, 208)
    NMBX N 2 Bright A_SetScale (10.8, 1)
    Stop
  }
}

ACTOR DRPGPlasmaDisruptorLightningPuff : BulletPuff
{
  -ALLOWPARTICLES
  -RANDOMIZE
  +PUFFONACTORS
  +ALWAYSPUFF
  +PUFFGETSOWNER
  +THRUGHOST
  +BLOODLESSIMPACT
  +NOTIMEFREEZE
  Damagetype "NullDamage"
  VSpeed 0
  Mass 5
  States
  {
  Spawn:
    TNT1 A 0
	Stop
  Melee:
    Goto Spawn
  }
}

ACTOR DRPGHeavyPowersuitMissile2 : DRPGHeavyPowersuitMissile
{
   -DONTSEEKINVISIBLE
   +NOTIMEFREEZE
   Damage 400
   Speed 38
   DeathSound ""
  States
  {
  Death:
    TNT1 A 0 A_Quake(2, 3, 0, 1024, "")
    TNT1 A 0 A_Quake(7, 3, 0, 350, "")
    TNT1 A 0 A_StopSound(CHAN_BODY)
    TNT1 A 0 A_PlaySound("drpgmarines/smallexp", CHAN_WEAPON, 1, 0, 0.6)
    TNT1 A 0 A_SetScale (3)
    TNT1 A 0 A_SetTranslucent (0.80, 1)
    TNT1 A 0 A_Explode (1028, 256, "None")
    MISL B 8 Bright
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  }
}
