ACTOR DRPGMarineRocketLauncher
{
  Health 100
  GibHealth -50
  Radius 20
  Height 56
  Speed 7
  PainChance 256
  DamageFactor "Crush", 10
  Monster
  +FLOORCLIP
  +AVOIDMELEE
  +MISSILEMORE
  -COUNTKILL
  +AMBUSH
  +NEVERRESPAWN
  +QUICKTORETALIATE
  +NOBLOCKMONST
  +NOINFIGHTING
  +DONTHARMSPECIES
  Obituary "The UAC was forced to fire %o."
  Species "Human"
  Tag "Marine"
  DamageFactor "Fire", 0.5
  SeeSound "drpgmarinemale1/sight"
  PainSound "drpgmarinemale1/pain"
  DeathSound "drpgmarinemale1/death"
  ActiveSound "drpgmarinemale/active"
  AttackSound ""
  States
  {
  Spawn:
    TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NEGATIVEXP | MF_NOPAYKILL | MF_NODROPS)
    TNT1 A 0
	Goto SpawnSearch
  SpawnSearch:
    NMA7 A 3 A_Wander
    TNT1 A 0 A_Look
    NMA7 A 3 A_Wander
    TNT1 A 0 A_Look
    NMA7 B 3 A_Wander
    TNT1 A 0 A_Look
    NMA7 B 3 A_Wander
    TNT1 A 0 A_Look
    NMA7 C 3 A_Wander
    TNT1 A 0 A_Look
    NMA7 C 3 A_Wander
    TNT1 A 0 A_Look
    NMA7 D 3 A_Wander
    TNT1 A 0 A_Look
    NMA7 D 3 A_Wander
    TNT1 A 0 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineShotgunShotCounter", 1, "RetreatReload")
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_JumpIfHealthLower (30, "SeeScared")
    NMA7 AABBCCDD 3 A_Chase
    Loop
  SeeScared:
    TNT1 A 0 A_JumpIfHealthLower (30, 1)
	Goto See
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    NMA7 AABBCCDD 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA7 A 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA7 B 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA7 B 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA7 C 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA7 C 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA7 D 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto Overwatch
    NMA7 D 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, "SeeScared")
	Goto Overwatch
  RetreatReload:
    NMA7 AABBCCDD 2 A_Chase ("", "Reload")
    Goto RetreatReload
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineShotgunShotCounter", 1, "RetreatReload")
    TNT1 A 0 A_JumpIfCloser (160, "JumpBack")
    NMA7 EEEEEEEEEEEEEEEE 1 A_FaceTarget (10)
  MissileFuckYou:
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_MonsterRefire (0, "Overwatch")
    TNT1 A 0 A_GiveInventory ("DRPGMarineShotgunShotCounter", 1)
    TNT1 A 0 A_TakeInventory ("DRPGMarineJumpCounter", 4)
    NMA7 F 10 Bright A_CustomMissile ("DRPGMarineRocket", 40, 11, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    NMA7 E 8
    TNT1 A 0 A_JumpIfHealthLower (30, 2)
    TNT1 A 0 A_Jump (64, "RetreatReload")
    TNT1 A 0
    Goto Reload
  JumpBack:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineJumpCounter", 4, "MissileFuckYou")
    TNT1 A 0 A_GiveInventory ("DRPGMarineJumpCounter", 1)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 ThrustThing(angle*256/360+128, 8, 0, 0)
    TNT1 A 0 ThrustThingZ(0, 4, 0, 1)
    NMA7 EEEEEEEEE 1 A_FaceTarget (10)
    Goto Missile
  Overwatch:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineShotgunShotCounter", 1, "Reload")
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_Chase ("", "Missile", CHF_DONTMOVE)
    NMA7 E 4 A_FaceTarget
    TNT1 A 0 A_Chase ("", "Missile", CHF_DONTMOVE)
    NMA7 E 4 A_FaceTarget
    TNT1 A 0 A_JumpIfHealthLower (30, 2)
    TNT1 A 0 A_Jump (5, 2)
    TNT1 A 0
    Loop
    TNT1 A 0
    Goto See
  Reload:
    NMA7 E 45
    TNT1 A 0 A_PlaySound("drpgmarines/rocketlauncherload", 7)
    TNT1 A 0 A_TakeInventory ("DRPGMarineShotgunShotCounter", 1)
    NMA7 E 19
    Goto See
  Pain:
    NMA7 G 3
    NMA7 G 3 A_Pain
    Goto See
  Death:
    NMA7 H 4
    NMA7 I 4 A_Scream
    NMA7 J 4 A_NoBlocking
    NMA7 K 4
    TNT1 A 0 A_SpawnItem ("DRPGBodyThud", 0, 0, 0, 0)
    NMA7 LM 4
    NMA7 N -1
    Stop
  XDeath:
    NMA7 O 4
    NMA7 P 4 A_XScream
    NMA7 Q 4 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("DRPGGibThud", 0, 0, 0, 0)
    NMA7 RSTUV 4
    NMA7 W -1
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("DRPGCrushedMarine",0,0,0,0)
    Stop
  }
}

ACTOR DRPGMarineRocket : Rocket
{
  SeeSound "drpgmarines/rocketlauncher"
  DeathSound "drpgmarines/largeexp"
  Obituary "The UAC was forced to fire %o."
  DamageType "Fire"
  +MTHRUSPECIES
  +DONTREFLECT
  +SKYEXPLODE
  States
  {
  Spawn:
    MISL A 1 Bright
    Loop
  Death:
    TNT1 A 0 A_Quake (1, 8, 0, 1024, "")
    TNT1 A 0 A_Quake (7, 8, 0, 300, "")
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  }
}

Actor DRPGMarineJumpCounter : Inventory
{
inventory.maxamount 4
}
