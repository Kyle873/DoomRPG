// Map Event - Termonuclear Bomb
// --------------------------------------------------

actor DRPGThermonuclearBomb : SwitchableDecoration
{
    var int user_disarmed;
    
    Health 50
    Radius 16
    Height 32
    Mass 50
    
    +SOLID
    -SHOOTABLE
    +NOBLOOD
    +DONTBLAST
    +DONTDRAIN
    +PUSHABLE
    -USESPECIAL
    
    Activation THINGSPEC_ThingTargets | THINGSPEC_Activate | THINGSPEC_Switch
    
    States
    {
    Spawn:
        TNT1 A 0 A_PlaySound("nuke/idle", CHAN_6, 1.0, true, ATTN_NORM)
        TBOM AB 6 Bright
        Loop
    SpawnActive:
        TNT1 A 0 A_ChangeFlag("SHOOTABLE", true)
        TNT1 A 0 A_ChangeFlag("USESPECIAL", true)
        TNT1 A 0 A_PlaySound("nuke/idle", CHAN_6, 1.0, true, ATTN_NORM)
        TBOM AB 6 Bright
        Loop
    Inactive:
    Active:
        TNT1 A 0 A_JumpIf(user_disarmed, "Disarmed")
        TNT1 A 0 A_PlaySound("nuke/use", CHAN_5, 1.0, false, ATTN_NORM)
        TNT1 A 0 ACS_NamedExecuteAlways("ThermonuclearBombActivate", 0)
        Goto SpawnActive
    Disarmed:
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombPickup")
        Stop
    Death:
    Explode:
        TNT1 A 0 A_StopSound(CHAN_BODY)
        TNT1 A 0 A_StopSound(CHAN_6)
        TNT1 A 0 ACS_NamedExecuteAlways("ThermonuclearBombExplode", 0)
        TNT1 A 0 A_PlaySound("nuke/detonate", CHAN_5, 1.0, false, 0.0)
        // Hit everything in the room regardless of height
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion1", 0, 0, 1, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion2", 0, 0, 1, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion1", 0, 0, CeilingZ - Z - 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion2", 0, 0, CeilingZ - Z - 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion1", 0, 0, (CeilingZ - Z) / 2, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion2", 0, 0, (CeilingZ - Z) / 2, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        // If you're way too close, no number of walls will save you
        TNT1 A 0 A_RadiusGive ("DRPGThermonuclearBombDirectDamage1", 2048, RGF_PLAYERS | RGF_MONSTERS | RGF_OBJECTS | RGF_CUBE | RGF_NOSIGHT)
        TNT1 A 0 A_RadiusGive ("DRPGThermonuclearBombDirectDamage2", 2048, RGF_PLAYERS | RGF_MONSTERS | RGF_OBJECTS | RGF_CUBE | RGF_NOSIGHT)
        // Nuclear fallout
        TNT1 A 0 A_SpawnItemEx("DRPGBombRadiationArea", 0, 0, 16, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SetScale(4.0, 4.0)
        MISL BCD 6 Bright
        Stop
        Stop
    }
}

actor DRPGThermonuclearBombPickup : CustomInventory
{
    Tag "Thermonuclear Bomb"
    
    Inventory.MaxAmount 10
    Inventory.PickupMessage "Picked up the ThermoNuclear Bomb!"
    Inventory.PickupSound "nuke/pickup"
    Inventory.Icon "NUKE"
    
    +INVBAR
    
    States
    {
    Spawn:
        TBOM C -1
        Stop
    Use:
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombDropped")
        Stop
    }
}

actor DRPGThermonuclearBombDropped
{
    -SOLID
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_ChangeVelocity(8, 0, 0, CVF_RELATIVE)
        TNT1 A 0 A_ChangeFlag("SPECIAL", false)
        TNT1 A 0 A_PlaySound("nuke/countdown", CHAN_5, 1.0, false, 0.0)
        TBOM A 350 Bright
        TNT1 A 0 A_PlaySound("nuke/detonate", CHAN_5, 1.0, false, 0.0)
        // Hit everything in the room regardless of height
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion1", 0, 0, 1, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion2", 0, 0, 1, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion1", 0, 0, CeilingZ - Z - 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion2", 0, 0, CeilingZ - Z - 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion1", 0, 0, (CeilingZ - Z) / 2, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGThermonuclearBombExplosion2", 0, 0, (CeilingZ - Z) / 2, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        // If you're way too close, no number of walls will save you
        TNT1 A 0 A_RadiusGive("DRPGThermonuclearBombDirectDamage1", 2048, RGF_PLAYERS | RGF_MONSTERS | RGF_OBJECTS | RGF_CUBE | RGF_NOSIGHT)
        TNT1 A 0 A_RadiusGive("DRPGThermonuclearBombDirectDamage2", 2048, RGF_PLAYERS | RGF_MONSTERS | RGF_OBJECTS | RGF_CUBE | RGF_NOSIGHT)
        // Nuclear fallout
        TNT1 A 0 A_SpawnItemEx("DRPGBombRadiationArea", 0, 0, 16, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SetScale(4.0, 4.0)
        MISL BCD 6 Bright
        Stop
    }
}

actor DRPGThermonuclearBombExplosion1
{
    Radius 4
    Height 4
    +SHOOTABLE
    +NONSHOOTABLE
    Projectile
    Speed 0
    DamageType "Fire"
    
    States
    {
    Spawn:
        TNT1 A 0 A_Die
    Death:
        TNT1 A 2 A_Explode(4170, 16000, 0, true, 16000)
        Stop
    }
}

actor DRPGThermonuclearBombExplosion2 : DRPGThermonuclearBombExplosion1
{
    DamageType "Melee"
}

actor DRPGThermonuclearBombDirectDamage1 : CustomInventory
{
    Inventory.MaxAmount 0
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Pickup:
        TNT1 A 0 A_DamageSelf(12500, "Fire", DMSS_AFFECTARMOR | DMSS_NOFACTOR)
        Stop
    }
}

actor DRPGThermonuclearBombDirectDamage2 : DRPGThermonuclearBombDirectDamage1
{
    States
    {
    Pickup:
        TNT1 A 0 A_DamageSelf(12500, "Melee", DMSS_AFFECTARMOR | DMSS_NOFACTOR)
        Stop
    }
}

actor DRPGBombRadiationArea
{
    +NoBlockmap
    +NoSector
    +NoInteraction
    
    var int user_timer;
    
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("DRPGRadiationParticle", Random(-2048, 2048), Random(-2048, 2048), Random(-256, 256), 0, 0, 0, 0)
        TNT1 A 0 A_SetUserVar("user_timer", user_timer + 1)
        TNT1 A 1 Light("BOMBRADIATIONGLOW") A_JumpIf(user_timer >= 32, "DoDamage")
        Loop
    DoDamage:
        TNT1 A 0 A_SetUserVar("user_timer", 0)
        TNT1 A 0 A_RadiusGive("DRPGBombRadiationAreaDamage", 2048, RGF_PLAYERS | RGF_MONSTERS | RGF_OBJECTS | RGF_CUBE | RGF_NOSIGHT)
        Goto Spawn
    }
}

actor DRPGBombRadiationAreaDamage : CustomInventory
{
    Inventory.MaxAmount 0
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("RadiationDamage", 10)
        Stop
    DoNothing:
        TNT1 A 0
        Stop
    }
}

actor DRPGNukeKey : Inventory
{
    Inventory.Amount 1
    Inventory.MaxAmount 1
    Inventory.PickupMessage "You got a Thermonuclear Bomb key."
    Inventory.PickupSound "arena/keydrop"
    
    Scale 0.5
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        TNT1 A 0
        Stop
    }
}

actor DRPGNukeKey1 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY A 6
        NKEY A 6 Bright
        Goto Spawn+1
    }
}

actor DRPGNukeKey2 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY B 6
        NKEY B 6 Bright
        Goto Spawn+1
    }
}

actor DRPGNukeKey3 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY C 6
        NKEY C 6 Bright
        Goto Spawn+1
    }
}

actor DRPGNukeKey4 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY D 6
        NKEY D 6 Bright
        Goto Spawn+1
    }
}

actor DRPGNukeKey5 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY E 6
        NKEY E 6 Bright
        Goto Spawn+1
    }
}

actor DRPGNukeKey6 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY F 6
        NKEY F 6 Bright
        Goto Spawn+1
    }
}

actor DRPGNukeKey7 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY G 6
        NKEY G 6 Bright
        Goto Spawn+1
    }
}

actor DRPGNukeKey8 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY H 6
        NKEY H 6 Bright
        Goto Spawn+1
    }
}

actor DRPGNukeKey9 : DRPGNukeKey
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        NKEY I 6
        NKEY I 6 Bright
        Goto Spawn+1
    }
}

// Map Event - Low Power
// --------------------------------------------------

actor DRPGGenerator : SwitchableDecoration
{
    var int user_powered;
    
    Radius 24
    Height 80
    
    Scale 0.5
    
    -SOLID
    +USESPECIAL
    
    Activation THINGSPEC_ThingTargets | THINGSPEC_Activate | THINGSPEC_Switch
    
    States
    {
    Spawn:
    Inactive:
    Active:
        TNT1 A 0 A_JumpIf(user_powered, "PoweredUp")
        TNT1 A 0 ACS_NamedExecuteAlways("PowerGeneratorActivate", 0)
        GENR B -1
        Stop
    PoweredUp:
        GENR A 1 Bright
        Loop
    }
}

actor DRPGGeneratorCell : Inventory
{
    Scale 0.5
    
    -INVBAR
    
    Inventory.Amount 1
    Inventory.MaxAmount 1
    Inventory.PickupMessage "You got a Generator Power Cell."
    Inventory.PickupSound "items/vial"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
    SpawnLoop:
        GCEL ABCDEFGFEDCB 6 Bright
        Loop
    }
}

actor DRPGGeneratorCellDead : Inventory
{
    Scale 0.5
    
    -INVBAR
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a dead Generator Power Cell."
    Inventory.PickupSound "items/vial"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        GCEL H -1
        Stop
    }
}

actor DRPGLowPowerJunkSpawner : RandomSpawner
{
    DropItem "DRPGLowPowerJunkPile"         256 60
    DropItem "DRPGDeadDemonSpawner"         256 50
    DropItem "DRPGLowPowerRadiationBarrel"  256 40
    DropItem "DRPGDeadMarine"               256 30
    DropItem "DRPGLowPowerBrokenLamp"       256 20
    DropItem "DRPGDestroyedTurret"          256 20
    DropItem "DRPGCrate"                    256 10
	DropItem "DRPGExperimentTank"           256 5
    DropItem "DRPGModulePickup"             256 2
}

actor DRPGExperimentTank
{
	Height 56
	Radius 24
	Health 10
	Mass 4000
	
	+SHOOTABLE
	+NOBLOOD
	
	States
	{
	Spawn:
		ETNK A -1
		Stop
	Death:
		ETNK B 0 A_PlaySound("misc/breakglass", CHAN_BODY)
		ETNK B 0 A_Jump(256,
            "DropAug",
            "DropShield",
            "DropModule", "DropModule",
            "DropStim", "DropStim", "DropStim", "DropStim", "DropStim", 
            "DropMoney", "DropMoney", "DropMoney", "DropMoney", "DropMoney", "DropMoney", "DropMoney", "DropMoney", "DropMoney", "DropMoney")
	DropAug:
		ETNK B 0 A_SpawnItemEx("DRPGAugDropper", 0, 0, 32, Random(-5, 5), Random(-5, 5), Random(-5, 5), Random(0, 359))
		Goto DeathEnd
	DropShield:
		ETNK B 0 A_SpawnItemEx("DRPGShieldDropper", 0, 0, 32, Random(-5, 5), Random(-5, 5), Random(-5, 5), Random(0, 359))
		Goto DeathEnd
	DropModule:
		ETNK B 0 A_SpawnItemEx("DRPGModulePickup", 0, 0, 32, Random(-5, 5), Random(-5, 5), Random(-5, 5), Random(0, 359))
		Goto DeathEnd
	DropStim:
		ETNK BBBBBBBBBBBB 0 A_SpawnItemEx("DRPGVialDropper", 0, 0, 32, Random(-5, 5), Random(-5, 5), Random(-5, 5), Random(0, 359))
        ETNK BBB 0 A_SpawnItemEx("DRPGStimDropper", 0, 0, 32, Random(-5, 5), Random(-5, 5), Random(-5, 5), Random(0, 359))
		Goto DeathEnd
	DropMoney:
		ETNK BBBBBBBBBBBBBBBBBBBB 0 A_SpawnItemEx("DRPGMoneyDropper", 0, 0, 32, Random(-5, 5), Random(-5, 5), Random(-5, 5), Random(0, 359), 0, 128)
		Goto DeathEnd
	DeathEnd:
		ETNK B -1
		Stop
	}
}

actor DRPGDeadDemonSpawner : RandomSpawner
{
    DropItem "DeadCacodemon"
    DropItem "DeadDemon"
    DropItem "DeadDoomImp"
    DropItem "DeadShotgunGuy"
    DropItem "DeadZombieMan"
}

actor DRPGLowPowerJunkPile
{
    Radius 25
    Height 25
    Scale 0.5
    States
    {
    Spawn:
        JPIL AAAAAAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("DRPGLowPowerJunk", 0, 0, 0, FRandom(-32, 32), FRandom(-32, 32), FRandom(0, 32), FRandom(0, 360))
        JPIL A 0 A_SpawnItemEx("DRPGBatteryDropper", 0, 0, 0, FRandom(-32, 32), FRandom(-32, 32), FRandom(0, 32), FRandom(0, 360), 0, 200)
        JPIL A 0 A_SpawnItemEx("DRPGTurretPartCrate", 0, 0, 0, FRandom(-32, 32), FRandom(-32, 32), FRandom(0, 32), FRandom(0, 360), 0, 200)
        JPIL A 0 A_Jump(256, "Pile1", "Pile2")
    Pile1:
        JPIL A -1
        Stop
    Pile2:
        JPIL B -1
        Stop
    }
}

actor DRPGLowPowerJunk
{
    Radius 4
    Height 8
    Scale 1.25
    States
    {
    Spawn:
        JUNK A 0 NoDelay A_Jump(256, "Junk1", "Junk2", "Junk3", "Junk4", "Junk5", "Junk6","Junk7", "Junk8", "Junk9", "Junk10", "Junk11", "Junk12", "Junk13", "Junk14","Junk15", "Junk16", "Junk17", "Junk18", "Junk19", "Junk20")
    Junk1:
        JUNK A -1
        Stop
    Junk2:
        JUNK B -1
        Stop
    Junk3:
        JUNK C -1
        Stop
    Junk4:
        JUNK D -1
        Stop
    Junk5:
        JUNK E -1
        Stop
    Junk6:
        JUNK F -1
        Stop
    Junk7:
        JUNK G -1
        Stop
    Junk8:
        JUNK H -1
        Stop
    Junk9:
        JUNK I -1
        Stop
    Junk10:
        JUNK J -1
        Stop
    Junk11:
        JUNK K -1
        Stop
    Junk12:
        JUNK L -1
        Stop
    Junk13:
        JUNK M -1
        Stop
    Junk14:
        JUNK N -1
        Stop
    Junk15:
        JUNK O -1
        Stop
    Junk16:
        JUNK P -1
        Stop
    Junk17:
        JUNK Q -1
        Stop
    Junk18:
        JUNK R -1
        Stop
    Junk19:
        JUNK S -1
        Stop
    Junk20:
        JUNK T -1
        Stop
    }
}

actor DRPGLowPowerRadiationBarrel
{
    Radius 16
    Height 32
    Scale 1.25
    DamageType "Radiation"
    // Obituary "%o was barrelled over in the dark."
    
    +DONTSPLASH
    +NODAMAGETHRUST
    +NEVERTARGET
    
    States
    {
    Spawn:
        RBAR A 0 NoDelay A_Jump(256, "RadBarrel1", "RadBarrel2", "RadBarrel3")
    RadBarrel1:
        RBAR A -1
        Stop
    RadBarrel2:
        RBAR B -1
        Stop
    RadBarrel3:
        RBAR C 0 A_PlaySound("misc/radiation", CHAN_BODY, 0.5, true, ATTN_NORM)
        RBAR CCCCC 0 A_SpawnItemEx("DRPGRadiationParticle", FRandom(-32,32), FRandom(-32,32), FRandom(0,64), 0, 0, 0, 0, 0, 128)
        RBAR C 32 A_RadiusGive("DRPGRadiationBarrelDamage", 128, RGF_PLAYERS | RGF_MONSTERS | RGF_OBJECTS | RGF_CUBE)
        Loop
    }
}

actor DRPGRadiationBarrelDamage : CustomInventory
{
    Inventory.MaxAmount 0
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("RadiationDamage", 20)
        Stop
    DoNothing:
        TNT1 A 0
        Stop
    }
}

actor DRPGLowPowerBrokenLamp
{
    Obituary "%o charged into the room, full power, and saw the light."
    States
    {
    Spawn:
        BLMP A 0 A_PlaySound("shield/lightning", CHAN_BODY, 0.5, false, ATTN_NORM)
        BLMP AAAA 0 A_CustomRailgun(Random(5,30), 0, "None", "D0 E0 FF", RGF_SILENT | RGF_FULLBRIGHT, 0, 50, "DRPGElectricalPuff", 180, 90, 128, 1, 1.5, 0, "DRPGShieldLightningParticle", 12)
        BLMP A 2 Bright Light("BROKENLAMPZAP")
        BLMP A 0 A_SetTics(Random(1,50))
        Loop
    }
}

actor DRPGElectricalPuff : BulletPuff
{
    DamageType "Plasma"
    PainType "Lightning"
    
    +ALWAYSPUFF
    
    States
    {
    Spawn:
        TNT1 A 4
        RBAR AAA 0 A_SpawnItemEx("DRPGShieldStaticParticle", 0, 0, 0, FRandom(-1,1), FRandom(-1,1), FRandom(-1,1), 0)
        Stop
    }
}

// Map Event - Hell Unleashed
// --------------------------------------------------

actor DRPGPandoraBox : SwitchableDecoration
{
    Radius 16
    
    -SOLID
    +USESPECIAL
    
    Activation THINGSPEC_Activate
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SetTranslucent(0.0)
        PBOX A 1 Bright
        TNT1 A 0 A_FadeIn(0.01, FTF_CLAMP)
        TNT1 A 0 A_SpawnItemEx("DRPGBlingParticle", Random(-Radius, Radius), Random(-Radius, Radius), Random(-Height + 16, Height + 16))
        TNT1 A 0 A_Jump(Alpha >= 1.0, "Spawn2")
        Goto Spawn+1
    Spawn2:
        PBOX A -1 Bright
        Stop
    Active:
        TNT1 A 0 A_SetTranslucent(1.0)
        TNT1 A 0 A_PlaySound("pandorabox/whoosh", CHAN_BODY, 1.0, false, ATTN_NORM)
        PBOX BCDEFG 6 Bright
        TNT1 A 0 A_PlaySound("pandorabox/open1", CHAN_5, 1.0, false, ATTN_NORM)
        PBOX HI 6 Bright
        TNT1 A 0 A_PlaySound("pandorabox/open2", CHAN_7, 1.0, false, ATTN_NORM)
        PBOX JKL 6 Bright
        PBOX L 15 Bright
        TNT1 A 0 ACS_NamedExecuteAlways("HellUnleashedStart", 0)
        PBOX R 280 Bright A_PlaySound("pandorabox/doom", CHAN_5, 1.0, false, ATTN_NONE)
        TNT1 A 0 A_PlaySound("pandorabox/loop", CHAN_6, 1.0, true, 2.0)
    ActiveLoop:
        PBOX MNOPQPON 4 Bright
        Loop
    }
}

// Map Event - Environmental Hazard
// --------------------------------------------------

actor DRPGRadiationNeutralizer : SwitchableDecoration
{
    Radius 34
    Height 40
    
    -SOLID
    +USESPECIAL
    
    var int user_running;
    
    Activation THINGSPEC_ThingTargets | THINGSPEC_Activate | THINGSPEC_Switch
    
    States
    {
    Spawn:
        RADM A 1
        Loop
    Active:
        RADM A 0 ACS_NamedExecuteWithResult("EnvironmentalHazardRefillGenerator")
        RADM A 0 A_JumpIf(user_running, "StartupPuffPuffSounds")
        RADM A 0 Thing_Deactivate(0)
        Goto Spawn
    StartupPuffPuffSounds:
        RADM A 71 A_PlaySound("radiation/startmachine", CHAN_BODY, 1.0, false, 0.25)
    PuffPuffLoop:
        RADM A 0 A_JumpIf(user_running == 0, "ShutdownPuffPuffSounds")
        RADM A 1 A_PlaySound("radiation/loopmachine", CHAN_BODY, 1.0, true, 0.25)
        Loop
    Inactive:
    ShutdownPuffPuffSounds:
        RADM A 0 A_JumpIf(user_running == 0, "Spawn")
        RADM A 0 A_SetUserVar("user_running", 0)
        RADM A 81 A_PlaySound("radiation/stopmachine", CHAN_BODY, 1.0, false, 0.25)
        Goto Spawn
    }
}

actor DRPGNeutralizerFuel : Inventory
{
    -INVBAR
    
    Inventory.Amount 1
    Inventory.MaxAmount 5
    Inventory.PickupMessage "You got a Radiation Neutralizer fuel tank."
    Inventory.PickupSound "radiation/tankup"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        RADM B -1
        Stop
    }
}

// UB0R H4X 2 MAEK UR DOLLZ NOT 2DIE FR0M RAD1AT10N
Actor DRPGAntiRadiationForMyVoodooDolls : CustomInventory
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Pickup:
        TNT1 A 0 A_RadiusGive("DRPGVoodooDollAntiRadiation", 16383, RGF_VOODOO | RGF_CUBE | RGF_NOSIGHT)
        Stop
    }
}

Actor DRPGVoodooDollAntiRadiation : CustomInventory
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Pickup:
        TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0) // Epic Megahacks - Because literally nothing else would work.â„¢
        Stop
    }
}

// Map Event - Cracks in the Veil
// --------------------------------------------------

ACTOR DRPGTeleportFogEffectSilent
{
    RenderStyle Add
    Alpha 1.0
    
    +NOBLOCKMAP
    +NOTELEPORT
    +NOGRAVITY

    States
    {
    Spawn:
        TFOG ABABCDEFGHIJ 6 Bright
        Stop
    }
}

ACTOR DRPGTeleportFogEffectSilentDim : DRPGTeleportFogEffectSilent { Alpha 0.125 }

ACTOR DRPGTeleportCrackIn
{
    Radius 48
    Height 64
    RenderStyle Add
    Alpha 1.0
    
    +NOBLOCKMAP
    +NOGRAVITY
    +NOTELEPORT
    
    States
    {
    Spawn:
        TFOG A 0 Bright NoDelay A_PlaySound("veil/teleportrift", CHAN_BODY, 1.0, true, ATTN_NORM)
        TFOG A 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG A 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG B 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG B 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG C 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG C 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG D 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG D 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG B 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG B 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG C 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG C 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG D 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG D 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG B 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG B 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG C 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG C 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG D 0 Bright A_RadiusThrust(-1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG D 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilent", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        Loop
    }
}

ACTOR DRPGTeleportCrackOut
{
    Radius 48
    Height 64
    RenderStyle Add
    Alpha 0.125
    
    +NOBLOCKMAP
    +NOGRAVITY
    +NOTELEPORT
    
    States
    {
    Spawn:
        TFOG A 0 Bright NoDelay A_PlaySound("veil/teleportrift", CHAN_BODY, 1.0, true, ATTN_NORM)
        TFOG A 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG A 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG B 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG B 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG C 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG C 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG D 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG D 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG B 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG B 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG C 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG C 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG D 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG D 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG B 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG B 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG C 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG C 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        TFOG D 0 Bright A_RadiusThrust(1000, 256, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
        TFOG D 4 Bright A_SpawnItemEx("DRPGTeleportFogEffectSilentDim", FRandom(-8, 8), FRandom(-8, 8), FRandom(-8, 8), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 360))
        Loop
    }
}

// Map Event - 12 Hours 'til Doomsday
// --------------------------------------------------

Actor DRPGDoomsdayDelayedExplosion
{
    Radius 4
    Height 8
    Speed 20
    Damage 0
    Projectile
    DamageType "Fire"
    RenderStyle Add
    
    -NoGravity
    -NoBlockmap
    -Randomize
    +SkyExplode
    +ThruActors
    +DontSplash
    
    States
    {
    Spawn:
        TNT1 A 1
        Wait
    Death:
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_SetTics(Random(70,175))
        TNT1 A 0 A_PlaySound("drpgmarines/largeexp")
        TNT1 A 0 A_Quake (1, 8, 0, 1024, "")
        TNT1 A 0 A_Quake (6, 8, 0, 512, "")
        MEXP A 3 Bright A_Explode
        MEXP BCDEF 3 Bright
        Stop
    }
}

Actor DRPGDoomsdayMortarBlast
{
    Radius 4
    Height 8
    
    +NoInteraction
    
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAAAAAA 0 NoDelay A_CustomMissile("DRPGDoomsdayDelayedExplosion", 0, 0, FRandom(0, 360), CMF_AIMDIRECTION, FRandom(15, 90))
        Stop
    }
}

Actor DRPGDoomsdayFireBloodRainSpawner
{
    Radius 4
    Height 8
    
    +NoInteraction
    
    States
    {
    Spawn:
        TNT1 AAAAAAAA 0 NoDelay A_SpawnItemEx("DRPGDoomsdayFireRainCheck", Random(-2048,2048), Random(-2048,2048), 0, 2, 2, -2, 0, SXF_NOCHECKPOSITION, 128)
        TNT1 AAAAAAAA 0 A_SpawnItemEx("DRPGDoomsdayBloodRainCheck", Random(-2048,2048), Random(-2048,2048), 0, 4, 4, -2, 0, SXF_NOCHECKPOSITION, 128)
        Stop
    }
}

Actor DRPGDoomsdayBloodRainCheck
{
    Radius 4
    Height 8
    
    +NoInteraction
    
    States
    {
    Spawn:
        TNT1 A 1
        TNT1 A 0 A_JumpIf(!CallACS("CheckActorSky"), "Remove")
        TNT1 A 0 A_CheckSight("Remove")
        TNT1 A 0 A_SpawnItemEx("DRPGDoomsdayBloodRain", 0, 0, CeilingZ - Z - 10, 2, 2, -12, 0, SXF_NOCHECKPOSITION)
        Stop
    Remove:
        TNT1 A 0
        Stop
    }
}

actor DRPGDoomsdayBloodRain
{
    Scale 0.75
    Radius 4
    Height 8
    Mass 1
    
    +Corpse
    +NoGravity
    +NotOnAutomap
    +NoTeleport
    +NoBlockmap
    +MoveWithSector
    +DontSplash
    
    states
    {
    Spawn:
        FBLD A 0 A_SpawnItemEx("DRPGDoomsdayBloodRainTrail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        FBLD A 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD A 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD A 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD A 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD B 0 A_SpawnItemEx("DRPGDoomsdayBloodRainTrail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        FBLD B 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD B 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD B 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD B 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD C 0 A_SpawnItemEx("DRPGDoomsdayBloodRainTrail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        FBLD C 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD C 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD C 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD C 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD D 0 A_SpawnItemEx("DRPGDoomsdayBloodRainTrail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        FBLD D 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD D 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD D 1 A_JumpIf(waterlevel > 1, "Remove")
        FBLD D 1 A_JumpIf(waterlevel > 1, "Remove")
        loop
    Crash:
        FBLD A 0 A_Gravity
        FBLD A 0 A_PlaySound("gibbage/blood",0)
        FBLD A 0 A_Jump(64,4)
        FBLD A 0 A_Jump(128,4)
        FBLD A 0 A_Jump(192,4)
        FBLD A 0 A_Jump(255,4)
        BSPT A 175
        Goto FadeOutA
        BSPT B 175
        Goto FadeOutB
        BSPT C 175
        Goto FadeOutC
        BSPT D 175
        Goto FadeOutD
    FadeOutA:
        BSPT A 1 A_Fadeout(0.025)
        loop
    FadeOutB:
        BSPT B 1 A_Fadeout(0.025)
        loop
    FadeOutC:
        BSPT C 1 A_Fadeout(0.025)
        loop
    FadeOutD:
        BSPT D 1 A_Fadeout(0.025)
        loop
    Remove:
        TNT1 A 0 
        stop
    }
}

actor DRPGDoomsdayBloodRainTrail
{
    Scale 0.70
    Mass 1
    
    +NotOnAutomap
    +LowGravity
    +NoTeleport
    +NoBlockmap
    +DontSplash
    
    states
    {
    Spawn:
        BTRL A 4 A_JumpIf(waterlevel > 1, "Remove")
        BTRL B 4 A_JumpIf(waterlevel > 1, "Remove")
        BTRL C 4 A_JumpIf(waterlevel > 1, "Remove")
        BTRL D 4 A_JumpIf(waterlevel > 1, "Remove")
        stop
    Remove:
        TNT1 A 0 
        stop
    }
}

Actor DRPGDoomsdayFireRainCheck
{
    Radius 4
    Height 8
    
    +NoInteraction
    
    States
    {
    Spawn:
        TNT1 A 1
        TNT1 A 0 A_JumpIf(!CallACS("CheckActorSky"), "Remove")
        TNT1 A 0 A_CheckSight("Remove")
        TNT1 A 0 A_SpawnItemEx("DRPGDoomsdayFireRain", 0, 0, CeilingZ - Z - 10, 2, 2, -4, 0, SXF_NOCHECKPOSITION)
        Stop
    Remove:
        TNT1 A 0
        Stop
    }
}

actor DRPGDoomsdayFireRain
{
    RenderStyle Add
    Scale 0.5
    Radius 4
    Height 8
    Mass 1
    
    Projectile
    +NotOnAutomap
    +DontSplash
    
    states
    {
    Spawn:
        BRBA A 1 Bright A_SpawnItemEx("DRPGDoomsdayFireRainTrail", FRandom(-4,4), FRandom(-4,4), FRandom(-4,4), 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
        loop
    Death:
        BRBA ABCDEFGHIJ 2 Bright
        stop
    }
}

actor DRPGDoomsdayFireRainTrail
{
    RenderStyle Add
    Scale 0.25
    
    +NoInteraction
    
    states
    {
    Spawn:
        BRBA ABCDEFGHIJ 2 Bright
        stop
    }
}

// Map Event - Vicious Downpour
// --------------------------------------------------

Actor DRPGViciousRainSpawner
{
    Radius 4
    Height 8
    
    +NoInteraction
    
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("DRPGViciousRainCheck", Random(-1024,1024), Random(-1024,1024), 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DRPGViciousRainCheck", Random(-1024,1024), Random(-1024,1024), 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        Stop
    }
}

Actor DRPGViciousRainCheck
{
    Radius 4
    Height 8
    
    +NoInteraction
    
    States
    {
    Spawn:
        TNT1 A 1
        TNT1 A 0 A_JumpIf(!CallACS("CheckActorSky"), "Remove")
        TNT1 A 0 A_SpawnItemEx("DRPGViciousRain", 0, 0, CeilingZ - Z - 10, 1, 1, -18, 0, SXF_NOCHECKPOSITION)
        Stop
    Remove:
        TNT1 A 0
        Stop
    }
}

actor DRPGRainAmbiance : MapSpot
{
    States
    {
    Spawn:
        TNT1 A -1 NoDelay A_PlaySound("weather/rainloop", CHAN_BODY, 1.0, true, ATTN_NONE)
        Stop
    }
}

actor DRPGViciousRain
{
    RenderStyle Shaded
    StencilColor "3F 83 2F"
    XScale 0.005
    YScale 0.033
    Radius 2
    Height 2
    Mass 1
    Damage 0
    
    Projectile
    +NotOnAutomap
    +DontSplash
    
    States
    {
    Spawn:
        PTCL F -1
        stop
    Death:
        PTCL FFFF 0 A_SpawnItemEx("DRPGViciousRainParticle", 0, 0, 8, FRandom(-4,4), 0, FRandom(1,4), FRandom(0, 360))
        PTCL F 0 A_Gravity
        PTCL F 0 A_SetScale(0.05, 0.01)
        PTCL F 35 A_RadiusGive("DRPGAcidRainDamage", 64, RGF_PLAYERS | RGF_CUBE)
        PTCL F 1 A_FadeOut(1.0 / 70.0)
        wait
    }
}

actor DRPGViciousRainParticle
{
    RenderStyle Shaded
    StencilColor "3F 83 2F"
    Scale 0.0125
    
    Projectile
    -NoGravity
    +NotOnAutomap
    +DontSplash

    States
    {
    Spawn:
        PTCL F -1
        stop
    Death:
        PTCL F 0 A_Gravity
        PTCL F 0 A_SetScale(0.025, 0.005)
        PTCL F 1 A_FadeOut(1.0 / 70.0)
        wait
    }
}

actor DRPGAcidRainDamage : CustomInventory
{
    Inventory.MaxAmount 0
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CauseCorrosion")
        Stop
    DoNothing:
        TNT1 A 0
        Stop
    }
}

// Map Event - The Dark Zone
// --------------------------------------------------

Actor DRPGPurpleFloorMistSpawner
{
    Radius 4
    Height 8
    
    +NoInteraction
    
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("DRPGPurpleFloorMistCheck", Random(-1024,1024), Random(-1024,1024), 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DRPGPurpleFloorMistCheck", Random(-1024,1024), Random(-1024,1024), 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        Stop
    }
}

Actor DRPGPurpleFloorMistCheck
{
    Radius 4
    Height 8
    
    +NoInteraction
    
    States
    {
    Spawn:
        TNT1 A 1
        TNT1 A 0 A_CheckSight("Remove")
        TNT1 A 0 A_SpawnItemEx("DRPGPurpleFloorMist", 0, 0, FloorZ - Z + 16, FRandom(-0.5,0.5), FRandom(-0.5,0.5), FRandom(-0.1,0.1), 0, SXF_NOCHECKPOSITION)
        Stop
    Remove:
        TNT1 A 0
        Stop
    }
}

actor DRPGPurpleFloorMist
{
    RenderStyle Translucent
    Alpha 0
    XScale 0.075
    YScale 0.075
    Radius 2
    Height 2
    Mass 1
    Damage 0
    
    +NoInteraction
    +NotOnAutomap
    
    States
    {
    Spawn:
        PTCL G 0 NoDelay A_Jump(256, "Sprite1", "Sprite2", "Sprite3", "Sprite4")
        goto FadeIn
    Sprite1:
        PTCL G 0
        goto FadeIn
    Sprite2:
        PTCL H 0
        goto FadeIn
    Sprite3:
        PTCL I 0
        goto FadeIn
    Sprite4:
        PTCL J 0
        goto FadeIn
    FadeIn:
        PTCL "#" 1 A_FadeIn (0.005)
        PTCL "#" 0 A_JumpIf (alpha >= 0.5, "Death")
        loop
    Death:
        PTCL "#" 35
        PTCL "#" 1 A_FadeOut (0.005)
        wait
    }
}
