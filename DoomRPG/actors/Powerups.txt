// Invulnerability Sphere
Actor DRPGInvulnerabilityPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type Invulnerable
    Powerup.Color GoldMap
}

ACTOR DRPGInvulnerabilitySphere : CustomInventory
{
    Tag "Invulnerability Sphere - Invulnerability for 30 seconds"
    
    Inventory.PickupMessage "You got an Invulnerability Sphere!"
    Inventory.PickupSound "powerups/protect"
    
    -COUNTITEM
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        PINV ABCD 6 Bright
        Goto Spawn+1
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("GetStimPowerupActive"), "PickupFail")
        TNT1 A 0 A_GiveInventory("DRPGInvulnerabilityPower", 1)
        Stop
    PickupFail:
        TNT1 A 0
        Fail
    }
}

// Invulnerability Charge
Actor DRPGInvulnerabilityChargePower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type Invulnerable
    Powerup.Mode Reflective
    Powerup.Color GoldMap
}

actor DRPGInvulnerabilityCharge : CustomInventory
{
    +INVBAR

    Tag "Invulnerability Charge - Reflective Invulnerability for 30 seconds"

    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got an Invulnerability Charge!"
    Inventory.PickupSound "powerups/protect"
    Inventory.UseSound "powerups/protect"
    Inventory.Icon "CRG2"

    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        CRG2 ABC 3 Bright
        Goto Spawn+1
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIf(CallACS("GetStimPowerupActive"), "UseFail")
        TNT1 A 0 A_GiveInventory("DRPGInvulnerabilityChargePower", 1)
        Stop
    UseFail:
        TNT1 A 0
        Fail
    } 
}

// Invisibility Sphere
Actor DRPGInvisibilityPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type Invisibility
}

ACTOR DRPGBlurSphere : CustomInventory
{
    Tag "Blur Sphere - Partial Invisibility for 60 seconds"
    
    +VISIBILITYPULSE
    +FLOATBOB
    -COUNTITEM

    Inventory.PickupMessage "You got an Invisibility Sphere!"
    Inventory.PickupSound "powerups/invis"

    RenderStyle Translucent

    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        PINS ABCD 6 Bright
        Goto Spawn+1
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("GetStimPowerupActive"), "PickupFail")
        TNT1 A 0 A_GiveInventory("DRPGInvisibilityPower", 1)
        Stop
    PickupFail:
        TNT1 A 0
        Fail
    }
}   

// Invisibility Charge
Actor DRPGInvisibilityChargePower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type Invisibility
    Powerup.Mode Translucent
    Powerup.Duration -60
    Powerup.Strength 100
    +GHOST
}

actor DRPGInvisibilityCharge : CustomInventory
{
    +INVBAR

    Tag "Invisibility Charge - Complete Invisibility for 60 seconds"

    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got an Invisibility Charge!"
    Inventory.PickupSound "powerups/invis"
    Inventory.UseSound "powerups/invis"
    Inventory.Icon "CRG1"

    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        CRG1 ABC 3 Bright
        Goto Spawn+1
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIf(CallACS("GetStimPowerupActive"), "UseFail")
        TNT1 A 0 A_GiveInventory("DRPGInvisibilityChargePower", 1)
        Stop
    UseFail:
        TNT1 A 0
        Fail
    }
} 

// Time Sphere
actor DRPGTimeFreezer : PowerupGiver
{
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER
    
    Powerup.Type "TimeFreezer"
    Powerup.Color Black
    Powerup.Duration -10
}

actor DRPGTimeSphere : CustomInventory 20052
{
    Tag "Time Sphere - Stops Time for 10 Seconds"
    
    Scale 0.75  
    
    +INVBAR
    +FLOATBOB
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.Pickupmessage "You got a Time Sphere!"
    Inventory.PickupSound "powerups/time"
    Inventory.Icon "TIME"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        TIME ABCDCB 4 bright
        Goto Spawn+1
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIf(CallACS("GetStimPowerupActive"), "UseFail")
        TNT1 A 2 A_SpawnItemEx("DRPGTimeFreezerSound")
        TNT1 A 2 A_GiveInventory("DRPGTimeFreezer", 1)
        Stop
    UseFail:
        TNT1 A 0
        Fail
    }
}

// Regeneration Sphere
actor DRPGRegenSphere : CustomInventory 20053
{
    Tag "Regeneration Sphere - Quickly regenerates HP and EP"
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Regeneration Sphere!"
    Inventory.PickupSound "powerups/regen"
    Inventory.UseSound "powerups/regen"
    Inventory.Icon "REGN"

    +INVBAR
    +FLOATBOB
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        REGN ABCDCB 4 Bright
        Goto Spawn+1
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIf(CallACS("GetStimPowerupActive"), "UseFail")
        TNT1 A 0 ACS_NamedExecute("RegenBoost", 0)
        Stop
    UseFail:
        TNT1 A 0
        Fail
    }
}

// Radiation Suit
Actor DRPGRadSuitPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type IronFeet
}

ACTOR DRPGRadSuit : CustomInventory Replaces Radsuit
{
    Tag "Radiation Suit - Protects against Damaging Environments"

    +INVBAR
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Radiation Suit!"
    Inventory.PickupSound "powerups/suit"
    Inventory.UseSound "powerups/suit"
    Inventory.Icon "SUIT"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        SUIT A -1 Bright
        Stop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGRadSuitPower", 1)
        Stop
    }
}   

// IR Goggles
Actor DRPGInfraredPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type LightAmp
}

ACTOR DRPGInfrared : CustomInventory Replaces Infrared
{
    Tag "Night-Vision Goggles - See in the Dark"
    
    +INVBAR
    -COUNTITEM
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a pair of Infrared Goggles!"
    Inventory.PickupSound "powerups/light"
    Inventory.UseSound "powerups/light" 
    Inventory.Icon "PVIS"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        PVIS A 6 Bright
        PVIS B 6
        Goto Spawn+1
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGInfraredPower", 1)
        Stop
    }
}

// Computer Area Map and Scanner
actor DRPGAllMap : AllMap Replaces AllMap
{
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.FANCYPICKUPSOUND
    +INVENTORY.ALWAYSPICKUP
    
    Inventory.PickupMessage "You got a Computer Area Map!"
    Inventory.PickupSound "powerups/map"
    
    -COUNTITEM
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        PMAP ABCDCB 6 Bright
        Goto Spawn+1
    Use:
        TNT1 A 1 A_GiveInventory("DRPGAllMapRevealer", 1)
//      TNT1 A 1 A_GiveInventory("DRPGAllMapScanner", 1)
        Stop
    }
}

actor DRPGAllMapRevealer : MapRevealer {}

actor DRPGAllMapScanner : PowerupGiver
{
    +INVENTORY.AUTOACTIVATE

    Powerup.Type Scanner
    Powerup.Duration -0x7FFFFFFF
}

// Berserk Pack
actor DRPGBerserk : Berserk Replaces Berserk
{
    Tag "Berserk Pack - Full Health and Increased Melee Damage"
    
    +INVBAR
    -INVENTORY.AUTOACTIVATE
    -INVENTORY.ALWAYSPICKUP
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Berserk Pack!"
    Inventory.PickupSound "powerups/berserk"
    Inventory.UseSound "powerups/berserk"
    Inventory.Icon "PSTR"
    
    -COUNTITEM
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
		PSTR A -1
        Stop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 2)
        TNT1 A 0 ACS_NamedExecuteAlways("AddToxicity", 0, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("AMShieldCharge", 0)
        TNT1 A 0 ACS_NamedExecuteAlways("AddHealth", 0, 100, 100)
        TNT1 A 0 A_GiveInventory("PowerStrength")
        Stop
    }
}

// Wings
actor DRPGFlightPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    +INVENTORY.AUTOACTIVATE
    
    Powerup.Type Flight
    Powerup.Duration -0x7FFFFFFF
}

actor DRPGWings : CustomInventory 20054
{
    Tag "Wings - Allows Flight"
    
    +INVBAR
    +FLOATBOB

    Inventory.DefMaxAmount
    Inventory.PickupMessage "You got a pair of Wings!"
    Inventory.PickupSound "powerups/wings"
    Inventory.UseSound "powerups/wings"
    Inventory.Icon "WINGS"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        WING A -1 Bright
        Stop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGFlightPower", 1)
        Stop
    }
}

// Megasphere
ACTOR DRPGMegasphere : CustomInventory Replaces MegaSphere
{
    RenderStyle Add
    
    Inventory.PickupMessage "You got a Megasphere!"
    Inventory.PickupSound "powerups/megasphere"
    
    -COUNTITEM
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("ItemInit", 0)
        MEGA ABCD 6 BRIGHT
        Goto Spawn+1
    Pickup:
        TNT1 A 0 A_GiveInventory("DRPGSoulSphere3")
        TNT1 A 0 ACS_NamedExecuteAlways("AddEP", 0, 1000000)
        TNT1 A 0 ACS_NamedExecuteAlways("AddShield", 0, 1000000)
        Stop
    }
}   

// Immunity Crystal
actor DRPGImmunityCrystal : CustomInventory
{
    var int user_type;
	
    Inventory.Amount 1
    Inventory.MaxAmount 1
	Inventory.PickupMessage "Got an Immunity Crystal!"
	Inventory.PickupSound "powerups/crystal"
	Inventory.Icon "CRYS"
    
    +INVBAR
    +BRIGHT
    +THRUACTORS
    -SPECIAL
    
    RenderStyle Add
    
    Tag "Immunity Crystal"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SetUserVar("user_type", CallACS("ImmunityCrystalCheck"))
		TNT1 A 0 A_JumpIf(Abs(VelZ) <= 0.01, "FloorHit")
        CRYS ABCD 1
        Goto Spawn+1
    FloorHit:
		TNT1 A 0 A_ChangeFlag("SPECIAL", true)
		TNT1 A 0 A_ChangeFlag("THRUACTORS", false)
    Spawn2:
        TNT1 A 0 A_JumpIf(user_type == IC_RANDOM, "RandomPick")
        TNT1 A 0 A_JumpIf(user_type == IC_BULLET, "BulletMode")
        TNT1 A 0 A_JumpIf(user_type == IC_MELEE, "MeleeMode")
        TNT1 A 0 A_JumpIf(user_type == IC_FIRE, "FireMode")
        TNT1 A 0 A_JumpIf(user_type == IC_PLASMA, "PlasmaMode")
        Stop
    RandomPick:
        TNT1 A 0 A_Jump(256, "BulletMode", "MeleeMode", "FireMode", "PlasmaMode")
        Stop
    BulletMode:
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystalBeamBullet",
            Cos((CallACS("GetTimer") / 256.0) * 360.0) * 512,
            Sin((CallACS("GetTimer") / 256.0) * 360.0) * 512,
            0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystalBeamBullet",
            Cos((CallACS("GetTimer") / 256.0 + 17.5) * 360.0) * 512,
            Sin((CallACS("GetTimer") / 256.0 + 17.5) * 360.0) * 512,
            0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        CRYS A 1 A_RadiusGive("DRPGPowerImmunityBullet", 512.0, RGF_PLAYERS)
        Loop
    MeleeMode:
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystalBeamMelee",
            Cos((CallACS("GetTimer") / 256.0) * 360.0) * 512,
            Sin((CallACS("GetTimer") / 256.0) * 360.0) * 512,
            0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystalBeamMelee",
            Cos((CallACS("GetTimer") / 256.0 + 17.5) * 360.0) * 512,
            Sin((CallACS("GetTimer") / 256.0 + 17.5) * 360.0) * 512,
            0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        CRYS B 1 A_RadiusGive("DRPGPowerImmunityMelee", 512.0, RGF_PLAYERS)
        Loop
    FireMode:
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystalBeamFire",
            Cos((CallACS("GetTimer") / 256.0) * 360.0) * 512,
            Sin((CallACS("GetTimer") / 256.0) * 360.0) * 512,
            0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystalBeamFire",
            Cos((CallACS("GetTimer") / 256.0 + 17.5) * 360.0) * 512,
            Sin((CallACS("GetTimer") / 256.0 + 17.5) * 360.0) * 512,
            0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        CRYS C 1 A_RadiusGive("DRPGPowerImmunityFire", 512.0, RGF_PLAYERS)
        Loop
    PlasmaMode:
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystalBeamPlasma",
            Cos((CallACS("GetTimer") / 256.0) * 360.0) * 512,
            Sin((CallACS("GetTimer") / 256.0) * 360.0) * 512,
            0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystalBeamPlasma",
            Cos((CallACS("GetTimer") / 256.0 + 17.5) * 360.0) * 512,
            Sin((CallACS("GetTimer") / 256.0 + 17.5) * 360.0) * 512,
            0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        CRYS D 1 A_RadiusGive("DRPGPowerImmunityPlasma", 512.0, RGF_PLAYERS)
        Loop
    Use:
        TNT1 A 0 A_SpawnItemEx("DRPGImmunityCrystal", 0, 0, 40, 16, 0, 0, 0, SXF_SETMASTER)
        Stop
    }
}

actor DRPGPowerImmunityBullet : PowerProtection
{
    +INVENTORY.AUTOACTIVATE
    
    DamageFactor "Bullet", 0
	Powerup.Duration 35
}

actor DRPGPowerImmunityMelee : PowerProtection
{
    +INVENTORY.AUTOACTIVATE
    
    DamageFactor "Melee", 0
	Powerup.Duration 35
}

actor DRPGPowerImmunityFire : PowerProtection
{
    +INVENTORY.AUTOACTIVATE
    
    DamageFactor "Fire", 0
	Powerup.Duration 35
}

actor DRPGPowerImmunityPlasma : PowerProtection
{
    +INVENTORY.AUTOACTIVATE
    
    DamageFactor "Plasma", 0
	Powerup.Duration 35
}

