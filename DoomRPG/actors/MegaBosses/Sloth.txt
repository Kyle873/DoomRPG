/*==============================================================================
			-SLOTH MEGABOSS-
  ==============================================================================
	Big fat sleepy Mancubus, has cannons that fire radiation bombs.
	Sometimes falls asleep, is displeased if awoken.
	OH WHAT A SHAME YOUR SUPER SHIELD AND DEFENCE DOESN'T PROTECT YOU FROM TOXICITY
	...
	Oh wait you have a TX-BGONN never mind you win
	
	Anyway, the main strengths of this boss are huge amounts of regenerating health,
	and giving you huge amounts of toxicity with his attacks, bypassing your defenses.
*/

ACTOR DRPGMegabossSloth
{
  Health 38000
  Radius 68
  Height 84
  Mass 100000
  Speed 5
  FastSpeed 6
  PainChance 80
  PainThreshold 50
  MeleeRange 82
  Monster
  BloodColor "2e386c"
  DamageFactor "Crush", 10
  +FLOORCLIP
  +BOSSDEATH
  +DONTHARMSPECIES
  +NOTARGET
  +NOINFIGHTING
  +BOSS
  +AMBUSH
  +NEVERRESPAWN
  +NOBLOCKMONST
  +NOTIMEFREEZE
  +NOFEAR
  +SEEINVISIBLE
  +DONTMORPH
  +DONTBLAST
  +NOTELEOTHER
  +DONTRIP
  +NOTELEFRAG
  +DONTDRAIN
  +DONTGIB
  +DROPOFF
  +NEVERFAST
  Scale 1.8
  SeeSound "sloth/sight"
  PainSound "sloth/pain"
  DeathSound "sloth/death"
  ActiveSound "sloth/active"
  Species "Megaboss"
  Obituary "%o was crippled by slothfulness."
  Tag "Sloth"
  States
  {
  Spawn:
	TNT1 A 0 A_JumpIfInventory ("DRPGMegabossInitializedToken", 1, "GonnaSleepThePainAway")
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0, MF_NOAURA | MF_MEGABOSS)
	TNT1 A 0 A_GiveInventory ("DRPGMegabossInitializedToken", 1)
	SLOF C 1 A_LookEx (LOF_NOSEESOUND, 0, 0, 0, 360, "StartBattle")
	Goto Spawn+3
  GonnaSleepThePainAway:
	TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
	TNT1 A 0 A_GiveInventory ("DRPGSlothSleepingToken", 1)
	TNT1 A 0 A_PlaySound("sloth/snore", CHAN_VOICE, 0.4, 0, 0.6)
	SLOF U 10 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	SLOF U 10 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	SLOF U 10 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	SLOF U 10 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	SLOF U 10 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	SLOF U 10 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	SLOF U 10 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	Goto GonnaSleepThePainAway+2
  StartBattle:
	TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
	TNT1 A 0 A_FaceTarget
	SLOF G 8
	TNT1 A 0 A_PlaySound("sloth/sight", CHAN_VOICE, 1, 0, 0.2)
	SLOF IIIG 8
	TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
	Goto See
  See:
	TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
	SLOF AABB 4 A_Chase ("MissileLazy", "Missile")
	TNT1 A 0 A_Quake (1, 4, 0, 512, "")
	TNT1 A 0 A_PlaySound("megaboss/step", CHAN_BODY, 1, 0, 0.7)
	TNT1 A 0 A_Jump (70, "Lazy")
	SLOF CC 4 A_Chase ("MissileLazy", "Lazy", CHF_DONTMOVE)
	SLOF DDEE 4 A_Chase ("MissileLazy", "Missile")
	TNT1 A 0 A_Quake (1, 4, 0, 512, "")
	TNT1 A 0 A_PlaySound("megaboss/step", CHAN_BODY, 1, 0, 0.7)
	SLOF FF 4 A_Chase ("MissileLazy", "Missile", CHF_DONTMOVE)
	Loop
  Lazy:
	SLOF C 2
	TNT1 A 0 A_Jump (230, 2)
	TNT1 A 0 A_JumpIfTargetInLOS ("MissileLazy", 0)
	TNT1 A 0 A_Jump (2, "SiestaTiem")
	Loop
  SiestaTiem:
	TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
	TNT1 A 0 A_GiveInventory ("DRPGSlothSleepingToken", 1)
	TNT1 A 0 A_PlaySound("sloth/snore", CHAN_VOICE, 0.4, 0, 0.6)
	SLOF U 35 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	SLOF U 35 ACS_NamedExecuteAlways("HealMonster", 0, 1)
	Goto SiestaTiem+2
  OISHUTUP:
	TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
	TNT1 A 0 A_TakeInventory ("DRPGSlothSleepingToken", 1)
	TNT1 A 0 A_Pain
	SLOF CCUCUUC 4
	TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
	Goto WakeUpBarrage
  MissileLazy:
	TNT1 A 0 A_PlaySound("sloth/raiseguns", CHAN_VOICE)
	TNT1 A 0 A_JumpIfCloser(1024, 1)
	Goto SnipeCanister
	TNT1 A 0 A_Jump(200, 1)
	Goto RadiationStream
	SLOF G 5 A_FaceTarget
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomMissile("DRPGSmokeBlastCloud", 34, -64, 0)
	TNT1 A 0 A_CustomMissile("DRPGSmokeBlastCloud", 34, 56, 0)
	TNT1 A 0 A_CustomMissile("DRPGSlothCanister", 34, -64, 5, CMF_OFFSETPITCH, 15)
	TNT1 A 0 A_CustomMissile("DRPGSlothCanister", 34, 56, -5, CMF_OFFSETPITCH, 15)
	SLOF H 10 Bright
	SLOF IG 5 A_FaceTarget
	Goto Lazy
  Missile:
	TNT1 A 0 A_PlaySound("sloth/raiseguns", CHAN_VOICE)
	SLOF G 20 A_FaceTarget
	TNT1 A 0 A_JumpIfCloser(1024, 1)
	Goto SnipeCanister
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomMissile("DRPGSmokeBlastCloud", 34, -64, 0)
	TNT1 A 0 A_CustomMissile("DRPGSmokeBlastCloud", 34, 56, 0)
	TNT1 A 0 A_CustomMissile("DRPGSlothCanister", 34, -64, 5, CMF_OFFSETPITCH, 15)
	TNT1 A 0 A_CustomMissile("DRPGSlothCanister", 34, 56, -5, CMF_OFFSETPITCH, 15)
	SLOF H 10 Bright
	SLOF IG 5 A_FaceTarget
	Goto See
  WakeUpBarrage:
	TNT1 A 0 A_PlaySound("sloth/raiseguns", CHAN_VOICE)
	SLOF G 20 A_FaceTarget
	TNT1 A 0 A_JumpIfCloser(1024, 1)
	Goto SnipeCanister
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomMissile("DRPGSmokeBlastCloud", 34, -64, 0)
	TNT1 A 0 A_CustomMissile("DRPGSmokeBlastCloud", 34, 56, 0)
	TNT1 A 0 A_CustomMissile("DRPGSlothCanister", 34, -64, frandom(5,25), CMF_OFFSETPITCH, 15)
	TNT1 A 0 A_CustomMissile("DRPGSlothCanister", 34, 56, frandom(-5,-25), CMF_OFFSETPITCH, 15)
	SLOF H 10 Bright
	SLOF IG 5 A_FaceTarget
	TNT1 A 0 A_MonsterRefire(0, "See")
	Goto WakeUpBarrage+2
  SnipeCanister:
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomMissile("DRPGSmokeBlastCloud", 34, -64, 0)
	TNT1 A 0 A_CustomMissile("DRPGSmokeBlastCloud", 34, 56, 0)
	TNT1 A 0 A_CustomMissile("DRPGSlothCanister2", 34, -64, 0)
	TNT1 A 0 A_CustomMissile("DRPGSlothCanister2", 34, 56, 0)
	SLOF H 10 Bright
	SLOF IG 5 A_FaceTarget
	Goto See
  RadiationStream:
	TNT1 A 0 A_PlaySound("sloth/raiseguns", CHAN_VOICE)
	SLOF G 20 A_FaceTarget
	TNT1 A 0 A_GiveInventory ("DRPGSlothRadiationStreamCounter", 1)
	TNT1 A 0 A_PlaySound("sloth/radiationstream", CHAN_WEAPON, 1, 0, 0.7)
  RadiationStreamContinue:
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_CustomMissile("DRPGSlothRadiationCloud", 39, -64, frandom(5,15))
	TNT1 A 0 A_CustomMissile("DRPGSlothRadiationCloud", 39, 60, frandom(-5,-15))
	SLOF H 1 Bright
	TNT1 A 0 A_JumpIfInventory ("DRPGSlothRadiationStreamCounter", 1, "RadiationStreamContinue")
	TNT1 A 0 A_MonsterRefire (80, "RadiationStreamEnd")
	Goto RadiationStreamEnd
  RadiationStreamEnd:
	SLOF IG 5 A_FaceTarget
	Goto See
  Pain:
	TNT1 A 0 A_StopSound (CHAN_WEAPON)
	TNT1 A 0 A_JumpIfInventory ("DRPGSlothSleepingToken", 1, "OISHUTUP")
	SLOF J 3
	SLOF J 3 A_Pain
	Goto See
  Pain.Crush:
	TNT1 A 0 ACS_NamedExecuteAlways("TeleportMonster", 0)
	Goto StartBattle
  Death:
	SLOF J 6 A_Pain
	SLOF JJJJJJJJJJJJJJJJJ Random (1,6) A_CustomMissile ("DRPGMegabossKaboom", Random (25,66), 0, Random (0,359), CMF_ABSOLUTEPITCH, Random (12,90))
	TNT1 AAAAAAAAA 0 A_CustomMissile ("DRPGMegabossKaboom", Random (25,66), 0, Random (0,359), CMF_ABSOLUTEPITCH, Random (12,90))
	SLOF K 6
	SLOF L 6 A_Scream
	SLOF M 6 A_NoBlocking
	SLOF N 6
	SLOF OPQRS 6
	SLOF T -1
	Stop
  }
}

ACTOR DRPGSlothCanister
{
  Radius 6
  Height 8
  Speed 20
  Damage 8
  DamageType "Plasma"
  Projectile
  Scale 0.6
  Gravity 0.3
  +RANDOMIZE
  +THRUGHOST
  +SEEKERMISSILE
  +MTHRUSPECIES
  -NOGRAVITY
  Decal "Scorch"
  SeeSound "sloth/canisterfire"
  States
  {
  Spawn:
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterRingShockwave", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
	SLOC A 3 Bright A_JumpIfTracerCloser (128, "Explode")
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterRingShockwave", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
	SLOC B 3 Bright A_JumpIfTracerCloser (128, "Explode")
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterRingShockwave", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
	SLOC C 3 Bright A_JumpIfTracerCloser (128, "Explode")
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterRingShockwave", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
	SLOC D 3 Bright A_JumpIfTracerCloser (128, "Explode")
	Loop
  Explode:
	TNT1 A 0 A_Die
	Goto Death
  Death:
    // TOTALLY NOT STOLEN FROM TERM HUEHUEHUE
	TNT1 A 0 A_PlaySound ("sloth/canisterblast", CHAN_BODY, 1, 0, 0.6)
	TNT1 A 0 A_Stop
	TNT1 A 0 A_ChangeFlag ("NOGRAVITY", 1)
	TNT1 A 0 A_ChangeFlag ("FORCEXYBILLBOARD", 1)
	TNT1 A 0 A_SetTranslucent (0.95, 1)
	TNT1 A 0 A_SetScale (1.3)
	TNT1 A 0 A_Explode (128, 256, 0)
	TNT1 A 0 A_RadiusGive ("DRPGSlothToxicityBlastGiver", 128, RGF_PLAYERS, 1)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 0,   SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 10,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 20,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 30,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 40,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 50,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 60,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 70,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 80,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 90,  SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 100, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 110, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 120, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 130, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 140, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 150, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 160, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 170, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 180, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 190, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 200, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 210, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 220, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 230, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 240, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 250, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 260, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 270, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 280, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 290, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 300, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 310, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 320, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 330, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 340, SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("DRPGSlothCanisterShockwave", 16,0,0, 20,0,0, 350, SXF_NOCHECKPOSITION)
	GEXP A 4 Bright
	GEXP B 4 Bright
	GEXP C 3 Bright
	GEXP D 3 Bright
	GEXP E 2 Bright
	GEXP F 2 Bright
	Stop
	}
}

ACTOR DRPGSlothCanister2 : DRPGSlothCanister
{
  +NOGRAVITY
}

ACTOR DRPGSmokeBlastCloud
{
   Projectile 
   Radius 8
   Height 8
   Speed 2
   Scale 0.6
   Damage 0
   RENDERSTYLE ADD
   Alpha 0.95
   +NOCLIP
   +NOINTERACTION
   +NOTONAUTOMAP
   +FORCEXYBILLBOARD
   States
   {
   Spawn:
   SMK2 ABCDEFGHIJKLMNOPQ 1 A_SpawnItemEx("DRPGSmokeBlastCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   Stop
   }
}

ACTOR DRPGSlothRadiationCloud
{
   Projectile 
   Radius 8
   Height 8
   Speed 28
   Scale 0.6
   Damage 0
   RENDERSTYLE ADD
   Alpha 0.95
   +RIPPER
   +NOTONAUTOMAP
   +FORCEXYBILLBOARD
   +VISIBILITYPULSE
   +NODAMAGETHRUST
   +BLOODLESSIMPACT
   States
   {
   Spawn:
   SMK3 A 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 B 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 C 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 D 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 E 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 F 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 G 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 H 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 I 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 J 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 K 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 L 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 M 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 N 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 O 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 P 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   SMK3 Q 1 Bright A_RadiusGive ("DRPGSlothRadiationGiver2", 92, RGF_PLAYERS, 1)
   TNT1 A 0 A_SpawnItemEx("DRPGSlothRadiationCloudTrail", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
   Stop
   Death:
   TNT1 A 0
   Stop
   }
}

ACTOR DRPGSlothRadiationCloudTrail : DRPGSmokeBlastCloud
{
   Speed 0
   Scale 0.8
   Alpha 0.65
   States
   {
   Spawn:
   SMK3 ABCDEFGHIJKLMNOPQ 1 Bright A_SetScale(ScaleX+0.12)
   Stop
   }
}

ACTOR DRPGSmokeBlastCloudTrail : DRPGSmokeBlastCloud
{
   Speed 0
   Scale 0.8
   Alpha 0.65
   States
   {
   Spawn:
   SMK2 ABCDEFGHIJKLMNOPQ 1 A_SetScale(ScaleX+0.12)
   Stop
   }
}

actor DRPGSlothCanisterShockwave
{
    +NOINTERACTION
    +FORCEXYBILLBOARD
	+NOTONAUTOMAP
    RenderStyle Add
    Alpha 0.75
    States
    {
      Spawn:
        SLOC E 0 Bright A_FadeOut(0.1)
        TNT1 A 0 A_RadiusGive ("DRPGSlothRadiationGiver", 24, RGF_PLAYERS, 1)
        SLOC E 1 Bright A_SpawnItemEx("DRPGSlothCanisterShockwaveTrail")
        loop
    }
}


actor DRPGSlothCanisterShockwaveTrail
{
    +NOINTERACTION
    +FORCEXYBILLBOARD
	+NOTONAUTOMAP
    RenderStyle Add
    Alpha 0.75
    Scale 0.75
    States
    {
      Spawn:
        SLOC E 1 Bright A_FadeOut(0.15)
        loop
    }
}

actor DRPGSlothCanisterRingShockwave
{
    +NOINTERACTION
    +FORCEXYBILLBOARD
	+NOTONAUTOMAP
    RenderStyle Add
    Alpha 0.75
	Scale 0.6
    States
    {
      Spawn:
        SLOX ABCD 1 Bright
        Stop
    }
}

Actor DRPGSlothSleepingToken : Inventory
{
inventory.maxamount 1
}

ACTOR DRPGSlothRadiationStreamCounter : Powerup
{
	Powerup.Duration 145
}

ACTOR DRPGSlothRadiationGiver : CustomInventory
{
  -COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage ""
  Inventory.PickupSound ""
  States
  {
  Spawn:
    TNT1 A 1
    Stop
  Pickup:
    TNT1 A 0
    TNT1 A 0 A_Jump (115, 2)
    TNT1 A 0 ACS_NamedExecuteAlways("StatusEffect", 0, SE_RADIATION, random(3,10), random(1,3))
    Stop
    TNT1 A 0
    Stop
  }
}

ACTOR DRPGSlothRadiationGiver2 : CustomInventory
{
  -COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage ""
  Inventory.PickupSound ""
  States
  {
  Spawn:
    TNT1 A 1
    Stop
  Pickup:
    TNT1 A 0
    TNT1 A 0 A_Jump (200, 2)
    TNT1 A 0 ACS_NamedExecuteAlways("StatusEffect", 0, SE_RADIATION, random(3,10), random(1,3))
    Stop
    TNT1 A 0
    Stop
  }
}

ACTOR DRPGSlothToxicityBlastGiver : CustomInventory
{
  -COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage ""
  Inventory.PickupSound ""
  States
  {
  Spawn:
    TNT1 A 1
    Stop
  Pickup:
    TNT1 A 0
    TNT1 A 0 ACS_NamedExecuteAlways("AddToxicity", 0, random(1,8))
    TNT1 A 0 A_Jump (45, 2)
    TNT1 A 0 ACS_NamedExecuteAlways("StatusEffect", 0, SE_RADIATION, random(5,30), random(1,5))
    Stop
    TNT1 A 0
    Stop
  }
}
