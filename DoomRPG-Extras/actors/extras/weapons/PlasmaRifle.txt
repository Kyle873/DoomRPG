//***********
// PLASMAGUN
//***********

Actor DRPGPlasmaRifleExtras : DRPGPlasmaRifle replaces PlasmaRifle
{
    Inventory.PickupSound "weapons/plasma/pickup"
    Weapon.SlotNumber 6
    Weapon.SelectionOrder 10
    Weapon.UpSound "weapons/plasma/draw"
    
    States
    {
    Select:
        PLGE A 1 A_Raise
        Loop
    Deselect:
        PLGE A 0 A_Lower
        PLGE A 1 A_Lower
        Loop
    Ready:
        PLGE A 1 A_WeaponReady
        Loop
    Fire:
        PLGE A 0 A_GunFlash
    Fire1:
        PLGF A 3 Bright A_FireCustomMissile("DRPGPlasmaBallExtras",0,1)
        PLGE A 0 A_Refire("Fire2")
        Goto Fire3 + 2
    Fire2:
        PLGF B 3 Bright A_FireCustomMissile("DRPGPlasmaBallExtras",0,1)
        PLGE A 0 A_Refire("Fire3")
        Goto Fire3 + 2
    Fire3:
        PLGF C 3 Bright A_FireCustomMissile("DRPGPlasmaBallExtras",0,1)
        PLGE A 0 A_Refire("Fire1")
        PLGE A 0 A_JumpIfInventory("PowerInfiniteAmmo", 1, "Ready")
        PLGE A 0 A_JumpIfInventory("DRPGDarkBlueAuraInfiniteAmmo", 1, "Ready")
        PLGC ABC 1
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGE A 0 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,-25),random(2,-14))
        PLGC D 14 A_PlaySound("weapons/plasma/cool")
        PLGC CBA 1
        Goto Ready
    Flash:
        TNT1 A 3 A_Light2
        TNT1 A 3 A_Light1
        Goto LightDone
    }
}

Actor BlueFlarePlasma : Flare_General
{
    Alpha 0.9
    Scale 0.1
    
    States
    {
    Spawn:
        LENB AA 0 Bright NoDelay A_CustomRailgun(0, 0, "None", "SkyBlue", RGF_SILENT, 0, 50, "DRPGElectricalPuff", 180, 90, 16, 1, 1.0, 0, "DRPGShieldLightningParticle", -4)
        LENB A 2 Bright
        Stop
    }
}

Actor BlueFlarePlasmaTrail : BlueFlarePlasma
{
    Alpha 0.8
    Scale 0.065
    
    States
    {
    Spawn:
        LENB A 1 NoDelay Bright A_FadeOut(0.2)
        Loop
    }
}

Actor DRPGPlasmaBallExtras : FastProjectile // [KS] If you say so...
{
    SpawnID 51
    Radius 13
    Height 8
    Speed 25
    Damage 5
    Obituary "%o was melted by %k's plasma gun."
    MissileType "BlueFlarePlasmaTrail" //"BluePlasmatrail"
    MissileHeight 8
    DamageType "Electric"
    Decal "BluePlasmaSplat"
    RenderStyle Add
    Scale 0.7
    Alpha 0.8
    SeeSound "weapons/plasma/fire"
    DeathSound "weapons/plasmax"
    
    Projectile
    +FORCEXYBILLBOARD
    +THRUGHOST
    +MTHRUSPECIES

    States
    {
    Spawn:
        PLBS AB 1 Bright A_SpawnItem("BlueFlarePlasma")
        Loop
    Death:
        TNT1 A 1 A_SpawnItem("BluePlasmaBallExplosion",0,0,0)
        Stop
    }
}

Actor BluePlasmaBallExplosion : DebrisGeneral
{
    Translation "9:12=242:245", "112:127=192:207","152:159=200:207"
    Scale 0.2
    RenderStyle Add
    Alpha .6
    
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAA 0 A_CustomMissile("BluePlasmaPiece",0,0,random(-360,360),2,random(-50,50))
        TNT1 AAAAAA 0 A_CustomMissile("BluePlasmaShred",0,0,random(-360,360),2,random(-90,90))
        TNT1 A 0 Bright A_SpawnItem("RocketLSmoke")
        PEXP ABCDEFGHIJKLM 2 Bright
        Stop
    }
}

Actor BluePlasmaShred : DebrisGeneral
{
    RenderStyle Add
    Speed 8
    Scale 0.1
    Alpha 0.8

    +DONTSPLASH

    States
    {
    Spawn:
        PLSE ABCDE 1 Bright A_SpawnItem("BluePlasmaShredTrail")
        Stop
    }
}

Actor BluePlasmaShredTrail : BluePlasmaShred
{
    Speed 0
    Alpha 0.5
    
    States
    {
    Spawn:
        PLSE ABCDE 2 Bright
        Stop
    }
}

Actor BluePlasmaPiece : DebrisGeneral
{
    RenderStyle Add
    Alpha 1.0
    Scale 0.08
    Speed 6
    Gravity 0.8
    
    +DONTSPLASH
    -NOGRAVITY

    States
    {
    Spawn:
        PLBS A 1 Bright A_FadeOut(0.03)
        Loop
    }
}
